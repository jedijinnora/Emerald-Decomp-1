mapscripts Verdant_Forest_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: VerdantForest_OnTransition
}

//Object IDs
const WOMAN_ID = 6
const LITTLE_GIRL_ID = 7
const MANIAC_ID = 8

//TEMPs used:
//VAR_TEMP_2 = Temptation level
//VAR_TEMP_3 = Disturbance level

//VAR_TEMP_4 = Guardian species
//VAR_TEMP_5 = Guardian item
//VAR_TEMP_6 = Guardian level
//FLAG_TEMP_1 = Guardian defeated

//FLAG_TEMP_8 = Maniac intro
//FLAG_TEMP_9 = Maniac defeated


script VerdantForest_OnTransition {
    call(VerdantForest_EventScript_SetWild_0)
    setvar(VAR_TEMP_2, 0) 
    setvar(VAR_TEMP_3, 0) 
    random(10) //To determine which fairy is the Guardian
    setvar(VAR_TEMP_5, ITEM_SITRUS_BERRY)  //default item

    //set level based on badges
    switch(var(VAR_NUM_BADGES)) {
        //need at least cut & surf to access, so can assume at least 2 badges
        //will be funny if someone cheats and fights lvl 80 guardian with 0 or 1 badge
        case 2:
            setvar(VAR_TEMP_6, 40)
        case 3:
            setvar(VAR_TEMP_6, 50)
        case 4:
            setvar(VAR_TEMP_6, 60)
        case 5:
            setvar(VAR_TEMP_6, 65)
        case 6:
            setvar(VAR_TEMP_6, 70)
        case 7:
            setvar(VAR_TEMP_6, 75)
        default:
            setvar(VAR_TEMP_6, 80)
    }
    switch(var(VAR_RESULT)) {
        case 0: //WHIMSICOTT
            setvar(VAR_TEMP_4, SPECIES_WHIMSICOTT)
            setvar(VAR_TEMP_5, ITEM_FOCUS_SASH)
        case 1: //GRIMMSNARL
            setvar(VAR_TEMP_4, SPECIES_GRIMMSNARL)
        case 2: //HATTERENE
            setvar(VAR_TEMP_4, SPECIES_HATTERENE)
            setvar(VAR_TEMP_5, ITEM_TWISTED_SPOON)
        case 3: //RIBOMBEE
            setvar(VAR_TEMP_4, SPECIES_RIBOMBEE)
            setvar(VAR_TEMP_5, ITEM_SILVER_POWDER)
        case 4: //SHIINOTIC
            setvar(VAR_TEMP_4, SPECIES_SHIINOTIC)
            setvar(VAR_TEMP_5, ITEM_KEBIA_BERRY)
        case 5: //RAPIDASH-G
            setvar(VAR_TEMP_4, SPECIES_RAPIDASH_GALARIAN)
            setvar(VAR_TEMP_5, ITEM_LIECHI_BERRY)
        case 6: //AROMATISSE
            setvar(VAR_TEMP_4, SPECIES_AROMATISSE)
        case 7: //SLURPUFF
            setvar(VAR_TEMP_4, SPECIES_SLURPUFF)
        case 8: //GRANBULL
            setvar(VAR_TEMP_4, SPECIES_GRANBULL)
            setvar(VAR_TEMP_5, ITEM_WHITE_HERB)
        default: //FLORGES
            setvar(VAR_TEMP_4, SPECIES_FLORGES)
    }
}

script VerdantForest_EventScript_SetWild_0 {
    setvar(VAR_WILD_SET_VERDANT_FOREST, 0)
    return
}

script VerdantForest_EventScript_SetWild_1 {
    //only activate fairy encounter set if the guardian hasn't been defeated
    if(flag(FLAG_TEMP_1)) {
        setvar(VAR_WILD_SET_VERDANT_FOREST, 2)
    }
    setvar(VAR_WILD_SET_VERDANT_FOREST, 1)
    return
}

script VerdantForest_EventScript_Maniac {
    lockall
    applymovement(MANIAC_ID, Common_Movement_FacePlayer)
    goto_if_set(FLAG_TEMP_8, ResearchBattle)
    BugFairy:
    msgbox(format("I'm studying the relationship between the Bug and Fairy types."))
    waitmessage
    msgbox(format("It seems that PokÃ©mon with the Fairy type are resistant against Bug-type attacks."))
    waitmessage
    setflag(FLAG_TEMP_8)
    call_if_unset(FLAG_GIVEN_VERDANT_FOREST_TANGA_BERRY, VerdantForest_EventScript_Tanga)
    ResearchBattle:
    call_if_unset(FLAG_TEMP_9, VerdantForest_EventScript_ResearchBattle)
    releaseall
    end
}

script VerdantForest_EventScript_Tanga {
    giveitem(ITEM_TANGA_BERRY, 5)
    setflag(FLAG_GIVEN_VERDANT_FOREST_TANGA_BERRY)
    msgbox(format("I thought it should be the other way around, but these Berries aren't useful for Fairy-types."))
    waitmessage
    return
}

script VerdantForest_EventScript_ResearchBattle {
    goto_if_set(FLAG_TEMP_9, BugFairy)
    msgbox(format("Want to battle to help me further my research?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        msgbox(format("Hmph. Fine then. Go away."))
        releaseall
        end
    }
    //TODO: battle
    setflag(FLAG_TEMP_9)
    return
}

script VerdantForest_EventScript_LittleGirl {
    lockall
    applymovement(LITTLE_GIRL_ID, Common_Movement_FacePlayer)
    msgbox(format("Puff, puff."))
    waitmessage
    msgbox(format("The other kids - puff - said the fairies can't get you - puff - if you don't stop - puff - moving."))
    waitmessage
    releaseall
    end
}

script VerdantForest_EventScript_Woman {
    lockall
    applymovement(WOMAN_ID, Common_Movement_FacePlayer)
    msgbox(format("This forest is peaceful as long as you don't trespass too far."))
    waitmessage
    if (var(VAR_TEMP_2) > 2) {
        msgbox(format("Oh? You seem to have wandered further than you should..."))
        waitmessage
        if (var(VAR_TEMP_2) > 8) {
            //receiving warning increases disturbance
            addvar(VAR_TEMP_3, 3)
            msgbox(format("I can no longer help you. If you leave now misfortune might pass you by."))
            waitmessage
            releaseall
            end
        }
        msgbox(format("Why don't you have a drink of water to clear your head?"), MSGBOX_YESNO)
        if(var(VAR_RESULT) == NO) {
            //slightly increase temptation
            addvar(VAR_TEMP_2, 1)
            msgbox(format("Suit yourself."))
            waitmessage
        } else {
            //reduce temptation but increase disturbance
            setvar(VAR_TEMP_2, 0)
            addvar(VAR_TEMP_3, 4)
            playfanfare(MUS_OBTAIN_BERRY)
            waitfanfare
            msgbox(format("You take a drink of tepid water..."))
            waitmessage
            msgbox(format("The world seems a little less bright. You're a bit sweaty."))
            waitmessage
        }
    } else {
        msgbox(format("I love the flowers here, though I never pick any. They're best admired from a distance."))
        waitmessage
    }
    releaseall
    end
}

script VerdantForest_EventScript_Flowerbox {
    lockall
    msgbox(format("A crate full of pretty flowers gathered by the people of Verdanturf."))
    releaseall
    end
}

script VerdantForest_EventScript_FairyFlowerbox_Lower {
    lockall
    goto_if_set(FLAG_TEMP_1, VerdantForest_EventScript_BoxQuiet)
    msgbox(format("The crate is full of beautifully cut flowers which seem fresh and perfectly preserved."))
    if (var(VAR_TEMP_2) > 7) {
        msgbox(format("You are overcome with the urge to take some for yourself!"))
        goto(LowerTemptation)
    }
    msgbox(format("Will you take one of the blossoms?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        addvar(VAR_TEMP_2, 1)
        msgbox(format("Probably a wise choice."))
        call(Verdant_Forest_EventScript_Danger)
        releaseall
        end
    }
    LowerTemptation:
    msgbox(format("You grab for a flower, but it crumbles to dust in your hand."))
    playmoncry(SPECIES_FLOETTE, CRY_MODE_ENCOUNTER)
    waitmoncry
    msgbox(format("An angry fae leaps forth to challenge you!"))
    waitmessage
    subvar(VAR_TEMP_6, 5)
    setwildbattle(SPECIES_FLOETTE, VAR_TEMP_6)
    dowildbattle
    addvar(VAR_TEMP_6, 5)
    setvar(VAR_TEMP_2, 0)
    msgbox(format("The sweet smell of flowers is a little less cloying now."))
    addvar(VAR_TEMP_3, 1)
    releaseall
    end
}

script VerdantForest_EventScript_FairyFlowerbox_Upper {
    lockall
    goto_if_set(FLAG_TEMP_1, VerdantForest_EventScript_BoxQuiet)
    msgbox(format("The crate is full of artfully placed flowers which drip sweet pollen. Their intoxicating beauty tempts you."))
    if (var(VAR_TEMP_2) > 7) {
        msgbox(format("You are overcome with need!"))
        goto(UpperTemptation)
    }
    msgbox(format("Will you take one of the blossoms?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        addvar(VAR_TEMP_2, 3)
        msgbox(format("So pretty..."))
        call(Verdant_Forest_EventScript_Danger)
        releaseall
        end
    }
    UpperTemptation:
    msgbox(format("You dreamily raise a bloom to your face to inhale its sweet scent only to choke on a noxious cloud of ash."))
    playmoncry(SPECIES_FLOETTE, CRY_MODE_ENCOUNTER)
    waitmoncry
    msgbox(format("An enraged fae leaps forth to challenge you!"))
    waitmessage
    setflag(FLAG_SMART_WILD_AI)
    setwildbattle(SPECIES_FLOETTE, VAR_TEMP_6, ITEM_FAIRY_GEM)
    dowildbattle
    clearflag(FLAG_SMART_WILD_AI)
    setvar(VAR_TEMP_2, 0)
    msgbox(format("The sweet smell of flowers is a little less cloying now."))
    addvar(VAR_TEMP_3, 2)
    releaseall
    end
}

script Verdant_Forest_EventScript_Danger {
    if (var(VAR_TEMP_2) > 7) {
        msgbox(format("It is agonizing to wrench your gaze away from the flowers. Perhaps that's enough temptation for today."))
        waitmessage
    }
    return
}

script VerdantForest_EventScript_TreeQuiet {
    msgbox(format("With its protector vanquished, the Guardian's tree is silent."))
    msgbox(format("The forest watches you with quiet hatred."))
    releaseall
    end
}

script VerdantForest_EventScript_BoxQuiet {
    msgbox(format("The box is full of ash and dry twigs."))
    msgbox(format("The forest watches you with quiet hatred."))
    releaseall
    end
}

script VerdantForest_EventScript_FaeTree {
    lockall
    goto_if_set(FLAG_TEMP_1, VerdantForest_EventScript_TreeQuiet)
    msgbox(format("This tree is the center of the forest's strange energy."))
    waitmessage
    //play message based on disturbance level
    if (var(VAR_TEMP_3) > 11) {
        msgbox(format("The attention upon you is overwhelming!"))
        call(VerdantForest_EventScript_Guardian)
    } elif (var(VAR_TEMP_3) > 7) {
        msgbox(format("You feel cold despite the warm sun. The hair on the back of your neck tingles. Petals rustle and leaves fall. There is no breeze."))
        addvar(VAR_TEMP_3, 3)
        playmoncry(VAR_TEMP_4, CRY_MODE_ENCOUNTER)
    } elif (var(VAR_TEMP_3) > 3) {
        msgbox(format("You sense hostility from the flowers around you. Maybe it's best for you to go elsewhere."))
        addvar(VAR_TEMP_3, 2)
        playmoncry(SPECIES_FLOETTE, CRY_MODE_ENCOUNTER)
    } else {
        msgbox(format("Something is watching you curiously."))
        addvar(VAR_TEMP_3, 1)
        playmoncry(SPECIES_FLABEBE, CRY_MODE_ENCOUNTER)
    }
    releaseall
    end
}

script VerdantForest_EventScript_Guardian {
    msgbox(format("The guardian of the forest emerges to punish you!"))
    call_if_unset(FLAG_DEFEATED_ETERNAL_FLOETTE, VerdantForest_EventScript_Eternal)    
    GuardianFight:
    if(!flag(FLAG_DEFEATED_ETERNAL_FLOETTE)) {
        setflag(FLAG_NO_CATCHING) //no catching guardian until after floette defeated
    }
    playmoncry(VAR_TEMP_4, CRY_MODE_ENCOUNTER)
    waitmoncry
    settotemboost(B_POSITION_OPPONENT_LEFT, 1, 1, 0, 1, 1)
    setwildbattle(VAR_TEMP_4, VAR_TEMP_6, VAR_TEMP_5)
    setflag(FLAG_SMART_WILD_AI)
    dowildbattle
    clearflag(FLAG_SMART_WILD_AI)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if ((var(VAR_RESULT) == B_OUTCOME_RAN) || (var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED)) {
        msgbox(format("You cannot flee from the anger of the fae!"))
        setvar(VAR_TEMP_5, ITEM_SITRUS_BERRY) //Prevent repeated item theft
        goto(GuardianFight)
    }
    //set guardian defeated, reset temptation and disturbance
    setflag(FLAG_TEMP_1)
    setvar(VAR_TEMP_2, 0)
    setvar(VAR_TEMP_3, 0)
    //clear catching flag if set and regenerate hidden items
    clearflag(FLAG_NO_CATCHING)
    clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_HONEY)
    clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_RED_NECTAR)
    clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_YELLOW_NECTAR)
    clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_PINK_NECTAR)
    clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_PURPLE_NECTAR)
    random(5)
    switch(var(VAR_RESULT)) {
        case 0:
            clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_ELIXIR_1)
        case 1:
            clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_ELIXIR_2)
        case 2:
            clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_ELIXIR_1)
            clearflag(FLAG_HIDDEN_ITEM_VERDANT_FOREST_ELIXIR_2)
        default:
    }
    releaseall
    end
}

script VerdantForest_EventScript_Eternal {
    goto_if_lt(VAR_NUM_BADGES, 7, GuardianFight)
    setflag(FLAG_DEFEATED_ETERNAL_FLOETTE)
    playmoncry(SPECIES_FLOETTE_ETERNAL_FLOWER, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(40)
    msgbox(format("An eldritch wind is blowing!"))
    waitmessage
    playmoncry(SPECIES_FLOETTE_ETERNAL_FLOWER, CRY_MODE_ENCOUNTER)
    waitmoncry
    setwildbattle(SPECIES_FLOETTE_ETERNAL_FLOWER, 50, ITEM_PIXIE_PLATE)
    settotemboost(B_POSITION_OPPONENT_LEFT, 1, 1, 1, 1, 1)
    setflag(FLAG_SMART_WILD_AI)
    dowildbattle
    clearflag(FLAG_SMART_WILD_AI)
    releaseall
    end
}