raw `
EverGrandeCity_PhoebesRoom_MapScripts::
	map_script MAP_SCRIPT_ON_LOAD, EverGrandeCity_PhoebesRoom_OnLoad
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, EverGrandeCity_PhoebesRoom_OnWarp
	map_script MAP_SCRIPT_ON_FRAME_TABLE, EverGrandeCity_PhoebesRoom_OnFrame
	.byte 0

EverGrandeCity_PhoebesRoom_OnWarp:
	map_script_2 VAR_TEMP_1, 0, EverGrandeCity_PhoebesRoom_EventScript_PlayerTurnNorth
	.2byte 0

EverGrandeCity_PhoebesRoom_EventScript_PlayerTurnNorth::
	turnobject OBJ_EVENT_ID_PLAYER, DIR_NORTH
	end

EverGrandeCity_PhoebesRoom_OnFrame:
	map_script_2 VAR_ELITE_4_STATE, 1, EverGrandeCity_PhoebesRoom_EventScript_WalkInCloseDoor
	.2byte 0

EverGrandeCity_PhoebesRoom_EventScript_WalkInCloseDoor::
	lockall
	call PokemonLeague_EliteFour_EventScript_WalkInCloseDoor
	setvar VAR_ELITE_4_STATE, 2
	releaseall
	end

EverGrandeCity_PhoebesRoom_OnLoad:
	call_if_set FLAG_DEFEATED_ELITE_4_LUCY, EverGrandeCity_PhoebesRoom_EventScript_ResetAdvanceToNextRoom
	call_if_eq VAR_ELITE_4_STATE, 2, EverGrandeCity_PhoebesRoom_EventScript_CloseDoor
	end

EverGrandeCity_PhoebesRoom_EventScript_ResetAdvanceToNextRoom::
	call PokemonLeague_EliteFour_EventScript_ResetAdvanceToNextRoom
	return

EverGrandeCity_PhoebesRoom_EventScript_CloseDoor::
	call PokemonLeague_EliteFour_EventScript_CloseDoor
	return

EverGrandeCity_PhoebesRoom_EventScript_PostBattleSpeech::
	msgbox EverGrandeCity_PhoebesRoom_Text_PostBattleSpeech, MSGBOX_DEFAULT
	release
	end

EverGrandeCity_PhoebesRoom_EventScript_Defeated::
	setflag FLAG_DEFEATED_ELITE_4_LUCY
	call PokemonLeague_EliteFour_SetAdvanceToNextRoomMetatiles
	msgbox EverGrandeCity_PhoebesRoom_Text_PostBattleSpeech, MSGBOX_DEFAULT
	release
	end
`

script EverGrandeCity_PhoebesRoom_EventScript_Lucy {
	lock
	faceplayer
	goto_if_set(FLAG_DEFEATED_ELITE_4_LUCY, EverGrandeCity_PhoebesRoom_EventScript_PostBattleSpeech)
	playbgm(MUS_ENCOUNTER_ELITE_FOUR, FALSE)
	setflag(FLAG_USE_NEXT_LEVEL_CAP)
	msgbox(EverGrandeCity_PhoebesRoom_Text_IntroSpeech, MSGBOX_DEFAULT)
	random(2)
	if(var(VAR_DIFFICULTY_SETTING) == 1) {
		setvar(VAR_RESULT, 0)
	}
	switch(var(VAR_RESULT)) {
		case 0:
			trainerbattle_no_intro(TRAINER_LUCY_1, EverGrandeCity_PhoebesRoom_Text_Defeat)
		default:
			trainerbattle_no_intro(TRAINER_LUCY_2, EverGrandeCity_PhoebesRoom_Text_Defeat)
	}
	clearflag(FLAG_USE_NEXT_LEVEL_CAP)
	goto(EverGrandeCity_PhoebesRoom_EventScript_Defeated)
	end
}


text EverGrandeCity_PhoebesRoom_Text_IntroSpeech {
	"I am Lucy.\n"
	"I am the law here…\p"
	"You already know it, but to advance,\n"
	"you must defeat me…\p"
	"…I'm not one for idle chatter.\n"
	"Hurry… I hope you didn't use up all of\l"
	"your luck getting here…"
}
text EverGrandeCity_PhoebesRoom_Text_Defeat {
	"Urk…"
}
text EverGrandeCity_PhoebesRoom_Text_PostBattleSpeech {
	"Head on to the next room…\p"
	"That's all there is…\n"
	"Disappear already…"
}