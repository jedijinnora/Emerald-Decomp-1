mapscripts ArrivalFerry_Room_MapScripts {
    MAP_SCRIPT_ON_TRANSITION : ArrivalFerry_Room_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_CLEANUP_STATE, 0: ArrivalFerry_Room_EventScript_Init
    ] 
}

//Object IDs
const LOCALID_ROOMMATE = 1
const LOCALID_BAG = 2

//TEMPs used
//VAR_TEMP_1 and VAR_TEMP_2 for Player x,y determination

script ArrivalFerry_Room_OnTransition {
    if (var(VAR_CLEANUP_STATE) > 3) {
        setflag(FLAG_TEMP_11)
        setflag(FLAG_TEMP_12)
    }
    end
}

script ArrivalFerry_Room_EventScript_Init {
    lock
    delay(20)
    setrespawn(HEAL_LOCATION_EVER_GRANDE_CITY)
    setvar(VAR_EVER_GRANDE_CITY_STATE, 1)
    random(6)
    copyvar(VAR_BLOCKER_RIVAL_RANDOM, VAR_RESULT)
    playse(SE_PIN)
    applymovement(LOCALID_ROOMMATE, Common_Movement_ExclamationMark)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("Ah, the sleeper awakens!"))
    applymovement(LOCALID_ROOMMATE, ApproachPlayer)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("It feels like an eternity since you burst in here like a hurricane and fell straight asleep!"))
    msgbox(format("Consider yourself lucky that I am not such a boor to deny someone their beauty rest..."))
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("But this is ridiculous! Zut alors! Look at all this rubbish! Get up!"))
    msgbox(format("You tossed your belongings all over our room! I spent the whole trip shielding myself from your clumsy bombardment!"))
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("Who do you think is going to remedy the deplorable state of your side of this cabin, your mother?"))
    msgbox(format("I'm certain she is a woman of prodigious talent, but she is most definitely not present in our current locale!"))
    msgbox(format("I have my book and water and was ready to leave ages ago but here I am babying some layabout who thinks they are too important to respect other people's schedules!"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("What? It's too early for this?"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("Then let me speak in language even a buffoon like you shall comprehend:"))
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("Go pick up all of your shit!"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    closemessage
    applymovement(LOCALID_ROOMMATE, ApproachDoor)
    waitmovement(LOCALID_ROOMMATE)
    setobjectxyperm(LOCALID_ROOMMATE, 5, 8)
    setvar(VAR_CLEANUP_STATE, 1)
    release
    end
}

movement ApproachPlayer {
    walk_left * 4
    face_up
}

movement ApproachDoor {
    walk_right * 2
    walk_down * 3
    face_up
}

movement SeetheU {
    walk_in_place_fast_up * 2
}

movement SeetheD {
    walk_in_place_fast_down * 2
}

movement SeetheL {
    walk_in_place_fast_left * 2
}

movement SeetheR {
    walk_in_place_fast_right * 2
}

movement MoveUp {
    walk_up
}

movement Leave {
    walk_in_place_down
}

script ArrivalFerry_Room_EventScript_Madman {
    lock
    faceplayer
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_1) == 4) {
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
        waitmovement(OBJ_EVENT_ID_PLAYER)
    }

    call(ArrivalFerry_Room_EventScript_CheckCleanup)
    switch(var(VAR_CLEANUP_STATE)) {
        case 3:
            call(ArrivalFerry_Room_EventScript_Finished)
        case 2:
            call(ArrivalFerry_Room_EventScript_GoFind)
        default:
            call(ArrivalFerry_Room_EventScript_GoClean)
    }
    release
    end
}


    //0 = Starting state
    //1 = need to clean visible items
    //2 = all visible items picked up, necessary hidden items remain
    //3 = all necessary items gathered
    //4 = exit from room has occurred
script ArrivalFerry_Room_EventScript_CheckCleanup {
    if (flag(FLAG_ITEM_ARRIVAL_FERRY_ROOM_POTION) && flag(FLAG_ITEM_ARRIVAL_FERRY_ROOM_SUPER_POTION) && flag(FLAG_ITEM_ARRIVAL_FERRY_ROOM_ESCAPE_ROPE) && flag(FLAG_TEMP_12)) {
        setvar(VAR_CLEANUP_STATE, 2)
        if (flag(FLAG_SYS_B_DASH) && flag(FLAG_SYS_POKEDEX_GET)) {
            setvar(VAR_CLEANUP_STATE, 3)
        }
    }
    return
}

//cleanup 1
script ArrivalFerry_Room_EventScript_GoClean {
    lock
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveUp)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("Not good enough! We won't depart until you have retrieved all of your belongings!"))
    msgbox(format("Be grateful I am not making us carry out our trash. To be clear:"))
    msgbox(format("Show me some more cleaning!"))
    waitmessage
    release
    end
}

//cleanup 2
script ArrivalFerry_Room_EventScript_GoFind {
    lock
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveUp)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("What? You cannot think of anywhere else to clean?"))
    msgbox(format("I am astonished that a ludicrously imbecilic individual such as yourself is capable of putting one foot in front of the other."))
    msgbox(format("Make certain you retrieved your disgusting shoes from beneath the bed!"))
    msgbox(format("And be sure to check your trash can! A nincompoop like you likely threw away valuable items!"))
    waitmessage
    release
    end
}

//cleanup 3
script ArrivalFerry_Room_EventScript_Finished {
    lock
    applymovement(LOCALID_ROOMMATE, Common_Movement_FacePlayer)
    msgbox(format("That's much better, isn't it?"))
    msgbox(format("It's important to neaten up after yourself! Cleaning staff are people too, don't give them unnecessary work!"))
    msgbox(format("Anyways, we've arrived at Ever Grande City. You mumbled something about being an exchange student before you collapsed."))
    msgbox(format("I'm eager to stretch my legs! Let's make tracks!"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, Leave)
    playse(SE_EXIT)
    waitse
    waitmovement(LOCALID_ROOMMATE)
    setflag(FLAG_TEMP_11)
    removeobject(LOCALID_ROOMMATE)
    setvar(VAR_CLEANUP_STATE, 4)
    release
    end
}

script ArrivalFerry_Room_EventScript_UnderBed {
    lock
    msgbox(format("You check under the bed…"))
    if(flag(FLAG_SYS_B_DASH)) {
        msgbox(format("You find nothing but dust here."))
        release
        end
    }
    msgbox(format("Hey! Your collector's edition Pikachu plushie is down here!"))
    giveitem(ITEM_POKE_DOLL, 1)
    msgbox(format("You knock some dirt off your shoes and put them on."))
    playfanfare(MUS_OBTAIN_ITEM)
    setflag(FLAG_SYS_B_DASH)
    release
    end
}

script ArrivalFerry_Room_EventScript_TrashCan {
    lock
    if(flag(FLAG_SYS_POKEDEX_GET)) {
        msgbox(format("Just food wrappers and papers you don't need."))
        release
        end
    }
    msgbox(format("Yikes! It's your Pokédex!"))
    playfanfare(MUS_OBTAIN_ITEM)
    message(format("{PLAYER} picked up the Pokédex!"))
    waitfanfare
    closemessage
    setflag(FLAG_SYS_POKEDEX_GET)
    setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    special(SetUnlockedPokedexFlags)
    setflag(FLAG_SYS_NATIONAL_DEX)
    setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
    special(EnableNationalPokedex)
    release
    end
}

script ArrivalFerry_Room_EventScript_Bag {
    lock
    msgbox(format("It's the satchel with your starter Pokémon!"))

    StarterChoice:
    msgbox(format("What kind of starter did you have?"))
    dynmultichoice(0,0,TRUE,6,0,DYN_MULTICHOICE_CB_NONE, "Traditional Grass/Fire/Water", "Monotype Starter", "Other Starters")
    switch(var(VAR_RESULT)) {
        case 0:
            call(ArrivalFerry_Room_EventScript_GrassFireWater)
        case 1:
            call(ArrivalFerry_Room_EventScript_Monotype)
        case 2:
            call(ArrivalFerry_Room_EventScript_OtherStarter)
        default:
            goto(StarterChoice)
    }

    ShowStarterChoice:
    showmonpic(VAR_STARTER_CHOICE, 10, 3)
    msgbox(format("Did you pick this Pokémon?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        hidemonpic
        goto(StarterChoice)
    }

    //get starter
    hidemonpic
    playfanfare(MUS_OBTAIN_ITEM)
    givemon(VAR_STARTER_CHOICE, 5, ITEM_ORAN_BERRY)
    setmonmetlocation(0, 0xFF)
    waitfanfare 
    setflag(FLAG_TEMP_12)
    removeobject(LOCALID_BAG)
    setflag(FLAG_SYS_POKEMON_GET)

    //nickname
    msgbox(format("Did you nickname your starter?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }

    //ending message
    msgbox(format("With your first Pokémon back at your side you are ready to take on the world!"))
    msgbox(format("Assuming you've cleaned up enough that the madman will let you out the door…"))
    release
    end
}

script ArrivalFerry_Room_EventScript_GrassFireWater {
    msgbox(format("Er… Which region?"))
    dynmultipush("Kanto", 1)
    dynmultipush("Johto", 2)
    dynmultipush("Hoenn", 3)
    dynmultipush("Sinnoh", 4)
    dynmultipush("Unova", 5)
    dynmultipush("Kalos", 6)
    dynmultipush("Alola", 7)
    dynmultipush("Galar", 8)
    dynmultipush("Paldea", 9)
    dynmultistack(0,0,FALSE,6,FALSE,1,DYN_MULTICHOICE_CB_NONE)
    switch(var(VAR_RESULT)) {
        case 1:
            dynmultipush("Bulbasaur", SPECIES_BULBASAUR)
            dynmultipush("Charmander", SPECIES_CHARMANDER)
            dynmultipush("Squirtle", SPECIES_SQUIRTLE)
        case 2:
            dynmultipush("Chikorita", SPECIES_CHIKORITA)
            dynmultipush("Cyndaquil", SPECIES_CYNDAQUIL)
            dynmultipush("Totodile", SPECIES_TOTODILE)
        case 3:
            dynmultipush("Treecko", SPECIES_TREECKO)
            dynmultipush("Torchic", SPECIES_TORCHIC)
            dynmultipush("Mudkip", SPECIES_MUDKIP)
        case 4:
            dynmultipush("Turtwig", SPECIES_TURTWIG)
            dynmultipush("Chimchar", SPECIES_CHIMCHAR)
            dynmultipush("Piplup", SPECIES_PIPLUP)
        case 5:
            dynmultipush("Snivy", SPECIES_SNIVY)
            dynmultipush("Tepig", SPECIES_TEPIG)
            dynmultipush("Oshawott", SPECIES_OSHAWOTT)
        case 6:
            dynmultipush("Chespin", SPECIES_CHESPIN)
            dynmultipush("Fennekin", SPECIES_FENNEKIN)
            dynmultipush("Froakie", SPECIES_FROAKIE)
        case 7:
            dynmultipush("Rowlet", SPECIES_ROWLET)
            dynmultipush("Litten", SPECIES_LITTEN)
            dynmultipush("Popplio", SPECIES_POPPLIO)
        case 8:
            dynmultipush("Grookey", SPECIES_GROOKEY)
            dynmultipush("Scorbunny", SPECIES_SCORBUNNY)
            dynmultipush("Sobble", SPECIES_SOBBLE)
        case 9:
            dynmultipush("Sprigatito", SPECIES_SPRIGATITO)
            dynmultipush("Fuecoco", SPECIES_FUECOCO)
            dynmultipush("Quaxly", SPECIES_QUAXLY)
        default:
            goto(StarterChoice)
    }
    dynmultipush("Choose a different region", 0)
    msgbox(format("Which starter was it?"))
    dynmultistack(0,0,TRUE,6,TRUE,1,DYN_MULTICHOICE_CB_NONE)
    goto_if_eq(VAR_RESULT, 0, ArrivalFerry_Room_EventScript_GrassFireWater)
    copyvar(VAR_STARTER_CHOICE, VAR_RESULT)
    return
}

script ArrivalFerry_Room_EventScript_Monotype {
    msgbox(format("Um… Which type was it?"))
    dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Normal","Fighting","Flying","Poison","Ground","Rock","Bug","Ghost","Steel","Fire","Water","Grass","Electric","Psychic","Ice","Dragon","Dark","Fairy")
    switch(var(VAR_RESULT)) {
        case 0:
            dynmultipush("Rattata", SPECIES_RATTATA)
            dynmultipush("Meowth", SPECIES_MEOWTH)
            dynmultipush("Sentret", SPECIES_SENTRET)
            dynmultipush("Teddiursa", SPECIES_TEDDIURSA)
            dynmultipush("Slakoth", SPECIES_SLAKOTH)
            dynmultipush("Whismur", SPECIES_WHISMUR)
            dynmultipush("Bidoof", SPECIES_BIDOOF)
            dynmultipush("Lillipup", SPECIES_LILLIPUP)
            dynmultipush("Wooloo", SPECIES_WOOLOO)
            dynmultipush("Lechonk", SPECIES_LECHONK)
        case 1:
            dynmultipush("Mankey", SPECIES_MANKEY)
            dynmultipush("Machop", SPECIES_MACHOP)
            dynmultipush("Tyrogue", SPECIES_TYROGUE)
            dynmultipush("Makuhita", SPECIES_MAKUHITA)
            dynmultipush("Timburr", SPECIES_TIMBURR)
            dynmultipush("Crabrawler", SPECIES_CRABRAWLER)
            dynmultipush("Clobbopus", SPECIES_CLOBBOPUS)
        case 2:
            dynmultipush("Pidgey", SPECIES_PIDGEY)
            dynmultipush("Starly", SPECIES_STARLY)
            dynmultipush("Pidove", SPECIES_PIDOVE)
            dynmultipush("Fletchling", SPECIES_FLETCHLING)
            dynmultipush("Pikipek", SPECIES_PIKIPEK)
            dynmultipush("Rookidee", SPECIES_ROOKIDEE)
        case 3:
            dynmultipush("Ekans", SPECIES_EKANS)
            dynmultipush("Nidoran♀", SPECIES_NIDORAN_F)
            dynmultipush("Nidoran♂", SPECIES_NIDORAN_M)
            dynmultipush("Zubat", SPECIES_ZUBAT)
            dynmultipush("Grimer", SPECIES_GRIMER)
            dynmultipush("Koffing", SPECIES_KOFFING)
            dynmultipush("Wooper-P", SPECIES_WOOPER_PALDEAN)
            dynmultipush("Shroodle", SPECIES_SHROODLE)
        case 4:
            dynmultipush("Sandshrew", SPECIES_SANDSHREW)
            dynmultipush("Cubone", SPECIES_CUBONE)
            dynmultipush("Phanpy", SPECIES_PHANPY)
            dynmultipush("Trapinch", SPECIES_TRAPINCH)
            dynmultipush("Sandile", SPECIES_SANDILE)
            dynmultipush("Silicobra", SPECIES_SILICOBRA)
        case 5:
            dynmultipush("Geodude", SPECIES_GEODUDE)
            dynmultipush("Geodude-A", SPECIES_GEODUDE_ALOLAN)
            dynmultipush("Larvitar", SPECIES_LARVITAR)
            dynmultipush("Roggenrola", SPECIES_ROGGENROLA)
            dynmultipush("Rockruff", SPECIES_ROCKRUFF)
            dynmultipush("Rolycoly", SPECIES_ROLYCOLY)
            dynmultipush("Nacli", SPECIES_NACLI)
        case 6:
            dynmultipush("Yanma", SPECIES_YANMA)
            dynmultipush("Surskit", SPECIES_SURSKIT)
            dynmultipush("Sewaddle", SPECIES_SEWADDLE)
            dynmultipush("Venipede", SPECIES_VENIPEDE)
            dynmultipush("Grubbin", SPECIES_GRUBBIN)
            dynmultipush("Blipbug", SPECIES_BLIPBUG)
            dynmultipush("Nymble", SPECIES_NYMBLE)
        case 7:
            dynmultipush("Gastly", SPECIES_GASTLY)
            dynmultipush("Shuppet", SPECIES_SHUPPET)
            dynmultipush("Duskull", SPECIES_DUSKULL)
            dynmultipush("Yamask", SPECIES_YAMASK)
            dynmultipush("Litwick", SPECIES_LITWICK)
            dynmultipush("Phantump", SPECIES_PHANTUMP)
            dynmultipush("Greavard", SPECIES_GREAVARD)
        case 8:
            dynmultipush("Meowth-G", SPECIES_MEOWTH_GALARIAN)
            dynmultipush("Aron", SPECIES_ARON)
            dynmultipush("Beldum", SPECIES_BELDUM)
            dynmultipush("Klink", SPECIES_KLINK)
            dynmultipush("Honedge", SPECIES_HONEDGE)
            dynmultipush("Varoom", SPECIES_VAROOM)
        case 9:
            dynmultipush("Vulpix", SPECIES_VULPIX)
            dynmultipush("Growlithe", SPECIES_GROWLITHE)
            dynmultipush("Growlithe-H", SPECIES_GROWLITHE_HISUIAN)
            dynmultipush("Ponyta", SPECIES_PONYTA)
            dynmultipush("Magby", SPECIES_MAGBY)
            dynmultipush("Charcadet", SPECIES_CHARCADET)
        case 10:
            dynmultipush("Poliwag", SPECIES_POLIWAG)
            dynmultipush("Slowpoke", SPECIES_SLOWPOKE)
            dynmultipush("Horsea", SPECIES_HORSEA)
            dynmultipush("Wooper", SPECIES_WOOPER)
            dynmultipush("Lotad", SPECIES_LOTAD)
            dynmultipush("Tympole", SPECIES_TYMPOLE)
        case 11:
            dynmultipush("Oddish", SPECIES_ODDISH)
            dynmultipush("Bellsprout", SPECIES_BELLSPROUT)
            dynmultipush("Hoppip", SPECIES_HOPPIP)
            dynmultipush("Seedot", SPECIES_SEEDOT)
            dynmultipush("Budew", SPECIES_BUDEW)
            dynmultipush("Petilil", SPECIES_PETILIL)
            dynmultipush("Bounsweet", SPECIES_BOUNSWEET)
            dynmultipush("Smoliv", SPECIES_SMOLIV)
        case 12:
            dynmultipush("Magnemite", SPECIES_MAGNEMITE)
            dynmultipush("Mareep", SPECIES_MAREEP)
            dynmultipush("Elekid", SPECIES_ELEKID)
            dynmultipush("Shinx", SPECIES_SHINX)
            dynmultipush("Tynamo", SPECIES_TYNAMO)
            dynmultipush("Pawmi", SPECIES_PAWMI)
        case 13:
            dynmultipush("Abra", SPECIES_ABRA)
            dynmultipush("Slowpoke-G", SPECIES_SLOWPOKE_GALARIAN)
            dynmultipush("Ralts", SPECIES_RALTS)
            dynmultipush("Solosis", SPECIES_SOLOSIS)
            dynmultipush("Espurr", SPECIES_ESPURR)
            dynmultipush("Hatenna", SPECIES_HATENNA)
        case 14:
            dynmultipush("Sandshrew-A", SPECIES_SANDSHREW_ALOLAN)
            dynmultipush("Vulpix-A", SPECIES_VULPIX_ALOLAN)
            dynmultipush("Swinub", SPECIES_SWINUB)
            dynmultipush("Snorunt", SPECIES_SNORUNT)
            dynmultipush("Vanillite", SPECIES_VANILLITE)
            dynmultipush("Bergmite", SPECIES_BERGMITE)
        case 15:
            dynmultipush("Dratini", SPECIES_DRATINI)
            dynmultipush("Bagon", SPECIES_BAGON)
            dynmultipush("Gible", SPECIES_GIBLE)
            dynmultipush("Deino", SPECIES_DEINO)
            dynmultipush("Axew", SPECIES_AXEW)
            dynmultipush("Noibat", SPECIES_NOIBAT)
            dynmultipush("Applin", SPECIES_APPLIN)
        case 16:
            dynmultipush("Rattata-A", SPECIES_RATTATA_ALOLAN)
            dynmultipush("Houndour", SPECIES_HOUNDOUR)
            dynmultipush("Zigzagoon-G", SPECIES_ZIGZAGOON_GALARIAN)
            dynmultipush("Purrloin", SPECIES_PURRLOIN)
            dynmultipush("Impidimp", SPECIES_IMPIDIMP)
            dynmultipush("Maschiff", SPECIES_MASCHIFF)
        case 17:
            dynmultipush("Flabébé", SPECIES_FLABEBE)
            dynmultipush("Spritzee", SPECIES_SPRITZEE)
            dynmultipush("Swirlix", SPECIES_SWIRLIX)
            dynmultipush("Milcery", SPECIES_MILCERY)
            dynmultipush("Fidough", SPECIES_FIDOUGH)
            dynmultipush("Tinkatink", SPECIES_TINKATINK)
        default:
        goto(StarterChoice)
    }
    dynmultipush("Choose a different type", 0)
    msgbox(format("Which starter was it?"))
    dynmultistack(0,0,TRUE,6,TRUE,2,DYN_MULTICHOICE_CB_NONE)
    goto_if_eq(VAR_RESULT, 0, ArrivalFerry_Room_EventScript_Monotype)
    copyvar(VAR_STARTER_CHOICE, VAR_RESULT)
    return
}

script ArrivalFerry_Room_EventScript_OtherStarter {
    msgbox(format("Huh… Which one did you pick again?"))
    dynmultipush("Choose different starter type", 0)
    dynmultipush("Pichu", SPECIES_PICHU)
    dynmultipush("Pikachu", SPECIES_PIKACHU)
    dynmultipush("Eevee", SPECIES_EEVEE)
    dynmultipush("Ditto", SPECIES_DITTO)
    dynmultipush("Cleffa", SPECIES_CLEFFA)
    dynmultipush("Igglybuff", SPECIES_IGGLYBUFF)
    dynmultipush("Smeargle", SPECIES_SMEARGLE)
    dynmultipush("Tyrogue", SPECIES_TYROGUE)
    dynmultipush("Smoochum", SPECIES_SMOOCHUM)
    dynmultipush("Elekid", SPECIES_ELEKID)
    dynmultipush("Magby", SPECIES_MAGBY)
    dynmultipush("Shedinja", SPECIES_SHEDINJA)
    dynmultipush("Azurill", SPECIES_AZURILL)
    dynmultipush("Skitty", SPECIES_SKITTY)
    dynmultipush("Wynaut", SPECIES_WYNAUT)
    dynmultipush("Budew", SPECIES_BUDEW)
    dynmultipush("Chingling", SPECIES_CHINGLING)
    dynmultipush("Bonsly", SPECIES_BONSLY)
    dynmultipush("Mime Jr.", SPECIES_MIME_JR)
    dynmultipush("Happiny", SPECIES_HAPPINY)
    dynmultipush("Munchlax", SPECIES_MUNCHLAX)
    dynmultipush("Riolu", SPECIES_RIOLU)
    dynmultipush("Mantyke", SPECIES_MANTYKE)
    dynmultipush("Toxel", SPECIES_TOXEL)
    dynmultistack(0,0,TRUE,6,TRUE,2,DYN_MULTICHOICE_CB_NONE)
    goto_if_eq(VAR_RESULT, 0, StarterChoice)
    copyvar(VAR_STARTER_CHOICE, VAR_RESULT)
}