mapscripts ArrivalFerry_Room_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_CLEANUP_STATE, 0: ArrivalFerry_Room_EventScript_Init
    ] 
}

//Object IDs
const LOCALID_ROOMMATE = 1
const LOCALID_BAG = 2

//TEMPs used
//VAR_TEMP_1 and VAR_TEMP_2 for Player x,y determination

script ArrivalFerry_Room_EventScript_Init {
    lock
    delay(60)
    setrespawn(HEAL_LOCATION_EVER_GRANDE_CITY)
    setvar(VAR_PAINTER_STATE, 1)
    playse(SE_PIN)
    applymovement(LOCALID_ROOMMATE, Common_Movement_ExclamationMark)
    waitmovement(LOCALID_ROOMMATE)
    delay(15)
    msgbox(format("Ah, the sleeper awakens! It feels like an eternity since you burst in here like a hurricane and fell straight asleep!"))
    msgbox(format("Consider yourself lucky that I am not such a boor to deny someone their beauty sleep..."))
    applymovement(LOCALID_ROOMMATE, ApproachPlayer)
    waitmovement(LOCALID_ROOMMATE)
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("But this is ridiculous! Zut alors! Look at all this rubbish! Get up!"))
    msgbox(format("You tossed your belongings all over our room! I spent the whole trip from Sinnoh shielding myself from your clumsy bombardment!"))
    msgbox(format("Who do you think is going to remedy the deplorable state of your side of this cabin, your mother?"))
    msgbox(format("I'm certain she is a woman of prodigious talent, but she is most definitely not present in our current locale!"))
    msgbox(format("I have my book and water and was ready to leave hours ago but here I am babying some layabout who thinks they are too important to respect other people's schedules!"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("What? It's too early for this?"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("Then let me speak in language even a buffoon like you shall comprehend:"))
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    msgbox(format("PICK UP YOUR SHIT!"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    applymovement(LOCALID_ROOMMATE, ApproachDoor)
    waitmovement(LOCALID_ROOMMATE)
    setobjectxyperm(LOCALID_ROOMMATE, 5, 8)
    setvar(VAR_CLEANUP_STATE, 1)
    release
    end
}

movement ApproachPlayer {
    walk_left * 4
    face_up
}

movement ApproachDoor {
    walk_right * 2
    walk_down * 3
    face_up
}

movement SeetheU {
    walk_in_place_fast_up * 2
}

movement SeetheD {
    walk_in_place_fast_down * 2
}

movement SeetheL {
    walk_in_place_fast_left * 2
}

movement SeetheR {
    walk_in_place_fast_right * 2
}

movement MoveUp {
    walk_up
}

movement Leave {
    walk_in_place_down
}

script ArrivalFerry_Room_EventScript_Madman {
    lock
    applymovement(LOCALID_ROOMMATE, Common_Movement_FacePlayer)
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_1) == 4) {
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
        waitmovement(OBJ_EVENT_ID_PLAYER)
    }

    call(ArrivalFerry_Room_EventScript_CheckCleanup)
    switch(var(VAR_CLEANUP_STATE)) {
        case 3:
            call(ArrivalFerry_Room_EventScript_Finished)
        case 2:
            call(ArrivalFerry_Room_EventScript_GoFind)
        default:
            call(ArrivalFerry_Room_EventScript_GoClean)
    }
    release
    end
}


    //0 = Starting state
    //1 = need to clean visible items
    //2 = all visible items picked up, necessary hidden items remain
    //3 = all necessary items gathered
    //4 = exit from room has occurred
script ArrivalFerry_Room_EventScript_CheckCleanup {
    if (flag(FLAG_ITEM_ARRIVAL_FERRY_ROOM_POTION) && flag(FLAG_ITEM_ARRIVAL_FERRY_ROOM_SUPER_POTION) && flag(FLAG_ITEM_ARRIVAL_FERRY_ROOM_ESCAPE_ROPE) && flag(FLAG_HIDE_ARRIVAL_FERRY_ROOM_BAG)) {
        setvar(VAR_CLEANUP_STATE, 2)
        if (flag(FLAG_SYS_B_DASH) && flag(FLAG_SYS_POKEDEX_GET)) {
            setvar(VAR_CLEANUP_STATE, 3)
        }
    }
    return
}

//cleanup 1
script ArrivalFerry_Room_EventScript_GoClean {
    lock
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveUp)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("Not good enough! We won't depart until you have retrieved all of your belongings!"))
    msgbox(format("Be grateful I am not making us carry out our trash. To be clear:"))
    msgbox(format("LET'S SEE SOME MORE CLEANING!"))
    waitmessage
    release
    end
}

//cleanup 2
script ArrivalFerry_Room_EventScript_GoFind {
    lock
    applymovement(LOCALID_ROOMMATE, SeetheU)
    waitmovement(LOCALID_ROOMMATE)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveUp)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("What? You cannot think of anywhere else to clean?"))
    msgbox(format("I am astonished that a ludicrously imbecilic individual such as yourself has been sent to Hoenn by the eminent Professor Rowan."))
    msgbox(format("Make certain you retrieved your disgusting shoes from beneath the bed!"))
    msgbox(format("And be sure to check your trash can! A nincompoop like you likely threw away valuable items!"))
    waitmessage
    release
    end
}

//cleanup 3
script ArrivalFerry_Room_EventScript_Finished {
    lock
    applymovement(LOCALID_ROOMMATE, Common_Movement_FacePlayer)
    msgbox(format("That's much better, isn't it?"))
    msgbox(format("It's important to neaten up after yourself! Cleaning staff are people too, don't give them unnecessary work!"))
    msgbox(format("Anyways, we've arrived at Ever Grande City. You mumbled something about being an exchange student before you collapsed."))
    msgbox(format("I'm eager to stretch my legs! Let's make tracks!"))
    waitmessage
    applymovement(LOCALID_ROOMMATE, Leave)
    playse(SE_EXIT)
    waitse
    waitmovement(LOCALID_ROOMMATE)
    setflag(FLAG_HIDE_ARRIVAL_FERRY_ROOM_ROOMMATE)
    removeobject(LOCALID_ROOMMATE)
    setvar(VAR_CLEANUP_STATE, 4)
    release
    end
}

script ArrivalFerry_Room_EventScript_UnderBed {
    lock
    msgbox(format("You check under the bed..."))
    if(flag(FLAG_SYS_B_DASH)) {
        msgbox(format("You find nothing but dust here."))
        release
        end
    }
    msgbox(format("Hey! Your collector's edition Pikachu plushie is down here!"))
    giveitem(ITEM_POKE_DOLL, 1)
    msgbox(format("You knock some dirt off your shoes and put them on."))
    playfanfare(MUS_OBTAIN_ITEM)
    setflag(FLAG_SYS_B_DASH)
    release
    end
}

script ArrivalFerry_Room_EventScript_TrashCan {
    lock
    if(flag(FLAG_SYS_POKEDEX_GET)) {
        msgbox(format("Just food wrappers and papers you don't need."))
        release
        end
    }
    msgbox(format("Yikes! It's your Pokédex!"))
    playfanfare(MUS_OBTAIN_ITEM)
    message(format("{PLAYER} picked up the Pokédex!"))
    waitfanfare
    closemessage
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_ADVENTURE_STARTED)
    setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    special(SetUnlockedPokedexFlags)
    release
    end
}

script ArrivalFerry_Room_EventScript_Bag {
    lock
    msgbox(format("It's the satchel with your starter Pokémon!"))
    waitmessage

    //choose from selection
    StarterChoice:
    message(format("Er... Which one did you pick again?"))
    waitmessage
    multichoice(25, 5, MULTI_STARTER_CHOICE, 1)
    closemessage
    switch(var(VAR_RESULT)) {
        case 0: 
            setvar(VAR_STARTER_CHOICE, SPECIES_TURTWIG)
            bufferspeciesname(STR_VAR_1, SPECIES_TURTWIG)
        case 1:
            setvar(VAR_STARTER_CHOICE, SPECIES_CHIMCHAR)
            bufferspeciesname(STR_VAR_1, SPECIES_CHIMCHAR)  
        case 2:
            setvar(VAR_STARTER_CHOICE, SPECIES_PIPLUP)
            bufferspeciesname(STR_VAR_1, SPECIES_PIPLUP)
        default:
            msgbox(format("Something went wrong! 1"))
    }
    closemessage
    showmonpic(VAR_STARTER_CHOICE, 10, 3)
    msgbox(format("Did you pick {STR_VAR_1}?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        hidemonpic
        goto(StarterChoice)
    }

    //get starter
    hidemonpic
    playfanfare(MUS_OBTAIN_ITEM)
    givemon(VAR_STARTER_CHOICE, 5, ITEM_ORAN_BERRY)
    waitfanfare 
    setflag(FLAG_HIDE_ARRIVAL_FERRY_ROOM_BAG)
    removeobject(LOCALID_BAG)
    setflag(FLAG_SYS_POKEMON_GET)

    //nickname
    msgbox(format("Did you nickname your starter?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }

    //ending message
    msgbox(format("With your first Pokémon back at your side you are ready to take on the world!"))
    msgbox(format("...Assuming you've cleaned up enough that the madman will let you out the door."))
    release
    end
}