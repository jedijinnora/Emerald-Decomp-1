mapscripts Route119_WeatherInstitute_2F_MapScripts {}


script Route119_WeatherInstitute_2F_EventScript_CastformScientist {
	lock
	faceplayer
	goto_if_set(FLAG_RECEIVED_CASTFORM, CastformInfo)
	msgbox(format("Hello! I work with the Weather Institute's artificial Pokémon!"))
	msgbox(format("We're giving out Castform to suitable Trainers so we can gather more data on its use in battle."))
	msgbox(format("Would you be willing to take a Castform with you on your journey?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES) {
		goto(CastformGift)
	}
	msgbox(format("Come back if you change your mind."))
	release
	end

	CastformGift:
	msgbox(format("Excellent! Here you are!"))
	waitmessage
	setflag(FLAG_RECEIVED_CASTFORM)
	givemon(SPECIES_CASTFORM_NORMAL, 10)
	playfanfare(MUS_OBTAIN_ITEM)
	bufferspeciesname(STR_VAR_1, SPECIES_CASTFORM_NORMAL)
	msgbox("{PLAYER} received Castform!")
	waitmessage
	waitfanfare
	goto_if_eq(VAR_RESULT, MON_GIVEN_TO_PARTY, CastformToParty)
	goto_if_eq(VAR_RESULT, MON_GIVEN_TO_PC, CastformToPC)

	CastformToParty:
	msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES) {
		call(Common_EventScript_GetGiftMonPartySlot)
		call(Common_EventScript_NameReceivedPartyMon)
	}
	goto(CastformEnd)

	CastformToPC:
	msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES) {
		call(Common_EventScript_NameReceivedBoxMon)
	}
	call(Common_EventScript_TransferredToPC)
	CastformEnd:
	msgbox(format("Please take good care of Castform! Don't you dare leave them in the PC forever!"))
	release
	end

	CastformInfo:
	msgbox(format("Castform transforms into Sunny, Rainy, or Snowy Form depending on the weather."))
	msgbox(format("I'm still trying to modify Castform so they will change form during a sandstorm."))
	release
	end
}

script Route119_WeatherInstitute_2F_EventScript_BoobKid {
	lock
	faceplayer
	msgbox(format("Teehee…"))
	msgbox(format("Don't you think Castform looks a little… inappropriate?"))
	release
	end
}

script Route119_WeatherInstitute_2F_EventScript_TMScientist {
	lock
	faceplayer
	checkitem(ITEM_TM32_SANDSTORM)
	if(!var(VAR_RESULT)) {
		setflag(FLAG_TEMP_1)
		dynmultipush("TM32 Sandstorm", 0)
	}
	checkitem(ITEM_TM47_SUNNY_DAY)
	if(!var(VAR_RESULT)) {
		setflag(FLAG_TEMP_1)
		dynmultipush("TM47 Sunny Day", 1)
	}
	checkitem(ITEM_TM53_RAIN_DANCE)
	if(!var(VAR_RESULT)) {
		setflag(FLAG_TEMP_1)
		dynmultipush("TM53 Rain Dance", 2)
	}
	checkitem(ITEM_TM83_HAIL)
	if(!var(VAR_RESULT)) {
		setflag(FLAG_TEMP_1)
		dynmultipush("TM83 Hail", 3)
	}
	goto_if_unset(FLAG_TEMP_1, TMsGone)
	msgbox(format("I exchange colored Shards for weather TMs."))
	dynmultistack(0,0,FALSE,6,TRUE,0,DYN_MULTICHOICE_CB_NONE)
	switch(var(VAR_RESULT)) {
		case 0:
			checkitem(ITEM_YELLOW_SHARD, 5)
			if(var(VAR_RESULT)) {
				msgbox(format("Trade 5 Yellow Shards for TM32 Sandstorm?"), MSGBOX_YESNO)
			}
			if(var(VAR_RESULT)) {
				removeitem(ITEM_YELLOW_SHARD, 5)
				giveitem(ITEM_TM32_SANDSTORM)
				release
				end
			}
			msgbox(format("You don't have enough Yellow Shards."))
			release
			end
		case 1:
			checkitem(ITEM_RED_SHARD, 5)
			if(var(VAR_RESULT)) {
				msgbox(format("Trade 5 Red Shards for TM47 Sunny Day?"), MSGBOX_YESNO)
			}
			if(var(VAR_RESULT)) {
				removeitem(ITEM_RED_SHARD, 5)
				giveitem(ITEM_TM47_SUNNY_DAY)
				release
				end
			}
			msgbox(format("You don't have enough Red Shards."))
			release
			end
		case 2:
			checkitem(ITEM_BLUE_SHARD, 5)
			if(var(VAR_RESULT)) {
				msgbox(format("Trade 5 Blue Shards for TM53 Rain Dance?"), MSGBOX_YESNO)
			}
			if(var(VAR_RESULT)) {
				removeitem(ITEM_BLUE_SHARD, 5)
				giveitem(ITEM_TM53_RAIN_DANCE)
				release
				end
			}
			msgbox(format("You don't have enough Blue Shards."))
			release
			end
		case 3:
			checkitem(ITEM_GREEN_SHARD, 5)
			if(var(VAR_RESULT)) {
				msgbox(format("Trade 5 Green Shards for TM83 Hail?"), MSGBOX_YESNO)
			}
			if(var(VAR_RESULT)) {
				removeitem(ITEM_GREEN_SHARD, 5)
				giveitem(ITEM_TM83_HAIL)
				release
				end
			}
			msgbox(format("You don't have enough Green Shards."))
			release
			end
	}
	TMsGone:
	msgbox(format("I hope you're getting good use out of those TMs!"))
	release
	end
}

script Route119_WeatherInstitute_2F_EventScript_RockSeller {
	lock
	faceplayer
	goto_if_unset(FLAG_SYS_GAME_CLEAR, NoRockMart)
	msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemart(WeatherInstituteRockMart)
	msgbox(gText_PleaseComeAgain)
	release
	end

	NoRockMart:
	msgbox(format("I've heard that there are special rocks that a Pokémon could hold to make its weather effects last 8 turns instead of 5."))
	release
	end
}

mart WeatherInstituteRockMart {
	ITEM_DAMP_ROCK
	ITEM_HEAT_ROCK
	ITEM_SMOOTH_ROCK
	ITEM_ICY_ROCK
}