const LOCALID_STASH = 1

mapscripts SandsweptDesert_MapScripts {
    MAP_SCRIPT_ON_LOAD : SandsweptDesert_OnLoad
    MAP_SCRIPT_ON_TRANSITION : SandsweptDesert_OnTransition
}

script SandsweptDesert_OnLoad {
    call_if_unset(FLAG_DESERT_UNDERPASS_OPEN, SandsweptDesert_EventScript_CloseUnderpass)
    end
}

script SandsweptDesert_OnTransition {
    call(SandsweptDesert_EventScript_CheckSetSandstorm)
    call(SandsweptDesert_EventScript_TryMoveStash)
    call(Common_EventScript_DisableBPReward)
    end
}

script SandsweptDesert_EventScript_CheckSetSandstorm {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if(var(VAR_TEMP_1) > 19) {
        goto(Route111_EventScript_SandstormTrigger)
    }
    end
}

script SandsweptDesert_EventScript_TryMoveStash {
    goto_if_set(FLAG_HIDE_SECRET_STASH_SANDSWEPT_OASIS, Common_EventScript_NopReturn)
    switch(var(VAR_SECRET_STASH_STATE)) {
        case 0:
            setobjectxyperm(LOCALID_STASH, 19, 10)
        case 1:
            setobjectxyperm(LOCALID_STASH, 7, 12)
        case 2:
            setobjectxyperm(LOCALID_STASH, 14, 9)
        case 3:
            setobjectxyperm(LOCALID_STASH, 7, 7)
        default:
            setobjectxyperm(LOCALID_STASH, 25, 28)
    }
    return
}

script SandsweptDesert_EventScript_CloseUnderpass {
    setmetatile(24, 5, METATILE_General_RockWall_RockBase, TRUE)
	setmetatile(24, 6, METATILE_General_RockWall_SandBase, TRUE)
    return
}

script SandsweptDesert_EventScript_Stash {
    setvar(VAR_TEMP_D, ITEM_HP_UP)
    setvar(VAR_TEMP_E, ITEM_FIRE_STONE)
    setvar(VAR_TEMP_F, ITEM_SUN_STONE)
    call(Common_EventScript_SecretStash)
    end
}

script SandsweptDesert_EventScript_GogglesLady {
    lock
    faceplayer
    checkitem(ITEM_GO_GOGGLES)
    if (var(VAR_RESULT) == FALSE) {
        msgbox(format("How'd you get here without any Go-goggles?"))
        release
        end
    }
    msgbox(format("Those Go-goggles look rather fetching on you."))
    if(!flag(FLAG_RECEIVED_SAFETY_GOGGLES)) {
        msgbox(format("You should have some for your Pokémon as well!"))
        giveitem(ITEM_SAFETY_GOGGLES)
        setflag(FLAG_RECEIVED_SAFETY_GOGGLES)
    }
    release
    end
}

script SandsweptDesert_EventScript_LittleBoy {
    if(flag(FLAG_DESERT_UNDERPASS_OPEN)) {
        msgbox(format("I wanna explore the Desert Underpass, but I can't figure out how to get to the entrance from here!"), MSGBOX_NPC)
        end
    }
    msgbox(format("I wanna explore the Desert Underpass, but the entrance is missing!"), MSGBOX_NPC)
    end
}

script SandsweptDesert_EventScript_Neal {
	trainerbattle_single(TRAINER_NEAL, SandsweptDesert_Text_NealIntro, SandsweptDesert_Text_NealDefeat)
	msgbox(SandsweptDesert_Text_NealPostBattle, MSGBOX_AUTOCLOSE)
	call(SandsweptDesert_EventScript_TryNealRematch)
	release
	end
}
script SandsweptDesert_EventScript_TryNealRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_NEAL)
	setvar(VAR_0x8004, 1)
    if (defeated(TRAINER_RAOUL)) {
		msgbox(format("Include Raoul and make it a double battle?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			trainerbattle(TRAINER_BATTLE_SET_TRAINER_B, TRAINER_RAOUL, 0, NULL, SandsweptDesert_Text_RaoulDefeat)
			addvar(VAR_0x8004, 1)
		}
	}
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_NEAL, SandsweptDesert_Text_NealDefeat)
	special(UpdateRecentTrainers)
	return
}
text SandsweptDesert_Text_NealIntro {
	"Ho there, traveler!"
}
text SandsweptDesert_Text_NealDefeat {
	"You stumped me!"
}
text SandsweptDesert_Text_NealPostBattle {
	"How long can your Pokémon last in this\n"
	"sandstorm? I hope you brought\l"
    "protection!"
}

script SandsweptDesert_EventScript_Raoul {
	trainerbattle_single(TRAINER_RAOUL, SandsweptDesert_Text_RaoulIntro, SandsweptDesert_Text_RaoulDefeat)
	msgbox(SandsweptDesert_Text_RaoulPostBattle, MSGBOX_AUTOCLOSE)
	call(SandsweptDesert_EventScript_TryRaoulRematch)
	release
	end
}
script SandsweptDesert_EventScript_TryRaoulRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_RAOUL)
	setvar(VAR_0x8004, 1)
    if (defeated(TRAINER_NEAL)) {
		msgbox(format("Include Neal and make it a double battle?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			trainerbattle(TRAINER_BATTLE_SET_TRAINER_B, TRAINER_NEAL, 0, NULL, SandsweptDesert_Text_NealDefeat)
			addvar(VAR_0x8004, 1)
		}
	}
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_RAOUL, SandsweptDesert_Text_RaoulDefeat)
	special(UpdateRecentTrainers)
	return
}
text SandsweptDesert_Text_RaoulIntro {
	"My Pokémon trained with the dancers\n"
    "atop Wela Volcano!"
}
text SandsweptDesert_Text_RaoulDefeat {
	"Maybe we should have trained in the\n"
    "sandstorm instead of at the volcano."
}
text SandsweptDesert_Text_RaoulPostBattle {
	"A Thick Club doubles the Attack stat of\n"
	"Cubone or Marowak."
}

script SandsweptDesert_EventScript_Cleo {
	trainerbattle_single(TRAINER_CLEO, SandsweptDesert_Text_CleoIntro, SandsweptDesert_Text_CleoDefeat)
	msgbox(SandsweptDesert_Text_CleoPostBattle, MSGBOX_AUTOCLOSE)
	call(SandsweptDesert_EventScript_TryCleoRematch)
	release
	end
}
script SandsweptDesert_EventScript_TryCleoRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_CLEO)
	setvar(VAR_0x8004, 1)
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_CLEO, SandsweptDesert_Text_CleoDefeat)
	special(UpdateRecentTrainers)
	return
}
text SandsweptDesert_Text_CleoIntro {
	"A battle? Sure, but don't disturb the\n"
    "oasis."
}
text SandsweptDesert_Text_CleoDefeat {
	"What a mess!"
}
text SandsweptDesert_Text_CleoPostBattle {
	"I love this place. It's so pristine."
}