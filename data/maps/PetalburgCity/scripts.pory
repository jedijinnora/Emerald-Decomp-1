const LOCALID_BOY = 1

mapscripts PetalburgCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : PetalburgCity_OnTransition
}

script PetalburgCity_OnTransition {
	setflag(FLAG_VISITED_PETALBURG_CITY)
	call_if_unset(FLAG_DEFEATED_PETALBURG_GYM, PetalburgCity_EventScript_ResetGymDoors)
	end
}

script PetalburgCity_EventScript_ResetGymDoors {
	cleartrainerflag(TRAINER_RANDALL)
	cleartrainerflag(TRAINER_MARY)
	cleartrainerflag(TRAINER_PARKER)
	cleartrainerflag(TRAINER_ALEXIA)
	cleartrainerflag(TRAINER_GEORGE)
	cleartrainerflag(TRAINER_JODY)
	cleartrainerflag(TRAINER_BERKE)
	return
}

script PetalburgCity_EventScript_GymBoy {
	lock
	faceplayer
	msgbox(format("I'm not a good enough Trainer to take on the Gym Leader…"))
	msgbox(format("But I studied hard and I know everything about her team!"))
	msgbox(format("She likes to use strong Normal-type Pokémon."))
	switch(var(VAR_NUM_BADGES)) {
		case 0:
			//badge 1
			bufferspeciesname(STR_VAR_1, SPECIES_SPINDA)
			bufferspeciesname(STR_VAR_2, SPECIES_MUNCHLAX)
		case 1:
			//badge 2
			bufferspeciesname(STR_VAR_1, SPECIES_WIGGLYTUFF)
			bufferspeciesname(STR_VAR_2, SPECIES_LINOONE)
		case 2:
		case 3:
			//badge 3,4
			bufferspeciesname(STR_VAR_1, SPECIES_CINCCINO)
			bufferspeciesname(STR_VAR_2, SPECIES_HELIOLISK)
		case 4:
		case 5:
		case 6:
			//badge 5,6,7
			bufferspeciesname(STR_VAR_1, SPECIES_SMEARGLE)
			bufferspeciesname(STR_VAR_2, SPECIES_AMBIPOM)
		default:
			//badge 8 or rematch
			bufferspeciesname(STR_VAR_1, SPECIES_DUNSPARCE)
			bufferspeciesname(STR_VAR_2, SPECIES_HELIOLISK)
	}
	msgbox("Against you she'd lead {STR_VAR_1}\nand have {STR_VAR_2} as her ace.")
	release
	end
}

script PetalburgCity_EventScript_Boy{
	lock
	faceplayer
	msgbox(PetalburgCity_Text_WaterReflection, MSGBOX_DEFAULT)
	closemessage
	applymovement(LOCALID_BOY, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script PetalburgCity_EventScript_Gentleman {
	lock
	faceplayer
	//check if player has a shiny ducklett and reward somehow?
	msgbox(format("Petalburg is somewhat of an odd Ducklett when it comes to Hoenn's western cities."))
	msgbox(format("We have neither the industry of Rustboro or Mauville, and no marketplace like Slateport."))
	msgbox(format("That's not necessarily a bad thing."))
	msgbox(format("Vanilla is a perfectly wonderful flavor, after all."))
	release
	end
}

raw `
PetalburgCity_EventScript_GymSign::
	msgbox PetalburgCity_Text_GymSign, MSGBOX_SIGN
	end

PetalburgCity_EventScript_CitySign::
	msgbox PetalburgCity_Text_CitySign, MSGBOX_SIGN
	end

PetalburgCity_Text_WaterReflection:
	.string "My face is reflected in the water.\p"
	.string "It's a shining grin full of hope…\p"
	.string "Or it could be a look of somber silence\n"
	.string "struggling with fear…\p"
	.string "What do you see reflected in your face?$"

PetalburgCity_Text_GymSign:
	.string "Petalburg City Pokémon Gym\n"
	.string "Leader: Greta\l"
	.string "“The spirited karate queen!”$"

PetalburgCity_Text_CitySign:
	.string "Petalburg City\n"
	.string "“Where people mingle with nature.”$"
`
