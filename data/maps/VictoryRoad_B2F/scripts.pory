const LOCALID_GLICK = 1
const LOCALID_SHIRONA = 4

mapscripts VictoryRoad_B2F_MapScripts {
	MAP_SCRIPT_ON_LOAD : VictoryRoad_B2F_OnLoad
	MAP_SCRIPT_ON_TRANSITION : VictoryRoad_B2F_OnTransition
}

script VictoryRoad_B2F_OnLoad {
	call_if_set(FLAG_CHALLENGE_HM_NONE, VictoryRoad_B2F_EventScript_CreateSurfBypass)
}

script VictoryRoad_B2F_OnTransition {
	call_if_set(FLAG_CHALLENGE_HM_NONE, VictoryRoad_B2F_EventScript_MoveTrainers)
	call_if_set(FLAG_MET_SHIRONA, VictoryRoad_B2F_EventScript_MoveShirona)
	end
}

script VictoryRoad_B2F_EventScript_CreateSurfBypass {
	setmetatile(8, 21, 0x204, FALSE)
	setmetatile(8, 22, 0x204, FALSE)
	setmetatile(8, 23, 0x204, FALSE)
	setmetatile(40, 20, 0x211, FALSE)
	setmetatile(41, 20, 0x211, FALSE)
	setmetatile(42, 20, 0x211, FALSE)
	setmetatile(43, 20, 0x211, FALSE)
	return
}

script VictoryRoad_B2F_EventScript_MoveTrainers {
	setobjectxyperm(LOCALID_GLICK, 7, 24)
	return
}

script VictoryRoad_B2F_EventScript_MoveShirona {
	setobjectxyperm(LOCALID_SHIRONA, 56, 18)
	setobjectmovementtype(LOCALID_SHIRONA, MOVEMENT_TYPE_WANDER_AROUND)
	return
}

script VictoryRoad_B2F_EventScript_Arash {
	trainerbattle_single(TRAINER_ARASH, VictoryRoad_B2F_Text_JulieIntro, VictoryRoad_B2F_Text_JulieDefeat)
	msgbox(VictoryRoad_B2F_Text_JuliePostBattle, MSGBOX_AUTOCLOSE)
	end
}

script VictoryRoad_B2F_EventScript_Glick {
	trainerbattle_double(TRAINER_GLICK, VictoryRoad_B2F_Text_DianneIntro, VictoryRoad_B2F_Text_DianneDefeat, VictoryRoad_Text_NotEnoughMons)
	msgbox(VictoryRoad_B2F_Text_DiannePostBattle, MSGBOX_AUTOCLOSE)
	end
}

script VictoryRoad_B2F_EventScript_Grace {
	trainerbattle_single(TRAINER_GRACE, VictoryRoad_B2F_Text_GraceIntro, VictoryRoad_B2F_Text_GraceDefeat)
	msgbox(VictoryRoad_B2F_Text_GracePostBattle, MSGBOX_AUTOCLOSE)
	end
}

text VictoryRoad_B2F_Text_GraceIntro {
	format("Hoenn's been a nice region to travel through, but what I really love are Pokémon battles!")
}

text VictoryRoad_B2F_Text_GraceDefeat {
	"Congrats! You have a good team."
}

text VictoryRoad_B2F_Text_GracePostBattle {
	format("I grew up in Sinnoh! These days I travel around the world finding strong Trainers to battle.")
}

script VictoryRoad_B2F_EventScript_Hope {
	trainerbattle_single(TRAINER_HOPE, VictoryRoad_B2F_Text_CarolineIntro, VictoryRoad_B2F_Text_CarolineDefeat)
	msgbox(VictoryRoad_B2F_Text_CarolinePostBattle, MSGBOX_AUTOCLOSE)
	end
}

script VictoryRoad_B2F_EventScript_Shirona {
	lock
	goto_if_set(FLAG_MET_SHIRONA, VictoryRoad_B2F_EventScript_TryShironaRematch)
	msgbox(format("The ancient carvings found in the Hoenn region seem distantly related to those present in Celestica ruins in Sinnoh…"))
	msgbox(format("I wonder how much communication there was between our regions long ago?"))
	faceplayer
	msgbox(format("Oh, hello. Sorry for not noticing you sooner."))
	if(flag(FLAG_SYS_GAME_CLEAR)) {
		msgbox(format("You're Hoenn's new Champion? How nice."))
	} else {
		msgbox(format("You're here to challenge the Pokémon League? How nice."))
	}
	msgbox(format("I'm something of a strong Trainer myself."))
	trainerbattle_no_intro(TRAINER_SHIRONA, VictoryRoad_B2F_Text_ShironaDefeat)
	msgbox(format("That was excellent. Truly, an outstanding battle."))
	msgbox(format("Oh, I've finished examining these ruins. Here, I'll let you by."))
	waitmessage
	waitbuttonpress
	applymovement(LOCALID_SHIRONA, VictoryRoad_B2F_Movement_ShironaAway)
	waitmovement(0)
	setobjectmovementtype(LOCALID_SHIRONA, MOVEMENT_TYPE_FACE_UP)
	setflag(FLAG_MET_SHIRONA)
	release
	end
}

text VictoryRoad_B2F_Text_ShironaDefeat {
	"What a powerful Trainer!"
}

movement VictoryRoad_B2F_Movement_ShironaAway {
	walk_right
	walk_right
	walk_right
	walk_in_place_up
	step_end
}

script VictoryRoad_B2F_EventScript_TryShironaRematch {
	faceplayer
	msgbox(format("I enjoy Pokémon battles and exploring ancient history."))
	call_if_unset(FLAG_DAILY_SHIRONA_REMATCH, VictoryRoad_B2F_EventScript_ShironaRematch)
	msgbox(format("I'm thinking of traveling to Unova next. I've heard of some amazing underwater ruins near Undella Town."))
	release
	end
}

script VictoryRoad_B2F_EventScript_ShironaRematch {
	msgbox(format("Are you interested in battling again?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, Common_EventScript_NopReturn)
	cleartrainerflag(TRAINER_SHIRONA)
	trainerbattle_no_intro(TRAINER_SHIRONA, VictoryRoad_B2F_Text_ShironaDefeat)
	msgbox(format("Once again, you prove the skill and teamwork of your Pokémon. Here, I'd like you to have these."))
	giveitem(ITEM_HEART_SCALE, 3)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	setflag(FLAG_DAILY_SHIRONA_REMATCH)
	msgbox(format("I'll stick around a bit longer if I can keep finding tough Trainers like you!"))
	return
}

raw `
VictoryRoad_B2F_Text_CarolineIntro:
	.string "You must be getting a little tired.$"

VictoryRoad_B2F_Text_CarolineDefeat:
	.string "No signs of tiring at all!$"

VictoryRoad_B2F_Text_CarolinePostBattle:
	.string "Victory Road and the Pokémon League\n"
	.string "are long and grueling challenges.\l"
	.string "Beware of fatigue!$"

VictoryRoad_B2F_Text_JulieIntro:
	.string "You shouldn't get complacent just\n"
	.string "because you have a lot of Gym Badges.\p"
	.string "There's always going to be someone\n"
	.string "who's better than you!$"

VictoryRoad_B2F_Text_JulieDefeat:
	.string "You're better than me!$"

VictoryRoad_B2F_Text_JuliePostBattle:
	.string "Gaze on your collected Badges and\n"
	.string "remember the Trainers you've faced.$"

VictoryRoad_B2F_Text_DianneIntro:
	.string "The elite among the elite gather in\n"
	.string "this cave.\p"
	.string "How are you finding it?$"

VictoryRoad_B2F_Text_DianneDefeat:
	.string "Not rattled in the least bit!$"

VictoryRoad_B2F_Text_DiannePostBattle:
	.string "You do have guts! I like that!\n"
	.string "Keep it going!$"

`
