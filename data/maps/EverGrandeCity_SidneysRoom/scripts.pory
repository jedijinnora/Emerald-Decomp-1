raw `
EverGrandeCity_SidneysRoom_MapScripts::
	map_script MAP_SCRIPT_ON_LOAD, EverGrandeCity_SidneysRoom_OnLoad
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, EverGrandeCity_SidneysRoom_OnWarp
	map_script MAP_SCRIPT_ON_FRAME_TABLE, EverGrandeCity_SidneysRoom_OnFrame
	.byte 0

EverGrandeCity_SidneysRoom_OnLoad:
	call_if_set FLAG_DEFEATED_ELITE_4_BRANDON, EverGrandeCity_SidneysRoom_EventScript_ResetAdvanceToNextRoom
	call_if_eq VAR_ELITE_4_STATE, 1, EverGrandeCity_SidneysRoom_EventScript_CloseDoor
	end

EverGrandeCity_SidneysRoom_EventScript_ResetAdvanceToNextRoom::
	call PokemonLeague_EliteFour_EventScript_ResetAdvanceToNextRoom
	return

EverGrandeCity_SidneysRoom_EventScript_CloseDoor::
	call PokemonLeague_EliteFour_EventScript_CloseDoor
	return

EverGrandeCity_SidneysRoom_OnWarp:
	map_script_2 VAR_TEMP_1, 0, EverGrandeCity_SidneysRoom_EventScript_PlayerTurnNorth
	.2byte 0

EverGrandeCity_SidneysRoom_EventScript_PlayerTurnNorth::
	turnobject OBJ_EVENT_ID_PLAYER, DIR_NORTH
	end

EverGrandeCity_SidneysRoom_OnFrame:
	map_script_2 VAR_ELITE_4_STATE, 0, EverGrandeCity_SidneysRoom_EventScript_WalkInCloseDoor
	.2byte 0

EverGrandeCity_SidneysRoom_EventScript_WalkInCloseDoor::
	lockall
	call PokemonLeague_EliteFour_EventScript_WalkInCloseDoor
	setvar VAR_ELITE_4_STATE, 1
	releaseall
	end

EverGrandeCity_SidneysRoom_EventScript_PostBattleSpeech::
	msgbox EverGrandeCity_SidneysRoom_Text_PostBattleSpeech, MSGBOX_DEFAULT
	release
	end

EverGrandeCity_SidneysRoom_EventScript_Defeated::
	setflag FLAG_DEFEATED_ELITE_4_BRANDON
	call PokemonLeague_EliteFour_SetAdvanceToNextRoomMetatiles
	msgbox EverGrandeCity_SidneysRoom_Text_PostBattleSpeech, MSGBOX_DEFAULT
	release
	end
`

script EverGrandeCity_SidneysRoom_EventScript_Brandon {
	lock
	faceplayer
	goto_if_set(FLAG_DEFEATED_ELITE_4_BRANDON, EverGrandeCity_SidneysRoom_EventScript_PostBattleSpeech)
	playbgm(MUS_ENCOUNTER_ELITE_FOUR, FALSE)
	setflag(FLAG_USE_NEXT_LEVEL_CAP)
	msgbox(EverGrandeCity_SidneysRoom_Text_IntroSpeech, MSGBOX_DEFAULT)
	random(2)
	if(var(VAR_DIFFICULTY_SETTING) == 1) {
		setvar(VAR_RESULT, 0)
	}
	switch(var(VAR_RESULT)) {
		case 0:
			trainerbattle_no_intro(TRAINER_BRANDON_1, EverGrandeCity_SidneysRoom_Text_Defeat)
		default:
			trainerbattle_no_intro(TRAINER_BRANDON_2, EverGrandeCity_SidneysRoom_Text_Defeat)
	}
	clearflag(FLAG_USE_NEXT_LEVEL_CAP)
	goto(EverGrandeCity_SidneysRoom_EventScript_Defeated)
	end
}

text EverGrandeCity_SidneysRoom_Text_IntroSpeech {
	"Ah, young {PLAYER}…\p"
	"Wouldn't you agree that explorations\n"
	"are the grandest of adventures?\p"
	"Your own wits! Your own strength!\n"
	"Your own Pokémon!\p"
	"And, above all, only your courage to\n"
	"lead you through unknown worlds…\p"
	"Eh? I'm Brandon, the first member of\n"
	"Hoenn's Elite Four.\p"
	"Now, then! Bring your courage to our\n"
	"battle here in the Pokémon League!"
}
text EverGrandeCity_SidneysRoom_Text_Defeat {
	"That's it! You've done great!\n"
	"You've worked hard for this!"
}
text EverGrandeCity_SidneysRoom_Text_PostBattleSpeech {
	"Hahahah!\p"
	"Grand it was! Grand, yes, indeed!\p"
	"Young explorer!\n"
	"I look forward to our next meeting!\p"
	"Now, go on to the next room and enjoy\n"
	"your next battle!"
}