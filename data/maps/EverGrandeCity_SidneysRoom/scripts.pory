mapscripts EverGrandeCity_SidneysRoom_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE[
		VAR_ELITE_4_STATE, 0 : EverGrandeCity_SidneysRoom_EventScript_WalkInCloseDoor
	]
	MAP_SCRIPT_ON_LOAD : EverGrandeCity_SidneysRoom_OnLoad
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_1, 0 : PokemonLeague_EliteFour_EventScript_PlayerTurnNorth
    ]
}

script EverGrandeCity_SidneysRoom_EventScript_WalkInCloseDoor {
	lockall
	call(PokemonLeague_EliteFour_EventScript_WalkInCloseDoor)
	setvar(VAR_ELITE_4_STATE, 3)
	releaseall
	end
}

script EverGrandeCity_SidneysRoom_OnLoad {
	call_if_set(FLAG_DEFEATED_ELITE_4_BRANDON, PokemonLeague_EliteFour_EventScript_ResetAdvanceToNextRoom)
	call_if_eq(VAR_ELITE_4_STATE, 3, PokemonLeague_EliteFour_EventScript_CloseDoor)
	end
}

script EverGrandeCity_SidneysRoom_EventScript_Brandon {
	lock
	faceplayer
	goto_if_set(FLAG_DEFEATED_ELITE_4_BRANDON, EverGrandeCity_SidneysRoom_EventScript_PostBattleSpeech)
	playbgm(MUS_ENCOUNTER_ELITE_FOUR, FALSE)
	setflag(FLAG_USE_NEXT_LEVEL_CAP)
	msgbox(EverGrandeCity_SidneysRoom_Text_IntroSpeech, MSGBOX_DEFAULT)
	trainerbattle_no_intro(TRAINER_BRANDON_1, EverGrandeCity_SidneysRoom_Text_Defeat)
	
	clearflag(FLAG_USE_NEXT_LEVEL_CAP)
	setflag(FLAG_DEFEATED_ELITE_4_BRANDON)
	call(PokemonLeague_EliteFour_SetAdvanceToNextRoomMetatiles)
	EverGrandeCity_SidneysRoom_EventScript_PostBattleSpeech:
	msgbox(EverGrandeCity_SidneysRoom_Text_PostBattleSpeech, MSGBOX_DEFAULT)
	release
	end
}

text EverGrandeCity_SidneysRoom_Text_IntroSpeech {
	"Ah, young {PLAYER}…\p"
	"Wouldn't you agree that explorations\n"
	"are the grandest of adventures?\p"
	"Your own wits! Your own strength!\n"
	"Your own Pokémon!\p"
	"And, above all, only your courage to\n"
	"lead you through unknown worlds…\p"
	"Eh? I'm Brandon, the third member of\n"
	"Hoenn's Elite Four.\p"
	"Now, then! Bring your courage to our\n"
	"battle here in the Pokémon League!"
}
text EverGrandeCity_SidneysRoom_Text_Defeat {
	"That's it! You've done great!\n"
	"You've worked hard for this!"
}
text EverGrandeCity_SidneysRoom_Text_PostBattleSpeech {
	"Hahahah!\p"
	"Grand it was! Grand, yes, indeed!\p"
	"Young explorer!\n"
	"I look forward to our next meeting!\p"
	"Now, go on to the next room and enjoy\n"
	"your next battle!"
}