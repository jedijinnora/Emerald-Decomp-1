mapscripts SootopolisCity_House6_MapScripts {}

script SootopolisCity_House6_EventScript_HelioptileWoman {
	lock
	faceplayer
	goto_if_set(FLAG_RECEIVED_HELIOPTILE, GivenHelioptile)
	if(flag(FLAG_DEFEATED_SOOTOPOLIS_GYM)) {
		msgbox(format("Oh, you defeated the Sootopolis Gym already?"))
		msgbox(format("What a shame, this Pokémon would have been useful."))
	} else {
		msgbox(format("Oh, are you going to challenge Sootopolis Gym?"))
		msgbox(format("I have a Pokémon that would be useful."))
	}
	msgbox(format("Will you take Helioptile?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclinedHelioptile)
	specialvar(VAR_RESULT, CalculatePlayerPartyCount)
	goto_if_eq(VAR_RESULT, PARTY_SIZE, NoRoomForHelioptile)

	setflag(FLAG_RECEIVED_HELIOPTILE)
	givemon(SPECIES_HELIOPTILE, 10)
	playfanfare(MUS_OBTAIN_ITEM)
	bufferspeciesname(STR_VAR_1, SPECIES_HELIOPTILE)
    msgbox("{PLAYER} received {STR_VAR_1}!")
	waitmessage
	waitfanfare
	call(Common_EventScript_GetGiftMonPartySlot)
	switch(var(VAR_0x8004)) {
		case 1:
			setmonmove(1, 2, MOVE_CHARGE_BEAM)
			setmonmove(1, 3, MOVE_GRASS_KNOT)
		case 2:
			setmonmove(2, 2, MOVE_CHARGE_BEAM)
			setmonmove(2, 3, MOVE_GRASS_KNOT)
		case 3:
			setmonmove(3, 2, MOVE_CHARGE_BEAM)
			setmonmove(3, 3, MOVE_GRASS_KNOT)
		case 4:
			setmonmove(4, 2, MOVE_CHARGE_BEAM)
			setmonmove(4, 3, MOVE_GRASS_KNOT)
		default:
			setmonmove(5, 2, MOVE_CHARGE_BEAM)
			setmonmove(5, 3, MOVE_GRASS_KNOT)
	}

    msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
	msgbox(format("If your Heliptile has the ability Sand Veil, you should consider using an Ability Capsule on it."))
	release
	end

	DeclinedHelioptile:
	msgbox(format("No? Good luck."))
	release
	end

	NoRoomForHelioptile:
	msgbox(format("You don't have room."))
	release
	end

	GivenHelioptile:
	msgbox(format("Many Pokémon have abilities affected by the weather."))
	msgbox(format("A Helioptile or Heliolisk with the ability Dry Skin is excellent against Water types."))
	release
	end
}