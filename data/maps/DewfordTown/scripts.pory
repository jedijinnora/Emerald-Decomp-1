mapscripts DewfordTown_MapScripts{
	MAP_SCRIPT_ON_TRANSITION : DewfordTown_OnTransition
}

const LOCALID_POKEFAN_F = 4
const LOCALID_LITTLE_BOY = 5

//FLAG_TEMP_1 Little sand boy intro
//FLAG_TEMP_2 PokefanF request
//VAR_TEMP_1 player x-coord
//VAR_TEMP_2 player y-coord

script DewfordTown_OnTransition {
	call_if_unset(FLAG_VISITED_DEWFORD_TOWN, DewfordTown_EventScript_VisitDewford)
	call_if_set(FLAG_SAND_FAMILY_MOVED, DewfordTown_EventScript_HideNPCs)
	end
}

script DewfordTown_EventScript_VisitDewford {
	setflag(FLAG_VISITED_DEWFORD_TOWN)
	addvar(VAR_CITIES_VISITED, 1)
	return
}

script DewfordTown_EventScript_HideNPCs {
	setflag(FLAG_TEMP_11)
	setflag(FLAG_TEMP_12)
	return
}

script DewfordTown_EventScript_TownSign {
	msgbox("Dewford Town\n “A tiny island in the blue sea.”", MSGBOX_SIGN)
	end
}

script DewfordTown_EventScript_GymSign {
	msgbox("Dewford Town Pokémon Gym\n Leader: Sidney\l “The fighting punk!”", MSGBOX_SIGN)
	end
}

script DewfordTown_EventScript_HallSign {
	msgbox("Dewford Hall\n “Everyone's information exchange!”", MSGBOX_SIGN)
	end
}

script DewfordTown_EventScript_Woman {
	lock
	faceplayer
	msgbox(format("Dewford is a small island community."))
	msgbox(format("The tourists who come for the Resort have been causing some friction."))
	msgbox(format("I suppose conflict between locals and outsiders is inevitable."))
	release
	end
}

script DewfordTown_EventScript_Fisherman {
	lock
	faceplayer
	msgbox(format("There are three brothers who'll give out fishing rods to worthy Trainers."))
	checkitem(ITEM_OLD_ROD)
	if (!var(VAR_RESULT)) {
		msgbox(format("You oughta head back to Ever Grande and look around more!"))
		release 
		end
	}
	checkitem(ITEM_GOOD_ROD)
	if (!var(VAR_RESULT)) {
		msgbox(format("The middle brother hands around the eastern side of Route 118."))
		release 
		end
	}
	checkitem(ITEM_SUPER_ROD)
	if (!var(VAR_RESULT)) {
		msgbox(format("The oldest brother lives in Mossdeep and grants the Super Rod if you've beaten every other Fisherman in Hoenn!"))
		release 
		end
	}
	msgbox(format("Wowie! \n You're the best fisher in Hoenn!"))
	release
	end
}

script DewfordTown_EventScript_PokefanF {
	lock
	faceplayer
	msgbox(format("My son ran off when we left for the beach this morning."))
	msgbox(format("If you see him, please let me know."))
	setflag(FLAG_TEMP_2)
	goto_if_set(FLAG_TEMP_1, DewfordTown_EventScript_YouveSeenHim)
	release
	end
}

script DewfordTown_EventScript_YouveSeenHim {
	msgbox(format("Oh? Have you seen him?"))
	dynmultipush("Don't answer", 0)
	dynmultipush("(Lie) No idea?", 1)
	dynmultipush("(Truth) He's over there!", 2)
	dynmultistack(0,0,FALSE,6,FALSE,0,DYN_MULTICHOICE_CB_NONE)
	switch(var(VAR_RESULT)) {
		case 1:
			msgbox(format("Where could he be?"))
			release
			end
		case 2:
			msgbox(format("Oh, thank you!"))
			closemessage
			getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
			call_if_eq(VAR_TEMP_2, 25, DewfordTown_EventScript_MomLeaveSouth)
			call_if_ne(VAR_TEMP_2, 25, DewfordTown_EventScript_MomLeaveEast)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
			applymovement(LOCALID_POKEFAN_F, DewfordTown_Movement_MomLeave)
			waitmovement(0)
			removeobject(LOCALID_POKEFAN_F)
			removeobject(LOCALID_LITTLE_BOY)
			setflag(FLAG_TEMP_11)
			setflag(FLAG_TEMP_12)
			setflag(FLAG_SAND_FAMILY_MOVED)
		default:
			release
			end
	}
	release
	end
}

script DewfordTown_EventScript_MomLeaveEast {
	applymovement(LOCALID_POKEFAN_F, DewfordTown_Movement_MomEastFirst)
	waitmovement(0)
	return
}

script DewfordTown_EventScript_MomLeaveSouth {
	applymovement(LOCALID_POKEFAN_F, DewfordTown_Movement_MomSouthFirst)
	waitmovement(0)
	return
}

movement DewfordTown_Movement_MomEastFirst {
	walk_right
	walk_down
	step_end
}

movement DewfordTown_Movement_MomSouthFirst {
	walk_down
	walk_right
	step_end
}

movement DewfordTown_Movement_MomLeave {
	walk_down
	walk_right * 9
	step_end
}

script DewfordTown_EventScript_LittleBoy {
	lock
	faceplayer
	call_if_set(FLAG_TEMP_2, DewfordTown_EventScript_ImHiding)
	goto_if_set(FLAG_TEMP_1, DewfordTown_EventScript_IHateSand)
	msgbox(format("Mom wants to make me go to the beach! The nasty sand will get all over me!"))
	setflag(FLAG_TEMP_1)
	call(DewfordTown_EventScript_IHateSand)
	release
	end
}

script DewfordTown_EventScript_ImHiding {
	msgbox(format("Shh! Don't tell Mom where I am!"))
	return
}

script DewfordTown_EventScript_IHateSand {
	msgbox(format("I hate sand! It's coarse and rough, and it gets everywhere!"))
	release
	end
}

script DewfordTown_EventScript_OldMan {
	msgbox(format("Pokémon are so convenient! One Ampharos can light up the whole sea for a sailing ship."), MSGBOX_NPC)
	end
}


raw `
DewfordTown_EventScript_TrendyPhraseBoy::
	lock
	faceplayer
	call Common_EventScript_BufferTrendyPhrase
	msgbox DewfordTown_Text_XIsTheBiggestHappeningThingRight, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, DewfordTown_EventScript_ConfirmTrendyPhrase
	goto_if_eq VAR_RESULT, NO, DewfordTown_EventScript_RejectTrendyPhrase
	end

DewfordTown_EventScript_ConfirmTrendyPhrase::
	msgbox DewfordTown_Text_YeahDefinitionOfInRightNow, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_RejectTrendyPhrase::
	msgbox DewfordTown_Text_TellMeWhatsNewAndIn, MSGBOX_DEFAULT
	setvar VAR_0x8004, EASY_CHAT_TYPE_TRENDY_PHRASE
	call Common_ShowEasyChatScreen
	lock
	faceplayer
	goto_if_eq VAR_RESULT, TRUE, DewfordTown_EventScript_GiveNewTrendyPhrase
	goto_if_eq VAR_RESULT, FALSE, DewfordTown_EventScript_CancelNewTrendyPhrase
	end

DewfordTown_EventScript_GiveNewTrendyPhrase::
	incrementgamestat GAME_STAT_STARTED_TRENDS
	goto_if_eq VAR_0x8004, FALSE, DewfordTown_EventScript_PhraseNotTrendyEnough
	msgbox DewfordTown_Text_OfCourseIKnowAboutThat, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_CancelNewTrendyPhrase::
	msgbox DewfordTown_Text_HearOfAnyTrendsComeShareWithMe, MSGBOX_DEFAULT
	release
	end

DewfordTown_EventScript_PhraseNotTrendyEnough::
	msgbox DewfordTown_Text_XHuhIThinkYIsCool, MSGBOX_DEFAULT
	release
	end

DewfordTown_Text_XIsTheBiggestHappeningThingRight:
	.string "I like what's hip, happening, and trendy.\n"
	.string "I'm always checking it out.\p"
	.string "Listen, have you heard about this new\n"
	.string "“{STR_VAR_1}”?\p"
	.string "That's right!\n"
	.string "Of course you know!\p"
	.string "I mean, sheesh,\n"
	.string "“{STR_VAR_1}”…\l"
	.string "It's the hottest thing in cool!\p"
	.string "Wherever you're from,\n"
	.string "“{STR_VAR_1}”\l"
	.string "is the biggest happening thing, right?$"

DewfordTown_Text_TellMeWhatsNewAndIn:
	.string "Hunh?\n"
	.string "It's not the hip and happening thing?\p"
	.string "Well, hey, you have to tell me,\n"
	.string "what's new and what's “in”?$"

DewfordTown_Text_OfCourseIKnowAboutThat:
	.string "Hunh?\n"
	.string "“{STR_VAR_2}”?\p"
	.string "… …\p"
	.string "…Uh… Yeah! That's right!\n"
	.string "Yeah, I knew that! Knew it all along!\p"
	.string "Of course I know about that!\n"
	.string "“{STR_VAR_2},” right?\p"
	.string "Yeah, that's it, it's there!\n"
	.string "Isn't “{STR_VAR_2}”\l"
	.string "the coolest, or what?\p"
	.string "It's the hippest thing in hip.\n"
	.string "You think I'd not know about it?\p"
	.string "“{STR_VAR_1}”…\n"
	.string "It's, like, so five minutes ago.\p"
	.string "Now, “{STR_VAR_2}” is\n"
	.string "what's vital and in tune with the times!$"

DewfordTown_Text_XHuhIThinkYIsCool:
	.string "Hmm…\n"
	.string "“{STR_VAR_2},” huh?\p"
	.string "But personally, I think\n"
	.string "“{STR_VAR_1}”\l"
	.string "is what's real in cool.$"

DewfordTown_Text_HearOfAnyTrendsComeShareWithMe:
	.string "Well, if you hear of any happening new\n"
	.string "trends, come share them with me, okay?$"

DewfordTown_Text_YeahDefinitionOfInRightNow:
	.string "Yeah, absolutely right!\p"
	.string "“{STR_VAR_1}” is the\n"
	.string "definition of “in” right now.$"
`
