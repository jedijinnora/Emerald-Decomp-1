const LOCALID_REGIGIGAS = 12
const LOCALID_REGIROCK = 13
const LOCALID_REGICE = 14
const LOCALID_REGISTEEL = 15

mapscripts TitansGrave_MapScripts {
    MAP_SCRIPT_ON_TRANSITION : TitansGrave_OnTransition
    MAP_SCRIPT_ON_RESUME : TitansGrave_OnResume
}

script TitansGrave_OnTransition {
    setflag(FLAG_TEMP_E)
    setflag(FLAG_TEMP_15)
    setflag(FLAG_TEMP_16)
    setflag(FLAG_TEMP_17)
    setvar(VAR_TEMP_0, 1)
    if(flag(FLAG_DEFEATED_REGIGIGAS)) {
        setvar(VAR_TEMP_0, 10)
        setflag(FLAG_TEMP_11)
    }
    end
}

script TitansGrave_OnResume {
	setdivewarp(MAP_UNDERWATER_TITANS_GRAVE_ENTRY, 10, 8)
	setescapewarp(MAP_ROUTE109, 105, 11)
    call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, Common_EventScript_TryRemoveCaughtEncounter)
    removeobject(LOCALID_REGIROCK)
    removeobject(LOCALID_REGICE)
    removeobject(LOCALID_REGISTEEL)
    setmetatile(12, 13, 0x237, TRUE)
    setmetatile(13, 13, 0x237, TRUE)
    setmetatile(14, 13, 0x237, TRUE)
	end
}

script TitansGrave_EventScript_BrailleInstructions1 {
    lock
    braillemsgbox(TitansGrave_Braille_Instructions1)
    release
    end
}

script TitansGrave_EventScript_BrailleInstructions2 {
    lock
    braillemsgbox(TitansGrave_Braille_Instructions2)
    release
    end
}

script TitansGrave_EventScript_BrailleInstructions3 {
    lock
    braillemsgbox(TitansGrave_Braille_Instructions3)
    release
    end
}

script TitansGrave_EventScript_BrailleRock {
    lock
    braillemsgbox(TitansGrave_Braille_Rock)
    goto_if_set(FLAG_TEMP_1, TitansGrave_EventScript_BrailleEnd)
    goto_if_set(FLAG_DEFEATED_REGIGIGAS, TitansGrave_EventScript_BrailleEnd)
    goto_if_set(FLAG_DEFEATED_REGIROCK, TitansGrave_EventScript_UnlockRock)
    setvar(VAR_TEMP_0, 1)
    release
    end
}

script TitansGrave_EventScript_BrailleIce {
    lock
    braillemsgbox(TitansGrave_Braille_Ice)
    goto_if_set(FLAG_TEMP_2, TitansGrave_EventScript_BrailleEnd)
    goto_if_set(FLAG_DEFEATED_REGIGIGAS, TitansGrave_EventScript_BrailleEnd)
    goto_if_set(FLAG_DEFEATED_REGICE, TitansGrave_EventScript_UnlockIce)
    setvar(VAR_TEMP_0, 1)
    release
    end
}

script TitansGrave_EventScript_BrailleSteel {
    lock
    braillemsgbox(TitansGrave_Braille_Steel)
    goto_if_set(FLAG_TEMP_3, TitansGrave_EventScript_BrailleEnd)
    goto_if_set(FLAG_DEFEATED_REGIGIGAS, TitansGrave_EventScript_BrailleEnd)
    goto_if_set(FLAG_DEFEATED_REGISTEEL, TitansGrave_EventScript_UnlockSteel)
    setvar(VAR_TEMP_0, 1)
    release
    end
}

script TitansGrave_EventScript_UnlockRock {
    setflag(FLAG_TEMP_1)
    setvar(VAR_TEMP_0, 0)
    playse(SE_UNLOCK)
    checkforspecies(SPECIES_REGIROCK, TitansGrave_EventScript_PlayerHasRegirock)
    msgbox(format("You have defeated Regirock. Something unlocked!"))
    release
    end
}

script TitansGrave_EventScript_UnlockIce {
    setflag(FLAG_TEMP_2)
    setvar(VAR_TEMP_0, 0)
    playse(SE_UNLOCK)
    checkforspecies(SPECIES_REGICE, TitansGrave_EventScript_PlayerHasRegice)
    msgbox(format("You have defeated Regice. Something unlocked!"))
    release
    end
}

script TitansGrave_EventScript_UnlockSteel {
    setflag(FLAG_TEMP_3)
    setvar(VAR_TEMP_0, 0)
    playse(SE_UNLOCK)
    checkforspecies(SPECIES_REGISTEEL, TitansGrave_EventScript_PlayerHasRegisteel)
    msgbox(format("You have defeated Registeel. Something unlocked!"))
    release
    end
}

script TitansGrave_EventScript_BrailleEnd {
    release
    end
}

movement TitansGrave_Movement_RegiAppear {
    exit_pokeball
    face_down
}

script TitansGrave_EventScript_PlayerHasRegirock {
    setflag(FLAG_TEMP_4)
    playmoncry(SPECIES_REGIROCK, CRY_MODE_ENCOUNTER)
    addobject(LOCALID_REGIROCK)
    applymovement(LOCALID_REGIROCK, TitansGrave_Movement_RegiAppear)
    waitmovement(0)
    msgbox(format("Regirock added power!"))
    release
    end
}

script TitansGrave_EventScript_PlayerHasRegice {
    setflag(FLAG_TEMP_5)
    playmoncry(SPECIES_REGICE, CRY_MODE_ENCOUNTER)
    addobject(LOCALID_REGICE)
    applymovement(LOCALID_REGICE, TitansGrave_Movement_RegiAppear)
    waitmovement(0)
    msgbox(format("Regice added power!"))
    release
    end
}

script TitansGrave_EventScript_PlayerHasRegisteel {
    setflag(FLAG_TEMP_6)
    playmoncry(SPECIES_REGISTEEL, CRY_MODE_ENCOUNTER)
    addobject(LOCALID_REGISTEEL)
    applymovement(LOCALID_REGISTEEL, TitansGrave_Movement_RegiAppear)
    waitmovement(0)
    msgbox(format("Registeel added power!"))
    release
    end
}

script TitansGrave_EventScript_BrailleElectric {
    lock
    braillemsgbox(TitansGrave_Braille_Electric)
    release
    end
}

script TitansGrave_EventScript_BrailleDragon {
    lock
    braillemsgbox(TitansGrave_Braille_Dragon)
    release
    end
}

script TitansGrave_EventScript_WalkUp {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    waitmovement(0)
    goto(TitansGrave_EventScript_CheckRegigigas)
}

script TitansGrave_EventScript_CheckRegigigas {
    lock
    playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitse
	waitmovement(OBJ_EVENT_ID_PLAYER)
    fadeoutbgm(0)
    playse(SE_TRUCK_DOOR)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
    waitmovement(0)
    delay(10)
    call_if_set(FLAG_TEMP_4, TitansGrave_EventScript_Regirock_Support)
	goto_if_unset(FLAG_TEMP_1, TitansGrave_EventScript_CheckEnd)
    call(TitansGrave_EventScript_Shake)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    waitmovement(0)
    delay(10)
    call_if_set(FLAG_TEMP_5, TitansGrave_EventScript_Regice_Support)
	goto_if_unset(FLAG_TEMP_2, TitansGrave_EventScript_CheckEnd)
    call(TitansGrave_EventScript_Shake)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)
    delay(10)
    call_if_set(FLAG_TEMP_6, TitansGrave_EventScript_Registeel_Support)
	goto_if_unset(FLAG_TEMP_3, TitansGrave_EventScript_CheckEnd)
    call(TitansGrave_EventScript_Shake)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)
    delay(10)
    goto_if_unset(FLAG_TEMP_4, TitansGrave_EventScript_WeakRegigigas)
    goto_if_unset(FLAG_TEMP_5, TitansGrave_EventScript_WeakRegigigas)
    goto_if_unset(FLAG_TEMP_6, TitansGrave_EventScript_WeakRegigigas)
    goto_if_set(FLAG_SYS_GAME_CLEAR, TitansGrave_EventScript_StrongRegigigas)
    goto_if_gt(VAR_DIFFICULTY_SETTING, 1, TitansGrave_EventScript_WeakRegigigas)
    goto(TitansGrave_EventScript_StrongRegigigas)
}

script TitansGrave_EventScript_CheckEnd {
    msgbox("Interestingâ€¦")
    setvar(VAR_TEMP_0, 1)
    fadeinbgm(0)
    release
    end
}

script TitansGrave_EventScript_Shake {
    playse(SE_TRUCK_DOOR)
	special(DoSealedChamberShakingEffect_Short)
	waitstate
	delay(30)
    return
}

script TitansGrave_EventScript_Regirock_Support {
    applymovement(LOCALID_REGIROCK, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    applymovement(LOCALID_REGIROCK, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    playmoncry(SPECIES_REGIROCK, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(10)
    return
}
script TitansGrave_EventScript_Regice_Support {
    applymovement(LOCALID_REGICE, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    applymovement(LOCALID_REGICE, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    playmoncry(SPECIES_REGICE, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(10)
    return
}
script TitansGrave_EventScript_Registeel_Support {
    applymovement(LOCALID_REGISTEEL, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    applymovement(LOCALID_REGISTEEL, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    playmoncry(SPECIES_REGISTEEL, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(10)
    return
}

script TitansGrave_EventScript_RegigigasApproach {
    clearflag(FLAG_TEMP_11)
    call(TitansGrave_EventScript_Shake)
    setmetatile(12, 13, 0x279, FALSE)
    setmetatile(13, 13, 0x279, FALSE)
    setmetatile(14, 13, 0x279, FALSE)
    playse(SE_TRUCK_DOOR)
    special(DrawWholeMapView)
    applymovement(LOCALID_REGIGIGAS, TitansGrave_Movement_RegigigasApproach)
    waitmovement(0)
    msgbox(format("The ancient titan attacked!"))
    return
}

movement TitansGrave_Movement_RegigigasApproach {
    walk_down
    delay_16
    walk_down
    delay_16
    walk_down
    delay_16
    walk_down
    delay_16
    walk_down
    delay_16
}

script TitansGrave_EventScript_WeakRegigigas {
    msgbox(format("Something is awakening!"))
    delay(40)
    playmoncry(SPECIES_REGIGIGAS, CRY_MODE_WEAK)
    waitmoncry
    closemessage
    call(TitansGrave_EventScript_ChooseWeakRegigigasLevel)
    call(TitansGrave_EventScript_RegigigasApproach)
    setvar(VAR_LAST_TALKED, LOCALID_REGIGIGAS)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    special(StartRegiBattle)
    waitstate
    setflag(FLAG_DEFEATED_REGIGIGAS)
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    removeobject(LOCALID_REGIGIGAS)
    setvar(VAR_TEMP_0, 10)
    release
    end
}

script TitansGrave_EventScript_StrongRegigigas {
    msgbox(format("Something monstrous is awakening!"))
    delay(40)
    playmoncry(SPECIES_REGIGIGAS, CRY_MODE_ENCOUNTER)
    waitmoncry
    closemessage
    setflag(FLAG_WILD_HIDDEN_ABILITY)
    setflag(FLAG_WILD_PERFECT_IVS)
    setflag(FLAG_SMART_WILD_AI)
    call(TitansGrave_EventScript_ChooseStrongRegigigasLevel)
    call(TitansGrave_EventScript_RegigigasApproach)
    setvar(VAR_LAST_TALKED, LOCALID_REGIGIGAS)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    special(StartRegiBattle)
    waitstate
    setflag(FLAG_DEFEATED_REGIGIGAS)
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    removeobject(LOCALID_REGIGIGAS)
    setvar(VAR_TEMP_0, 10)
    clearflag(FLAG_WILD_HIDDEN_ABILITY)
    clearflag(FLAG_WILD_PERFECT_IVS)
    clearflag(FLAG_SMART_WILD_AI)
    release
    end
}

script TitansGrave_EventScript_ChooseWeakRegigigasLevel {
    switch(var(VAR_NUM_BADGES)) {
        case 4:
            createmon(1, 0, SPECIES_REGIGIGAS, 50, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT)
        case 5:
            createmon(1, 0, SPECIES_REGIGIGAS, 60, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT)
        case 6:
            createmon(1, 0, SPECIES_REGIGIGAS, 70, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT)
        case 7:
            createmon(1, 0, SPECIES_REGIGIGAS, 75, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT)
        default:
            createmon(1, 0, SPECIES_REGIGIGAS, 85, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT)
    }
    return
}

script TitansGrave_EventScript_ChooseStrongRegigigasLevel {
    switch(var(VAR_NUM_BADGES)) {
        case 4:
            createmon(1, 0, SPECIES_REGIGIGAS, 50, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT, hpIv=31, atkIv=31, defIv=31, speedIv=31, spAtkIv=31, spDefIv=31, abilityNum=2)
        case 5:
            createmon(1, 0, SPECIES_REGIGIGAS, 60, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT, hpIv=31, atkIv=31, defIv=31, speedIv=31, spAtkIv=31, spDefIv=31, abilityNum=2)
        case 6:
            createmon(1, 0, SPECIES_REGIGIGAS, 70, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT, hpIv=31, atkIv=31, defIv=31, speedIv=31, spAtkIv=31, spDefIv=31, abilityNum=2)
        case 7:
            createmon(1, 0, SPECIES_REGIGIGAS, 75, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT, hpIv=31, atkIv=31, defIv=31, speedIv=31, spAtkIv=31, spDefIv=31, abilityNum=2)
        default:
            createmon(1, 0, SPECIES_REGIGIGAS, 85, ITEM_LEPPA_BERRY, move1=MOVE_ZEN_HEADBUTT, move2=MOVE_HEAVY_SLAM, move3=MOVE_HAMMER_ARM, move4=MOVE_GIGA_IMPACT, hpIv=31, atkIv=31, defIv=31, speedIv=31, spAtkIv=31, spDefIv=31, abilityNum=2)
    }
    return
}