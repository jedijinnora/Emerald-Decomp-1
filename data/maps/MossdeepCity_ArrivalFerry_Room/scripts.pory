mapscripts MossdeepCity_ArrivalFerry_Room_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_CLEANUP_STATE, 0: MossdeepCity_ArrivalFerry_Room_EventScript_Init
    ] 
}

script MossdeepCity_ArrivalFerry_Room_EventScript_Init {
    lock
    delay(60)
    setrespawn(HEAL_LOCATION_MOSSDEEP_CITY)
    applymovement(1, Exclaim)
    msgbox(format("Ah, the sleeper awakens! It feels like an eternity since you burst in here like a hurricane and fell straight asleep!"))
    msgbox(format("Consider yourself lucky that I am not such a boor to deny someone their beauty sleep..."))
    applymovement(1, ApproachPlayer)
    waitmovement(1)
    applymovement(1, SeetheU)
    waitmovement(1)
    msgbox(format("But this is ridiculous! Zut alors! Look at all this rubbish! Get up!"))
    msgbox(format("You tossed your belongings all over our room in your sleep! I spent the whole trip from SINNOH shielding myself from your clumsy bombardment!"))
    msgbox(format("Who do you think is going to remedy the deplorable state of your side of this cabin, your mother?"))
    msgbox(format("I'm certain she is a woman of prodigious talent, but she is most definitely not present in our current locale!"))
    msgbox(format("I have my book and water and was ready to leave hours ago but here I am babying some layabout who thinks they are too important to respect other people's schedules!"))
    waitmessage
    applymovement(1, SeetheU)
    waitmovement(1)
    msgbox(format("What? It's too early for this?"))
    waitmessage
    applymovement(1, SeetheU)
    waitmovement(1)
    msgbox(format("Then let me speak in language even a buffoon like you shall comprehend:"))
    applymovement(1, SeetheU)
    waitmovement(1)
    msgbox(format("PICK UP YOUR SHIT!"))
    waitmessage
    applymovement(1, SeetheU)
    waitmovement(1)
    applymovement(1, ApproachDoor)
    waitmovement(1)
    setobjectxyperm(1, 5, 8)
    setvar(VAR_CLEANUP_STATE, 1)
    release
    end
}

movement Exclaim {
    emote_exclamation_mark
}

movement ApproachPlayer {
    walk_left * 4
    face_up
}

movement ApproachDoor {
    walk_right * 2
    walk_down * 3
    face_up
}

movement SeetheU {
    walk_in_place_fast_up * 2
}

movement SeetheD {
    walk_in_place_fast_down * 2
}

movement SeetheL {
    walk_in_place_fast_left * 2
}

movement SeetheR {
    walk_in_place_fast_right * 2
}

movement MoveUp {
    walk_up
}

movement Leave {
    walk_in_place_down
}

movement Face_Player {
    face_player
}

movement Face_Left {
    face_left
}

movement Face_Right {
    face_right
}

script MossdeepCity_ArrivalFerry_Room_EventScript_Madman {
    lock
    applymovement(1, Face_Player)
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_1) == 4) {
        applymovement(OBJ_EVENT_ID_PLAYER, Face_Right)
        waitmovement(OBJ_EVENT_ID_PLAYER)
    }

    call(MossdeepCity_ArrivalFerry_Room_EventScript_CheckCleanup)
    switch(var(VAR_CLEANUP_STATE)) {
        case 3:
            call(MossdeepCity_ArrivalFerry_Room_EventScript_Finished)
        case 2:
            call(MossdeepCity_ArrivalFerry_Room_EventScript_GoFind)
        default:
            call(MossdeepCity_ArrivalFerry_Room_EventScript_GoClean)
    }
    release
    end
}


    //0 = Starting state
    //1 = need to clean visible items
    //2 = all visible items picked up, necessary hidden items remain
    //3 = all necessary items gathered
    //4 = exit from room has occurred
script MossdeepCity_ArrivalFerry_Room_EventScript_CheckCleanup {
    if (flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKE_BALL1) && flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKE_BALL2) && flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POTION) && flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKEDEX) && flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_BAG)) {
        setvar(VAR_CLEANUP_STATE, 2)
        if (flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKEBLOCK_CASE) && flag(FLAG_SYS_NATIONAL_DEX)) {
            setvar(VAR_CLEANUP_STATE, 3)
        }
    }
    return
}

//cleanup 1
script MossdeepCity_ArrivalFerry_Room_EventScript_GoClean {
    lock
    applymovement(1, SeetheU)
    waitmovement(1)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveUp)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("Not good enough! We won't depart until you have retrieved all of your belongings!"))
    msgbox(format("Be grateful I am not making us carry out our trash. To be clear:"))
    msgbox(format("LET'S SEE SOME MORE CLEANING!"))
    waitmessage
    release
    end
}

//cleanup 2
script MossdeepCity_ArrivalFerry_Room_EventScript_GoFind {
    lock
    applymovement(1, SeetheU)
    waitmovement(1)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveUp)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("What? You cannot think of anywhere else to clean?"))
    msgbox(format("I am astonished that a ludicrously imbecilic individual such as yourself has been sent to Hoenn by the eminent Professor Rowan."))
    msgbox(format("Make certain you retrieved your disgusting shoes from beneath the bed!"))
    msgbox(format("And be sure to check the trash can! A nincompoop like you has likely thrown away valuable items!"))
    waitmessage
    release
    end
}

//cleanup 3
script MossdeepCity_ArrivalFerry_Room_EventScript_Finished {
    lock
    msgbox(format("That's much better, isn't it?"))
    msgbox(format("It's important to neaten up after yourself! Cleaning staff are people too, don't give them unnecessary work!"))
    msgbox(format("Anyways, we've arrived at MOSSDEEP CITY. You mumbled something about coming to HOENN as an exchange student before you collapsed."))
    msgbox(format("I'm sure you're eager to stretch your legs! Let's make tracks!"))
    waitmessage
    applymovement(1, Leave)
    playse(SE_EXIT)
    waitse
    waitmovement(1)
    setflag(FLAG_HIDE_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_ROOMMATE)
    removeobject(1)
    setvar(VAR_CLEANUP_STATE, 4)
    release
    end
}


script MossdeepCity_ArrivalFerry_Room_EventScript_Pokedex {
    lock
    playfanfare(MUS_OBTAIN_ITEM)
    message(format("{PLAYER} picked up the POKEDEX!"))
    waitfanfare
    closemessage
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_ADVENTURE_STARTED)
    setflag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKEDEX)
    special(SetUnlockedPokedexFlags)
    setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    setflag(FLAG_HIDE_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_ROOMMATE)
    removeobject(5)
    release
    end
}

script MossdeepCity_ArrivalFerry_Room_EventScript_UnderBed {
    lock
    msgbox(format("You check under the bed..."))
    if(flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKEBLOCK_CASE)) {
        msgbox(format("You find nothing but dust here."))
        releaseall
        end
    }
    msgbox(format("Hey! There's a bunch of your stuff down here!"))
    setflag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKEBLOCK_CASE)
    giveitem(ITEM_POKEBLOCK_CASE)
    giveitem(ITEM_POKE_DOLL, 1)
    giveitem(ITEM_POKE_BALL, 3)
    msgbox(format("You knock some dirt off your shoes and put them on."))
    playfanfare(MUS_OBTAIN_ITEM)
    setflag(FLAG_SYS_B_DASH)
    release
    end
}

script MossdeepCity_ArrivalFerry_Room_EventScript_TrashCan {
    lock
    if(flag(FLAG_SYS_NATIONAL_DEX)) {
        msgbox(format("Just food wrappers and papers you don't need."))
        release
        end
    }
    msgbox(format("There's a data drive in this trash can that looks important!"))
    if(flag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_POKEDEX)) {
        msgbox(format("Installed the National Dex Expansion!"))
        playfanfare(MUS_OBTAIN_ITEM)
        waitfanfare
        waitmessage
        setflag(FLAG_SYS_NATIONAL_DEX)
        special(EnableNationalPokedex)
        msgbox(format("Can you imagine coming all the way to HOENN thinking a regional POKÃ©DEX would be sufficient?"))
        waitmessage
        release
        end
    }
    msgbox(format("Unfortunately, you don't have anything to do with it right now."))
    release
    end
}

script MossdeepCity_ArrivalFerry_Room_EventScript_Bag {
    lock
    msgbox(format("It's the satchel with your starter POKEMON!"))
    waitmessage

    //choose from selection
    StarterChoice:
    message(format("Er... Which one did you pick again?"))
    waitmessage
    multichoice(25, 5, MULTI_STARTER_CHOICE, 1)
    closemessage
    switch(var(VAR_RESULT)) {
        case 0: 
            setvar(VAR_STARTER_CHOICE, SPECIES_TURTWIG)
            bufferspeciesname(STR_VAR_1, SPECIES_TURTWIG)
        case 1:
            setvar(VAR_STARTER_CHOICE, SPECIES_CHIMCHAR)
            bufferspeciesname(STR_VAR_1, SPECIES_CHIMCHAR)  
        case 2:
            setvar(VAR_STARTER_CHOICE, SPECIES_PIPLUP)
            bufferspeciesname(STR_VAR_1, SPECIES_PIPLUP)
        default:
            msgbox(format("Something went wrong! 1"))
    }
    closemessage
    showmonpic(VAR_STARTER_CHOICE, 10, 3)
    msgbox(format("Did you pick {STR_VAR_1}?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        hidemonpic
        goto(StarterChoice)
    }

    //get starter
    hidemonpic
    playfanfare(MUS_OBTAIN_ITEM)
    givemon(VAR_STARTER_CHOICE, 5, ITEM_ORAN_BERRY)
    waitfanfare 
    setflag(FLAG_ITEM_MOSSDEEP_CITY_ARRIVAL_FERRY_ROOM_BAG)
    removeobject(2)
    setflag(FLAG_SYS_POKEMON_GET)

    //nickname
    msgbox(format("Did you nickname your starter?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }

    //ending message
    msgbox(format("With your first POKEMON back at your side you are ready to take on the world!"))
    msgbox(format("...Assuming you've cleaned up enough that the madman will let you out the door."))
    release
    end
}