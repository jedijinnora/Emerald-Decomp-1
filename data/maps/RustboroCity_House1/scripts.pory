mapscripts RustboroCity_House1_MapScripts {}

script RustboroCity_House1_EventScript_Hiker {
	lock
	faceplayer
	msgbox(RustboroCity_House1_Text_AllSortsOfPlaces, MSGBOX_NPC)
	release
	end
}

script RustboroCity_House1_EventScript_SeedotTrader {
	lock
	faceplayer
	goto_if_set(FLAG_RUSTBORO_NPC_TRADE_COMPLETED, SeedotTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_SEEDOT)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(RustboroCity_House1_Text_IllTradeIfYouWant, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineSeedotTrade)
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineSeedotTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotRalts)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(RustboroCity_House1_Text_PleaseBeGoodToMyPokemon, MSGBOX_NPC)
	setflag(FLAG_RUSTBORO_NPC_TRADE_COMPLETED)
	release
	end

	DeclineSeedotTrade:
	msgbox(RustboroCity_House1_Text_YouDontWantToThatsOkay, MSGBOX_DEFAULT)
	release
	end

	SelectedNotRalts:
	bufferspeciesname(STR_VAR_1, VAR_0x8009)
	msgbox(RustboroCity_House1_Text_DoesntLookLikeMonToMe, MSGBOX_DEFAULT)
	release
	end

	SeedotTradeCompleted:
	msgbox(RustboroCity_House1_Text_AnyPokemonCanBeCute, MSGBOX_NPC)
	release
	end
}

script RustboroCity_House1_EventScript_HonedgeTrader {
	lock
	faceplayer
	goto_if_set(FLAG_RUSTBORO_HONEDGE_TRADE_COMPLETED, HonedgeTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_HONEDGE)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("I need an awesome Dragon-type Pokémon to prove I'm the toughest Youngster on the block!"))
	msgbox(format("I've got this cool Ghost-type sword I'll trade you?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineHonedgeTrade)
	msgbox(format("Yes! Gimme your Salamence!"))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineHonedgeTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotSalamence)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Sweet! I'm gonna take your Salamence and teach all those lame Bug Catchers a lesson!"))
	setflag(FLAG_RUSTBORO_HONEDGE_TRADE_COMPLETED)
	release
	end

	DeclineHonedgeTrade:
	msgbox(format("Bah. You'll be back."))
	release
	end

	SelectedNotSalamence:
	bufferspeciesname(STR_VAR_1, VAR_0x8009)
	msgbox(format("I said I wanted an awesome dragon! Get that crap outta here!"))
	release
	end

	HonedgeTradeCompleted:
	msgbox(format("Don't worry, I'll give Salamence plenty of exercise bully- er, sparring with the local kids."))
	release
	end
}



raw `
RustboroCity_House1_Text_IllTradeIfYouWant:
	.string "Huh? My Pokémon is cute?\n"
	.string "Sure, I knew that.\p"
	.string "But if you really want, I'm willing\n"
	.string "to trade it to you.\p"
	.string "I'll trade you my {STR_VAR_2} for\n"
	.string "a {STR_VAR_1} if you want.$"

RustboroCity_House1_Text_PleaseBeGoodToMyPokemon:
	.string "Eheheh…\n"
	.string "Please be good to my Pokémon.$"

RustboroCity_House1_Text_DoesntLookLikeMonToMe:
	.string "Huh? That doesn't look anything like\n"
	.string "a {STR_VAR_1} to me.$"

RustboroCity_House1_Text_YouDontWantToThatsOkay:
	.string "Oh, if you don't want to, that's okay.\n"
	.string "But my Pokémon is cute, you know…$"

RustboroCity_House1_Text_AnyPokemonCanBeCute:
	.string "Any Pokémon can be cute if you raise\n"
	.string "it with care and kindness.$"

RustboroCity_House1_Text_AllSortsOfPlaces:
	.string "In all sorts of places, there are all\n"
	.string "sorts of Pokémon and people.\p"
	.string "I find that fascinating, so I go to all\n"
	.string "sorts of places.$"

`
