mapscripts SlateportCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : SlateportCityOnTransition
}

script SlateportCityOnTransition {
	setflag(FLAG_VISITED_SLATEPORT_CITY)
	setvar(VAR_SLATEPORT_MUSEUM_1F_STATE, 0)
	end
}

script SlateportCity_EventScript_FerrySign {
	lock
	msgbox(format("Hoenn Fast Ferry \n Slateport Terminal"))
	release
	end
}

script SlateportCity_EventScript_SternsShipyardSign {
	lock
	msgbox(format("Stern's Shipyard \p Now hiring: \n engineers and technical specialists."))
	release
	end
}

script SlateportCity_EventScript_FatMan {
	lock
	faceplayer
	msgbox(format("Whew… I'm just bushed… I hiked all the way from Mauville City."))
	msgbox(format("I wish I had ridden my bike instead!"))
	release
	end
}

script SlateportCity_EventScript_Man1 {
	lock
	faceplayer
	msgbox(format("Stern is the fellow who built the museum and runs the shipyard."))
	msgbox(format("Everyone just calls him Captain Stern."))
	release
	end
}

script SlateportCity_EventScript_RichBoy {
	lock
	faceplayer
	msgbox(format("Scott has amazing plans! He's gonna bulldoze this lot and put in a high-tech Battle Tent!"))
	msgbox(format("Assuming he can get his dreams off the ground, anyways."))
	release
	end
}

script SlateportCity_EventScript_Woman1 {
	lock
	faceplayer
	msgbox(format("I'm grateful for the museum. I've been going since I was a girl and always learn something new."), MSGBOX_NPC)
	msgbox(format("The ocean is so vast and mysterious, even today."), MSGBOX_NPC)
	release
	end
}

script SlateportCity_EventScript_Cook {
	lock
	faceplayer
	msgbox(format("Order up!"))
	waitmessage
	pokemart(SlateportCookMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart SlateportCookMart {
	ITEM_ADRENALINE_ORB
	ITEM_WEAKNESS_POLICY
	ITEM_BLUNDER_POLICY
	ITEM_UTILITY_UMBRELLA
	ITEM_ROOM_SERVICE
	ITEM_METRONOME
}

script SlateportCity_EventScript_OldWoman {
	lock
	faceplayer
	msgbox(format("Where the water is clean, the fruits of bountiful harvest gather."))
	msgbox(format("And where people gather, a market soon starts."))
	msgbox(format("That is how a town is born and grown."))
	release
	end
}

script SlateportCity_EventScript_Girl {
	lock
	faceplayer
	msgbox(format("I love shopping here, with the ocean breeze and gorgeous view."))
	msgbox(format("Millennium Field is a close second though. Have you been to the festival?"))
	release
	end
}

script SlateportCity_EventScript_Sailor1 {
	lock
	faceplayer
	msgbox(format("The sea is just so vast…"))
	msgbox(format("I heard most of the water on Earth came from the impact of comets shortly after the planet formed."))
	msgbox(format("Strange that events we usually consider destructive are what blessed this place with the capacity for life…"))
	release
	end
}

script SlateportCity_EventScript_Sailor2 {
	lock
	faceplayer
	msgbox(format("Human civilization has had massive impacts on every part of the Earth."))
	msgbox(format("Our industry and pollution has even changed the average composition of seawater!"))
	msgbox(format("We sink old ships in the sea to become habitats for Pokémon…"))
	msgbox(format("It's a small way to give back, but even grains of sand can pile up to form a mountain, given time."))
	release
	end
}

script SlateportCity_EventScript_PokefanF {
	lock
	faceplayer
	msgbox(format("Whenever I visit the market I get carried away and buy too much!"))
	release
	end
}

script SlateportCity_EventScript_Man2 {
	lock
	faceplayer
	msgbox(format("Shipbuilding is Slateport's most vital industry. Our shipyards build the region's ferries!"))
	release
	end
}

script SlateportCity_EventScript_Maniac {
	lock
	faceplayer
	msgbox(format("There are a couple of people looking to trade Pokémon at the Fan Club."))
	msgbox(format("I'd be interested, but they want Pokémon evolved with expensive stones…"))
	release
	end
}

script SlateportCity_EventScript_Woman2 {
	lock
	faceplayer
	goto_if_set(FLAG_SLATEPORT_FURFROU_TRADE_COMPLETED, FurfrouTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_FURFROU)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Cinccino is just soooo fetch."))
	msgbox(format("Would you trade me one for my Furfrou? He's a bit old-fashioned, and I need to keep up with the times."), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineFurfrouTrade)
	msgbox(format("How good of you!"))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineFurfrouTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotCinccino)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Be sure to groom him at licensed stylists only!"))
	setflag(FLAG_SLATEPORT_FURFROU_TRADE_COMPLETED)
	release
	end

	DeclineFurfrouTrade:
	msgbox(format("So basic."))
	release
	end

	SelectedNotCinccino:
	bufferspeciesname(STR_VAR_1, VAR_0x800B)
	msgbox(format("That's a {STR_VAR_1}. I'm not trading my darling for that!"))
	release
	end

	FurfrouTradeCompleted:
	msgbox(format("Are you sure you're taking proper care of Bruiser?"))
	release
	end
}

script SlateportCity_EventScript_DecorClerk {
    lock
	faceplayer
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemartdecoration(SlateportDecorMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart SlateportDecorMart {
	DECOR_RED_BRICK
	DECOR_BLUE_BRICK
	DECOR_YELLOW_BRICK
	DECOR_RED_BALLOON
	DECOR_BLUE_BALLOON
	DECOR_YELLOW_BALLOON
	DECOR_C_LOW_NOTE_MAT
	DECOR_D_NOTE_MAT
	DECOR_E_NOTE_MAT
	DECOR_F_NOTE_MAT
	DECOR_G_NOTE_MAT
	DECOR_A_NOTE_MAT
	DECOR_B_NOTE_MAT
	DECOR_C_HIGH_NOTE_MAT
}

script SlateportCity_EventScript_DollClerk {
    lock
	faceplayer
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemartdecoration(SlateportDollMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart SlateportDollMart {
	DECOR_AZURILL_DOLL
	DECOR_MARILL_DOLL
	DECOR_SKITTY_DOLL
}

script SlateportCity_EventScript_Man3 {
	lock
	faceplayer
	msgbox(format("Pollution isn't just chemicals in the air or water."))
	msgbox(format("The light from human civilization stretches across the sky…"))
	msgbox(format("Noise from ship's propellers and sonar fills the seas…"))
	msgbox(format("So little of the world remains in its natural state."))
	release
	end
}

script SlateportCity_EventScript_StoneClerk {
	lock
	faceplayer
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemart(SlateportStoneMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart SlateportStoneMart {
	ITEM_FIRE_STONE
	ITEM_WATER_STONE
	ITEM_THUNDER_STONE
	ITEM_LEAF_STONE
	ITEM_ICE_STONE
	ITEM_SUN_STONE
	ITEM_MOON_STONE
	ITEM_SHINY_STONE
	ITEM_DUSK_STONE
	ITEM_DAWN_STONE
}

script SlateportCity_EventScript_EvoGuy {
	lock
	faceplayer
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemart(SlateportEvoMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart SlateportEvoMart {
	ITEM_METAL_COAT
	ITEM_KINGS_ROCK
	ITEM_RAZOR_CLAW
	ITEM_RAZOR_FANG
	ITEM_MAGMARIZER
	ITEM_ELECTIRIZER
}

script SlateportCity_EventScript_MailGirl {
	lock
	faceplayer
    msgbox(format("Hi! I sell stationary!"))
	waitmessage
	pokemart(SlateportMailMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart SlateportMailMart {
	ITEM_ORANGE_MAIL
	ITEM_HARBOR_MAIL
	ITEM_GLITTER_MAIL
	ITEM_MECH_MAIL
	ITEM_WOOD_MAIL
	ITEM_WAVE_MAIL
	ITEM_BEAD_MAIL
	ITEM_SHADOW_MAIL
	ITEM_TROPIC_MAIL
	ITEM_DREAM_MAIL
	ITEM_FAB_MAIL
	ITEM_RETRO_MAIL
}

script SlateportCity_EventScript_ScottsHouseSign {
	msgbox(format("Scott's House\p“Building the future of Pokémon \nbattles!”"), MSGBOX_SIGN)
	end
}

raw `
@ Note: LOCALID_SLATEPORT_ENERGY_GURU is a local id for this map used elsewhere. It's defined in event_objects.h

SlateportCity_EventScript_EnergyGuru::
	lock
	faceplayer
	message SlateportCity_Text_EnergyGuruSellWhatYouNeed
	waitmessage
	pokemart SlateportCity_Pokemart_EnergyGuru
	msgbox gText_PleaseComeAgain, MSGBOX_DEFAULT
	release
	end

	.align 2
SlateportCity_Pokemart_EnergyGuru:
	.2byte ITEM_PROTEIN
	.2byte ITEM_IRON
	.2byte ITEM_CARBOS
	.2byte ITEM_ZINC
	.2byte ITEM_CALCIUM
	.2byte ITEM_HP_UP
	.2byte ITEM_NONE
	release
	end

SlateportCity_EventScript_EffortRibbonWoman::
	lock
	faceplayer
	bufferleadmonspeciesname STR_VAR_1
	msgbox SlateportCity_Text_OhYourPokemon, MSGBOX_DEFAULT
	specialvar VAR_RESULT, LeadMonHasEffortRibbon
	call_if_eq VAR_RESULT, TRUE, SlateportCity_EventScript_MonHasEffortRibbon
	specialvar VAR_RESULT, Special_AreLeadMonEVsMaxedOut
	call_if_eq VAR_RESULT, FALSE, SlateportCity_EventScript_MonEVsNotMaxed
	msgbox SlateportCity_Text_PleaseGiveItThisEffortRibbon, MSGBOX_DEFAULT
	playfanfare MUS_OBTAIN_ITEM
	message SlateportCity_Text_ReceivedEffortRibbon
	waitfanfare
	msgbox SlateportCity_Text_PutEffortRibbonOnMon, MSGBOX_DEFAULT
	special GiveLeadMonEffortRibbon
	release
	end

SlateportCity_EventScript_MonEVsNotMaxed::
	msgbox SlateportCity_Text_GoForItLittleHarder, MSGBOX_DEFAULT
	release
	end

SlateportCity_EventScript_MonHasEffortRibbon::
	msgbox SlateportCity_Text_EffortRibbonLooksGoodOnIt, MSGBOX_DEFAULT
	release
	end

SlateportCity_EventScript_PokemonFanClubSign::
	msgbox SlateportCity_Text_PokemonFanClubSign, MSGBOX_SIGN
	end

SlateportCity_EventScript_OceanicMuseumSign::
	msgbox SlateportCity_Text_OceanicMuseumSign, MSGBOX_SIGN
	end

SlateportCity_EventScript_CitySign::
	msgbox SlateportCity_Text_CitySign, MSGBOX_SIGN
	end

SlateportCity_EventScript_MarketSign::
	msgbox SlateportCity_Text_MarketSign, MSGBOX_SIGN
	end

SlateportCity_EventScript_BerryPowderClerk::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_POWDER_JAR, SlateportCity_EventScript_ReceivedPowderJar
	msgbox SlateportCity_Text_ExplainBerries, MSGBOX_DEFAULT
	giveitem ITEM_POWDER_JAR
	setflag FLAG_RECEIVED_POWDER_JAR
	msgbox SlateportCity_Text_ExplainBerryPowder, MSGBOX_DEFAULT
	release
	end

SlateportCity_EventScript_ReceivedPowderJar::
	setvar VAR_0x8004, 1
	specialvar VAR_RESULT, HasEnoughBerryPowder
	goto_if_eq VAR_RESULT, FALSE, SlateportCity_EventScript_ExplainBerryPowder
	msgbox SlateportCity_Text_BroughtMeSomeBerryPowder, MSGBOX_DEFAULT
	special DisplayBerryPowderVendorMenu
	goto SlateportCity_EventScript_ChooseBerryPowderItem
	end

SlateportCity_EventScript_ExplainBerryPowder::
	msgbox SlateportCity_Text_ExplainBerryPowder, MSGBOX_DEFAULT
	release
	end

SlateportCity_EventScript_ChooseBerryPowderItem::
	message SlateportCity_Text_ExchangeWhatWithIt
	waitmessage
	setvar VAR_0x8004, SCROLL_MULTI_BERRY_POWDER_VENDOR
	special ShowScrollableMultichoice
	waitstate
	switch VAR_RESULT
	case 0, SlateportCity_EventScript_EnergyPowder
	case 1, SlateportCity_EventScript_EnergyRoot
	case 2, SlateportCity_EventScript_HealPowder
	case 3, SlateportCity_EventScript_RevivalHerb
	case 4, SlateportCity_EventScript_Protein
	case 5, SlateportCity_EventScript_Iron
	case 6, SlateportCity_EventScript_Carbos
	case 7, SlateportCity_EventScript_Calcium
	case 8, SlateportCity_EventScript_Zinc
	case 9, SlateportCity_EventScript_HPUp
	case 10, SlateportCity_EventScript_PPUp
	case 11, SlateportCity_EventScript_CancelPowderItemSelect
	case MULTI_B_PRESSED, SlateportCity_EventScript_CancelPowderItemSelect
	end

SlateportCity_EventScript_EnergyPowder::
	bufferitemname STR_VAR_1, ITEM_ENERGY_POWDER
	setvar VAR_0x8008, ITEM_ENERGY_POWDER
	setvar VAR_0x8009, 50
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_EnergyRoot::
	bufferitemname STR_VAR_1, ITEM_ENERGY_ROOT
	setvar VAR_0x8008, ITEM_ENERGY_ROOT
	setvar VAR_0x8009, 80
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_HealPowder::
	bufferitemname STR_VAR_1, ITEM_HEAL_POWDER
	setvar VAR_0x8008, ITEM_HEAL_POWDER
	setvar VAR_0x8009, 50
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_RevivalHerb::
	bufferitemname STR_VAR_1, ITEM_REVIVAL_HERB
	setvar VAR_0x8008, ITEM_REVIVAL_HERB
	setvar VAR_0x8009, 300
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_Protein::
	bufferitemname STR_VAR_1, ITEM_PROTEIN
	setvar VAR_0x8008, ITEM_PROTEIN
	setvar VAR_0x8009, 1000
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_Iron::
	bufferitemname STR_VAR_1, ITEM_IRON
	setvar VAR_0x8008, ITEM_IRON
	setvar VAR_0x8009, 1000
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_Carbos::
	bufferitemname STR_VAR_1, ITEM_CARBOS
	setvar VAR_0x8008, ITEM_CARBOS
	setvar VAR_0x8009, 1000
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_Calcium::
	bufferitemname STR_VAR_1, ITEM_CALCIUM
	setvar VAR_0x8008, ITEM_CALCIUM
	setvar VAR_0x8009, 1000
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_Zinc::
	bufferitemname STR_VAR_1, ITEM_ZINC
	setvar VAR_0x8008, ITEM_ZINC
	setvar VAR_0x8009, 1000
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_HPUp::
	bufferitemname STR_VAR_1, ITEM_HP_UP
	setvar VAR_0x8008, ITEM_HP_UP
	setvar VAR_0x8009, 1000
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_PPUp::
	bufferitemname STR_VAR_1, ITEM_PP_UP
	setvar VAR_0x8008, ITEM_PP_UP
	setvar VAR_0x8009, 3000
	goto SlateportCity_EventScript_TryBuyBerryPowderItem
	end

SlateportCity_EventScript_CancelPowderItemSelect::
	msgbox SlateportCity_Text_ComeBackToTradeBerryPowder, MSGBOX_DEFAULT
	special RemoveBerryPowderVendorMenu
	release
	end

SlateportCity_EventScript_TryBuyBerryPowderItem::
	msgbox SlateportCity_Text_ExchangeBerryPowderForItem, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, NO, SlateportCity_EventScript_ChooseBerryPowderItem
	copyvar VAR_0x8004, VAR_0x8009
	specialvar VAR_RESULT, HasEnoughBerryPowder
	goto_if_eq VAR_RESULT, FALSE, SlateportCity_EventScript_NotEnoughBerryPowder
	giveitem VAR_0x8008
	goto_if_eq VAR_RESULT, FALSE, SlateportCity_EventScript_NoRoomForBerryPowderItem
	copyvar VAR_0x8004, VAR_0x8009
	special TakeBerryPowder
	special PrintPlayerBerryPowderAmount
	msgbox SlateportCity_Text_FineBerryPowderTradeSomethingElse, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, SlateportCity_EventScript_ChooseBerryPowderItem
	msgbox SlateportCity_Text_WhenYouGetMoreBringItToMe, MSGBOX_DEFAULT
	special RemoveBerryPowderVendorMenu
	release
	end

SlateportCity_EventScript_NoRoomForBerryPowderItem::
	msgbox gText_TheBagIsFull, MSGBOX_DEFAULT
	special RemoveBerryPowderVendorMenu
	release
	end

SlateportCity_EventScript_NotEnoughBerryPowder::
	msgbox SlateportCity_Text_DontHaveEnoughBerryPowder, MSGBOX_DEFAULT
	goto SlateportCity_EventScript_ChooseBerryPowderItem
	end

SlateportCity_EventScript_BerryCrushRankingsSign::
	lockall
	special ShowBerryCrushRankings
	waitstate
	releaseall
	end

SlateportCity_Text_EnergyGuruSellWhatYouNeed:
	.string "I'm the Energy Guru! I'll go for it and\n"
	.string "sell you what you need!$"

SlateportCity_Text_OhYourPokemon:
	.string "Oh?\n"
	.string "Your {STR_VAR_1}…$"

SlateportCity_Text_PleaseGiveItThisEffortRibbon:
	.string "Went for it stupendously!\p"
	.string "As its reward, please give it this\n"
	.string "Effort Ribbon.$"

SlateportCity_Text_ReceivedEffortRibbon:
	.string "{PLAYER} received the Effort Ribbon.$"

SlateportCity_Text_PutEffortRibbonOnMon:
	.string "{PLAYER} put the Effort Ribbon\n"
	.string "on {STR_VAR_1}.$"

SlateportCity_Text_GoForItLittleHarder:
	.string "You have to go for it a little harder.\p"
	.string "If you do, I'll give your Pokémon\n"
	.string "something nice.$"

SlateportCity_Text_EffortRibbonLooksGoodOnIt:
	.string "Oh! Your {STR_VAR_1}, that Effort\n"
	.string "Ribbon looks good on it!$"

SlateportCity_Text_BuyBricksSoDecorWontGetDirty:
	.string "If you put Dolls or Cushions on\n"
	.string "the floor, they'll get dirty.\p"
	.string "I'm going to buy some Bricks so my\n"
	.string "Dolls and Cushions won't get dirty\l"
	.string "when I leave them out.$"

SlateportCity_Text_PokemonFanClubSign:
	.string "Pokémon FAN CLUB\n"
	.string "“Calling all fans of Pokémon!”$"

SlateportCity_Text_OceanicMuseumSign:
	.string "“The endless sea sustains\n"
	.string "all life.”\p"
	.string "Oceanic Museum$"

SlateportCity_Text_CitySign:
	.string "Slateport City\p"
	.string "“The port where people and Pokémon\n"
	.string "cross paths.”$"

SlateportCity_Text_MarketSign:
	.string "SLATEPORT MARKET\n"
	.string "“Unique items found nowhere else!”$"

SlateportCity_Text_ExplainBerries:
	.string "If a wild Pokémon gets hurt, it heals\n"
	.string "itself by chewing on Berries.\p"
	.string "Did you know that?\p"
	.string "Somebody saw that and got the idea\n"
	.string "of making medicine from Berries.\p"
	.string "Did you know that?\p"
	.string "To turn Berries into medicine,\n"
	.string "they must be crushed into powder.\p"
	.string "Did you know that?\p"
	.string "You seem to have an interest\n"
	.string "in Berries!\p"
	.string "I've got something good for someone\n"
	.string "like you.$"

SlateportCity_Text_ExplainBerryPowder:
	.string "I recently had machines installed at\n"
	.string "Pokémon Centers for crushing Berries.\p"
	.string "The Berry Crush machines are at\n"
	.string "the Direct Corner upstairs.\p"
	.string "Did you know?\p"
	.string "Could you make Berry Powder for\n"
	.string "me using those machines?\p"
	.string "I can make you all sorts of medicine\n"
	.string "if you bring me lots of Berry Powder.$"

SlateportCity_Text_BroughtMeSomeBerryPowder:
	.string "Have you brought me some\n"
	.string "Berry Powder?$"

SlateportCity_Text_ExchangeWhatWithIt:
	.string "What would you like to exchange\n"
	.string "it with?$"

SlateportCity_Text_ExchangeBerryPowderForItem:
	.string "Okay, you want to exchange your\n"
	.string "Berry Powder for one {STR_VAR_1}?$"

SlateportCity_Text_DontHaveEnoughBerryPowder:
	.string "Oh, dear. You don't have enough\n"
	.string "Berry Powder.$"

SlateportCity_Text_FineBerryPowderTradeSomethingElse:
	.string "This is fine Berry Powder.\n"
	.string "It will make excellent medicine.\p"
	.string "Would you like to trade more of your\n"
	.string "Berry Powder for something else?$"

SlateportCity_Text_WhenYouGetMoreBringItToMe:
	.string "Okay! When you get some more\n"
	.string "Berry Powder, bring it to me, please!$"

SlateportCity_Text_ComeBackToTradeBerryPowder:
	.string "Come back if you'd like to trade your\n"
	.string "Berry Powder for some medicine.\p"
	.string "I'm always running a bazaar here.\p"
	.string "Did you know?$"
`
