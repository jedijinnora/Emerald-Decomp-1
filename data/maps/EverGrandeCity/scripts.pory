//Object IDs
const LOCALID_JINNORA = 25
const LOCALID_AIPOM = 26

//TEMPs used
//FLAG_TEMP_1 Sailor intro
//FLAG_TEMP_2 TuberM intro
//FLAG_TEMP_3 and FLAG_TEMP_4 Gentleman conversation
//FLAG_TEMP_5 Maniac intro

//FLAG_TEMP_14 Jinnora hide flag

//FLAG_TEMP_1E mainland gyms check
//FLAG_TEMP_1F Surf check

//VAR_TEMP_1 store current mapsec during ferry script
//VAR_TEMP_3 E4 Girl


mapscripts EverGrandeCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : EverGrandeCity_OnTransition
}

script EverGrandeCity_OnTransition {
	call_if_unset(FLAG_VISITED_EVER_GRANDE_CITY, EverGrandeCity_EventScript_VisitEverGrande)
	call_if_gt(VAR_EVER_GRANDE_CITY_STATE, 1, EverGrandeCity_EventScript_HideJinnora)
	end
}

script EverGrandeCity_EventScript_VisitEverGrande {
	setflag(FLAG_VISITED_EVER_GRANDE_CITY)
	//addvar(VAR_CITIES_VISITED, 1)
	return
}

script EverGrandeCity_EventScript_HideJinnora {
	setflag(FLAG_TEMP_14)
	return
}

script EverGrandeCity_EventScript_Jinnora {
	lock
	faceplayer
	msgbox(format("Hey there! I'm Jinnora, the creator!"))
	msgbox(format("Jinnora: I'm here as a plot device to let you skip the rest of the intro if you want?"))
	dynmultipush("No skip wanted", 10)
	dynmultipush("Skip to ferry departure", 1)
	dynmultipush("Skip to difficulty/challenge selection", 2)
	dynmultistack(0,0,TRUE,6,FALSE,0,DYN_MULTICHOICE_CB_NONE)
	switch(var(VAR_RESULT)) {
		case 1:
			call(EverGrandeCity_EventScript_OfferAllStartingItems)
			setvar(VAR_EVER_GRANDE_CITY_STATE, 20)
		case 2:
			call(EverGrandeCity_EventScript_OfferAllStartingItems)
			warp(MAP_EVER_GRANDE_CITY_HEADQUARTERS_1F, 4)
			setvar(VAR_EVER_GRANDE_CITY_STATE, 20)
			release
			end
		default: 
	}
	goto(EverGrandeCity_EventScript_JinnoraLeave)
}

script EverGrandeCity_EventScript_OfferAllStartingItems {
	additem(ITEM_POKE_BALL, 30)
	additem(ITEM_DREAM_BALL, 10)
    additem(ITEM_EXP_SHARE)
	additem(ITEM_CANDY_JAR)
	setflag(FLAG_SYS_POKENAV_GET)
	msgbox(format("Would you me to give you all the items available at this point?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, Common_EventScript_NopReturn)
	playfanfare(MUS_OBTAIN_ITEM)
	if(!flag(FLAG_HIDDEN_ITEM_ARRIVAL_FERRY_ROOM_FRESH_WATER)) {
		additem(ITEM_FRESH_WATER)
		setflag(FLAG_HIDDEN_ITEM_ARRIVAL_FERRY_ROOM_FRESH_WATER)
	}
	if(!flag(FLAG_GIVEN_ARRIVAL_FERRY_LEMONADE)) {
		additem(ITEM_LEMONADE)
		setflag(FLAG_GIVEN_ARRIVAL_FERRY_LEMONADE)
	}
	if(!flag(FLAG_ITEM_EVER_GRANDE_CITY_HEADQUARTERS_3F_LINKING_CORD)) {
		additem(ITEM_LINKING_CORD)
		setflag(FLAG_ITEM_EVER_GRANDE_CITY_HEADQUARTERS_3F_LINKING_CORD)
	}
	if(!flag(FLAG_HIDDEN_ITEM_EVER_GRANDE_CITY_STARDUST)) {
		additem(ITEM_STARDUST)
		setflag(FLAG_HIDDEN_ITEM_EVER_GRANDE_CITY_STARDUST)
	}
	if(!flag(FLAG_GIVEN_POWER_ITEMS)) {
		additem(ITEM_POWER_WEIGHT)
		additem(ITEM_POWER_BRACER)
		additem(ITEM_POWER_BELT)
		additem(ITEM_POWER_LENS)
		additem(ITEM_POWER_BAND)
		additem(ITEM_POWER_ANKLET)
		setflag(FLAG_GIVEN_POWER_ITEMS)
	}
	if(!flag(FLAG_RECEIVED_WAILMER_PAIL)) {
		additem(ITEM_WAILMER_PAIL)
		setflag(FLAG_RECEIVED_WAILMER_PAIL)
	}
	checkitem(ITEM_OLD_ROD)
	if(var(VAR_RESULT) == FALSE) {
		additem(ITEM_OLD_ROD)
	}
	//why the heck not, save them some farming
	additem(ITEM_CHERI_BERRY, 99)
	additem(ITEM_CHESTO_BERRY, 99)
	additem(ITEM_PECHA_BERRY, 99)
	additem(ITEM_RAWST_BERRY, 99)
	additem(ITEM_ASPEAR_BERRY, 99)
	additem(ITEM_ORAN_BERRY, 99)
	additem(ITEM_PERSIM_BERRY, 99)
	setflag(FLAG_RECEIVED_EGC_BONUS_BERRIES)
	return
}

script EverGrandeCity_EventScript_JinnoraLeave {
	msgbox(format("Thanks for playing! Be seeing you!"))
	waitmessage
	fadescreen(FADE_TO_BLACK)
	removeobject(LOCALID_JINNORA)
	setflag(FLAG_TEMP_14)
	release
	fadescreen(FADE_FROM_BLACK)
	end
}

script EverGrandeCity_EventScript_Aipom {
	lock
	faceplayer
	waitse
	playmoncry(SPECIES_AIPOM, CRY_MODE_ENCOUNTER)
	msgbox(format("Aipom: Pom pom!"))
	giveitem(ITEM_CHERI_BERRY, 99)
	giveitem(ITEM_CHESTO_BERRY, 99)
	giveitem(ITEM_PECHA_BERRY, 99)
	giveitem(ITEM_RAWST_BERRY, 99)
	giveitem(ITEM_ASPEAR_BERRY, 99)
	giveitem(ITEM_ORAN_BERRY, 99)
	giveitem(ITEM_PERSIM_BERRY, 99)
	setflag(FLAG_RECEIVED_EGC_BONUS_BERRIES)
	applymovement(LOCALID_AIPOM, KecleonVanish)
	waitmovement(0)
	release
	end
}

script EverGrandeCity_EventScript_CaveBlocked {
	lock
	faceplayer
	msgbox(format("I'm sorry, this shortcut to the Pokemon League is for administrative staff. You'll have to become Champion first."))
	release
	end
}

script EverGrandeCity_EventScript_Ferry {
	lock
	faceplayer
	goto_if_gt(VAR_EVER_GRANDE_CITY_STATE, 11, Common_EventScript_Ferry)
	goto_if_set(FLAG_TEMP_1, BuildingDirections)
	msgbox(format("Ahoy, {PLAYER}! The ferry won't leave for a while. Explore the city and make sure you are all squared away for your journey!"))
	setflag(FLAG_TEMP_1)
	BuildingDirections:
	msgbox(format("Check-in is at the big building north of the plaza. You can't miss it!"))
	release
	end
}

script EverGrandeCity_EventScript_TuberM {
	lock
	faceplayer
	if (flag(FLAG_TEMP_2)) {
		msgbox(format("Actually, there's an intersection of four maps behind me. I'm protecting your game from the extra graphics load. You should be grateful!"))
		clearflag(FLAG_TEMP_2)
		release
		end
	}
	msgbox(format("This is my section of the beach. No big kids allowed!"))
	setflag(FLAG_TEMP_2)
	release
	end
}

script EverGrandeCity_EventScript_Beauty {
	lock
	faceplayer
	msgbox(format("I love the beach. The view is pretty, and the sand feels soothing on my bare feet."))
	msgbox(format("When I was a little girl I used to spend hours collecting pretty shells and digging for treasure."))
	msgbox(format("I think it's sad that as we grow older we lose the wonder and excitement that filled our lives as children."))
	msgbox(format("Now I come here every day after work to remind myself that all my adult worries are small compared to the magnificence of the world."))
	release
	end
}

script EverGrandeCity_EventScript_TuberF {
	lock
	faceplayer
	msgbox(format("My brother has his stupid section of the beach. I've got this awesome rock!"))
	release
	end
}

script EverGrandeCity_EventScript_Painter {
	lock
	msgbox(format("Oops!"))
	msgbox(format("Oh well, another happy tree."))
	release
	end
}

script EverGrandeCity_EventScript_Lass {
	lock
	faceplayer
	msgbox(format("Living in Ever Grande City means I get to catch a lot of glimpses of the Elite Four."))
	if(var(VAR_TEMP_3) > 4) {
		setvar(VAR_TEMP_1, 0)
	}
	switch(var(VAR_TEMP_3)) {
		case 0:
			msgbox(format("Brandon has a lot of strong Pokémon from all the ancient ruins he's explored."))
		case 1:
			msgbox(format("I think Lucy is hot, but her style is a bit too edgy for my taste."))
		case 2:
			msgbox(format("Glacia sometimes visits the beach during the summer and has her Pokémon ice things up for a bit. The kids love it!"))
		case 3:
			msgbox(format("Drake intimidates me, honestly. He's a crusty old sailor from the Draconid tribes, and the strongest after the Champion."))
		default:
			msgbox(format("I don't see Champion Anabel much at all. She usually looks bored."))
	}
	addvar(VAR_TEMP_3, 1)
	release
	end
}

script EverGrandeCity_EventScript_Gentleman {
	lock
	faceplayer
	goto_if_set(FLAG_TEMP_3, Harrumph)
	goto_if_set(FLAG_TEMP_4, Archipelago)
	msgbox(format("Er-hem! Do you know who I am?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == NO) {
		setflag(FLAG_TEMP_3)
		Harrumph:
        msgbox(format("Hmph. Kids these days. Go fight a Gym Leader or something."))
        release
        end
    } else {
		setflag(FLAG_TEMP_4)
		msgbox(format("That's right! I used to set energy policy throughout the Hoenn Archipelago. Hah! Still got it!"))
		release
		end
	}
	Archipelago:
	msgbox(format("The Hoenn Archipelago refers to the islands to the east of mainland Hoenn."))
	msgbox(format("That includes Mossdeep, Sootopolis, Ever Grande, Pacifidlog, and a whole bundle of places with no permanent settlements."))
	msgbox(format("Want to start a fight in a boring meeting? Ask an intern whether Dewford and the southern islands should be grouped in the Archipelago!"))
	msgbox(format("Ah, the Director hated my shenanigans."))
	release
	end
}

script EverGrandeCity_EventScript_Fisherman {
	lock
	faceplayer
	checkitem(ITEM_OLD_ROD)
	if(var(VAR_RESULT)) {
		goto(GoodRodCheck)
	}

	msgbox(format("A promising Trainer! Tell me, do you like to fish?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == NO) {
		msgbox(format("A shame, but I suppose fishing isn't for everyone."))
		release
		end
	}
	msgbox(format("Excellent! Here's a little something to get you started!"))
	giveitem(ITEM_OLD_ROD)
	waitmessage
	release
	end

	GoodRodCheck:
	checkitem(ITEM_GOOD_ROD)
	if(var(VAR_RESULT)) {
		goto(SuperRodCheck)
	}
	msgbox(format("If you want a better fishing rod, my younger brother hangs around the river east of Mauville City."))
	release
	end

	SuperRodCheck:
	checkitem(ITEM_SUPER_ROD)
	if(var(VAR_RESULT)) {
		msgbox(format("Hey {PLAYER}! How are the fish biting?"))
		release
		end
	}
	msgbox(format("If you're looking to upgrade to the ultimate fishing rod, seek out my older brother in Mossdeep!"))
	release
	end
}

script EverGrandeCity_EventScript_SchoolKidM {
	lock
	faceplayer
	msgbox(format("They say it's good luck to catch a Pokémon here before challenging the Elite Four."))
	msgbox(format("There's even a whole superstition around which Pokémon are lucky or unlucky!"))
	msgbox(format("Finding an Absol means bad luck in your future, but I think that's ridiculous! Absol are cool! That's way better than any lame Xatu or Oricorio."))
	release
	end
}

script EverGrandeCity_EventScript_Mom {
	lock
	faceplayer
	msgbox(format("My kids make everything a competition."))
	msgbox(format("I hope when they get old enough to challenge the gym circuit they will drive each other to greater heights with that competitiveness."))
	msgbox(format("Until then, I'm stuck here dealing with every tantrum and argument."))
	release
	end
}

script EverGrandeCity_EventScript_Hiker {
	lock
	faceplayer
	goto_if_set(FLAG_GIVEN_POWER_ITEMS, WinstrateHint)
	msgbox(format("A full League challenge? In this economy? You'll want these!"))
	setflag(FLAG_GIVEN_POWER_ITEMS)
	giveitem(ITEM_POWER_WEIGHT)
	giveitem(ITEM_POWER_BRACER)
	giveitem(ITEM_POWER_BELT)
	giveitem(ITEM_POWER_LENS)
	giveitem(ITEM_POWER_BAND)
	giveitem(ITEM_POWER_ANKLET)
	msgbox(format("Power items can be held by Pokémon to influence the Effort Values they gain in battle."))
	WinstrateHint:
	msgbox(format("I hand out one free set of power items to newbie trainers. If you want more, the Winstrate family sells them. They live north of Mauville."))
	release
	end
}

script EverGrandeCity_EventScript_Maniac {
	lock
	faceplayer
	if(flag(FLAG_TEMP_5)) {
		goto(GoodLuck)
	}
	setflag(FLAG_TEMP_5)
	msgbox(format("Unknown trainer spotted! Foreign accent detected! Conclusion: you're here to challenge the Hoenn League!"))
	GoodLuck:
	msgbox(format("Good luck, stranger! You'll need it."))
	release
	end
}

script EverGrandeCity_EventScript_CitySign {
	lock
	msgbox(format("Ever Grande City\n“Flowering heart of the Hoenn region.”"))
	release
	end
}

script EverGrandeCity_EventScript_PlazaSign {
	lock
	msgbox(format("Ever Grande City\nLeague Plaza"))
	release
	end
}

script EverGrandeCity_EventScript_CaveSign {
	lock
	msgbox(format("Champion's Cut \n Access to Pokémon League"))
	release
	end
}

//Common ferry script for use at all terminals
script Common_EventScript_Ferry {
	lock
	faceplayer
	checkitem(ITEM_HM03_SURF)
	if(var(VAR_RESULT)) {
		//if the player has surf, allow any previously visited destination
		setflag(FLAG_TEMP_1F)
	} 
	//check map and count badges
	specialvar(VAR_RESULT, GetCurrentMapSec)
	copyvar(VAR_TEMP_1, VAR_RESULT) //mapsec stored in temp 1
	call(Common_EventScript_CountBadges)

	//check if in Ever Grande and need to go teleport to Victory Road
	if(flag(FLAG_CHALLENGE_HM_NONE) && var(VAR_NUM_BADGES) > 7 && !flag(FLAG_SYS_GAME_CLEAR) && var(VAR_TEMP_1) == 0x0F) {
		msgbox(format("If you're ready to go to Emerald Cape, the warp statue in the Pokémon Center has been unlocked!"))
		goto_if_eq(VAR_DIFFICULTY_SETTING, 2, Common_EventScript_FerryEnd)
	}

	//check ticket
	call_if_eq(VAR_DIFFICULTY_SETTING, 2, Common_EventScript_CheckFerryTicket)
	if(var(VAR_TEMP_1) == 0x0E) {
		//sootopolis city has a different intro
		msgbox(format("Yo, {PLAYER}! The ferry can't reach inside the caldera, but we can take you by Flying Pokémon!"))
	} else {
		msgbox(format("Ahoy, {PLAYER}! The ferry is ready to take you to your next destination!"))
	}

	//check mainland gyms
	//rustboro, mauville, lavaridge, petalburg, fortree
	if (flag(FLAG_BADGE01_GET) && flag(FLAG_BADGE03_GET) && flag(FLAG_BADGE04_GET) && flag(FLAG_BADGE05_GET) && flag(FLAG_BADGE06_GET)) {
		setflag(FLAG_TEMP_1E) //all mainland gyms defeated, must go to islands
	}
	
	if (var(VAR_TEMP_1) != 0x0A 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2) 
	    || flag(FLAG_SYS_GAME_CLEAR)
	  	|| !flag(FLAG_TEMP_1E) 
		|| (flag(FLAG_TEMP_1F) && flag(FLAG_VISITED_RUSTBORO_CITY)))) 
	{
		dynmultipush("Rustboro", 0)
	}

	if (var(VAR_TEMP_1) != 0x02 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2)
	  	|| flag(FLAG_SYS_GAME_CLEAR)
	  	|| !flag(FLAG_BADGE02_GET) 
		|| (flag(FLAG_TEMP_1F) && flag(FLAG_VISITED_DEWFORD_TOWN)))) 
	{
		dynmultipush("Dewford", 1)
	}

	if (var(VAR_TEMP_1) != 0x4B 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2)
	    || flag(FLAG_SYS_GAME_CLEAR)
	  	|| !flag(FLAG_TEMP_1E) 
	  	|| (flag(FLAG_TEMP_1F) && flag(FLAG_VISITED_LITTLEROOT_TOWN))))
	{
		dynmultipush("Littleroot", 2)
	}

	if (var(VAR_TEMP_1) != 0x08 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2)
	  	|| flag(FLAG_SYS_GAME_CLEAR)
	    || !flag(FLAG_TEMP_1E) 
	    || (flag(FLAG_TEMP_1F) && flag(FLAG_VISITED_SLATEPORT_CITY))))
	{
		dynmultipush("Slateport", 3)
	}

	if (var(VAR_TEMP_1) != 0x0C 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2)
	  	|| flag(FLAG_SYS_GAME_CLEAR)
	    || !flag(FLAG_TEMP_1E) 
		|| (flag(FLAG_TEMP_1F) && flag(FLAG_VISITED_LILYCOVE_CITY))))
	{
		dynmultipush("Lilycove", 4)
	}

	if (var(VAR_TEMP_1) != 0x0D 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2)
	  	|| flag(FLAG_SYS_GAME_CLEAR)
	    || !flag(FLAG_BADGE07_GET) 
		|| (flag(FLAG_TEMP_1F) && flag(FLAG_VISITED_MOSSDEEP_CITY))))
	{
		dynmultipush("Mossdeep", 5)
	}

	if (var(VAR_TEMP_1) != 0x0E 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2)
	  	|| flag(FLAG_SYS_GAME_CLEAR)
	    || !flag(FLAG_BADGE08_GET) 
	    || (flag(FLAG_TEMP_1F) && flag(FLAG_VISITED_SOOTOPOLIS_CITY))))
	{
		dynmultipush("Sootopolis", 6)
	}

	if (var(VAR_TEMP_1) != 0x0F 
	  && ((var(VAR_DIFFICULTY_SETTING) != 2)
	  	|| flag(FLAG_SYS_GAME_CLEAR)
	    || (var(VAR_NUM_BADGES) > 7) 
		|| flag(FLAG_TEMP_1F))) 
	{
		dynmultipush("Ever Grande", 7)
	}

	//finished all checks, do menu choice
	msgbox(format("Where would you like to go?"))
	dynmultistack(0,0,FALSE,6,TRUE,0,DYN_MULTICHOICE_CB_NONE)
	goto_if_eq(VAR_RESULT, 127, Common_EventScript_FerryEnd)
	copyvar(VAR_FERRY_DESTINATION, VAR_RESULT)

	//use ticket
	call_if_set(FLAG_DEPARTED_EVER_GRANDE, Common_EventScript_UseFerryTicket)
	
	//warp to new map, set heal spawn to prevent locking
	switch(var(VAR_FERRY_DESTINATION)) {
		case 0:
			warp(MAP_RUSTBORO_CITY, 13)
			setrespawn(HEAL_LOCATION_RUSTBORO_CITY)
		case 1:
			warp(MAP_DEWFORD_RESORT, 6)
			setrespawn(HEAL_LOCATION_DEWFORD_TOWN)
		case 2:
			warp(MAP_LITTLEROOT_SHORE, 1)
			setrespawn(HEAL_LOCATION_LITTLEROOT_TOWN_MAYS_HOUSE)
		case 3:
			warp(MAP_SLATEPORT_CITY, 9)
			setrespawn(HEAL_LOCATION_SLATEPORT_CITY)
		case 4:
			warp(MAP_LILYCOVE_CITY, 13)
			setrespawn(HEAL_LOCATION_LILYCOVE_CITY)
		case 5:
			warp(MAP_MOSSDEEP_CITY, 13)
			setrespawn(HEAL_LOCATION_MOSSDEEP_CITY)
		case 6:
			warp(MAP_SOOTOPOLIS_CITY, 14)
			setrespawn(HEAL_LOCATION_SOOTOPOLIS_CITY)
		case 7:
			warp(MAP_EVER_GRANDE_CITY, 10)
			setrespawn(HEAL_LOCATION_EVER_GRANDE_CITY)
		default:
			Common_EventScript_FerryEnd:
			msgbox(format("See you later!"))
			release
			end
	}
	setflag(FLAG_DEPARTED_EVER_GRANDE)
	call_if_eq(VAR_DIFFICULTY_SETTING, 2, Common_EventScript_RandomizeBlockerRivalCounter)
	release
	end
}

script Common_EventScript_RandomizeBlockerRivalCounter {
	random(2)
	copyvar(VAR_CITIES_VISITED, VAR_RESULT)
	return
}

script Common_EventScript_CheckFerryTicket {
	checkitem(ITEM_SS_TICKET)
	goto_if_eq(VAR_RESULT, TRUE, Common_EventScript_NopReturn)
	goto_if_unset(FLAG_DEPARTED_EVER_GRANDE, Common_EventScript_NopReturn) //can always leave Ever Grande
	msgbox(format("Sorry, {PLAYER}! You need to earn a new ferry ticket by beating a Gym Leader!"))
	release
	end
}

script Common_EventScript_UseFerryTicket {
	goto_if_ne(VAR_DIFFICULTY_SETTING, 2, Common_EventScript_NopReturn)
	removeitem(ITEM_SS_TICKET)
	return
}
