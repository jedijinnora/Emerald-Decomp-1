mapscripts EverGrandeCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : EverGrandeCity_OnTransition
}

script EverGrandeCity_OnTransition {
	call_if_unset(FLAG_VISITED_EVER_GRANDE_CITY, EverGrandeCity_EventScript_VisitEverGrande)
	call_if_gt(VAR_EVER_GRANDE_CITY_STATE, 1, EverGrandeCity_EventScript_HideJinnora)
	end
}

script EverGrandeCity_EventScript_VisitEverGrande {
	setflag(FLAG_VISITED_EVER_GRANDE_CITY)
	//addvar(VAR_CITIES_VISITED, 1)
	return
}

script EverGrandeCity_EventScript_HideJinnora {
	setflag(FLAG_TEMP_14)
	return
}

//Object IDs
const LOCALID_JINNORA = 25

//TEMPs used
//FLAG_TEMP_1 Sailor intro
//FLAG_TEMP_2 TuberM intro
//FLAG_TEMP_3 and FLAG_TEMP_4 Gentleman conversation
//FLAG_TEMP_5 Maniac intro

//FLAG_TEMP_14 Jinnora hide flag

//FLAG_TEMP_1E mainland gyms check
//FLAG_TEMP_1F Surf check


script EverGrandeCity_EventScript_Jinnora {
	lock
	faceplayer
	msgbox(format("Jinnora: Hey there! I'm Jinnora, the creator of Pokémon Emerald Beryl!"))
	msgbox(format("I'm here as a plot device to let you skip the rest of the intro if you want?"))
	dynmultipush("No skip wanted", 10)
	dynmultipush("Skip to ferry departure", 1)
	dynmultipush("Skip to difficulty/challenge selection", 2)
	dynmultistack(0,0,TRUE,6,FALSE,0,DYN_MULTICHOICE_CB_NONE)
	switch(var(VAR_RESULT)) {
		case 1:
			call(EverGrandeCity_EventScript_OfferAllStartingItems)
			setvar(VAR_EVER_GRANDE_CITY_STATE, 20)
		case 2:
			call(EverGrandeCity_EventScript_OfferAllStartingItems)
			warp(MAP_EVER_GRANDE_CITY_HEADQUARTERS_1F, 4)
			setvar(VAR_EVER_GRANDE_CITY_STATE, 20)
			release
			end
		default: 
	}
	goto(EverGrandeCity_EventScript_JinnoraLeave)
}

script EverGrandeCity_EventScript_OfferAllStartingItems {
	additem(ITEM_POKE_BALL, 30)
	additem(ITEM_DREAM_BALL, 10)
    additem(ITEM_EXP_SHARE)
	setflag(FLAG_SYS_POKENAV_GET)
	//todo infinite candy
	msgbox(format("Would you me to give you all the items available at this point?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, Common_EventScript_NopReturn)
	playfanfare(MUS_OBTAIN_ITEM)
	if(!flag(FLAG_HIDDEN_ITEM_ARRIVAL_FERRY_ROOM_FRESH_WATER)) {
		additem(ITEM_FRESH_WATER)
		setflag(FLAG_HIDDEN_ITEM_ARRIVAL_FERRY_ROOM_FRESH_WATER)
	}
	if(!flag(FLAG_GIVEN_ARRIVAL_FERRY_LEMONADE)) {
		additem(ITEM_LEMONADE)
		setflag(FLAG_GIVEN_ARRIVAL_FERRY_LEMONADE)
	}
	if(!flag(FLAG_ITEM_EVER_GRANDE_CITY_HEADQUARTERS_3F_LINKING_CORD)) {
		additem(ITEM_LINKING_CORD)
		setflag(FLAG_ITEM_EVER_GRANDE_CITY_HEADQUARTERS_3F_LINKING_CORD)
	}
	if(!flag(FLAG_HIDDEN_ITEM_EVER_GRANDE_CITY_STARDUST)) {
		additem(ITEM_STARDUST)
		setflag(FLAG_HIDDEN_ITEM_EVER_GRANDE_CITY_STARDUST)
	}
	if(!flag(FLAG_GIVEN_POWER_ITEMS)) {
		additem(ITEM_POWER_WEIGHT)
		additem(ITEM_POWER_BRACER)
		additem(ITEM_POWER_BELT)
		additem(ITEM_POWER_LENS)
		additem(ITEM_POWER_BAND)
		additem(ITEM_POWER_ANKLET)
		setflag(FLAG_GIVEN_POWER_ITEMS)
	}
	if(!flag(FLAG_RECEIVED_WAILMER_PAIL)) {
		additem(ITEM_WAILMER_PAIL)
		setflag(FLAG_RECEIVED_WAILMER_PAIL)
	}
	checkitem(ITEM_OLD_ROD)
	if(var(VAR_RESULT) == FALSE) {
		additem(ITEM_OLD_ROD)
	}
	//why the heck not, save them some farming
	additem(ITEM_POMEG_BERRY, 99)
	additem(ITEM_KELPSY_BERRY, 99)
	additem(ITEM_QUALOT_BERRY, 99)
	additem(ITEM_HONDEW_BERRY, 99)
	additem(ITEM_GREPA_BERRY, 99)
	additem(ITEM_TAMATO_BERRY, 99)
	return
}

script EverGrandeCity_EventScript_JinnoraLeave {
	msgbox(format("Thanks for playing! Be seeing you!"))
	waitmessage
	fadescreen(FADE_TO_BLACK)
	removeobject(LOCALID_JINNORA)
	setflag(FLAG_TEMP_14)
	release
	fadescreen(FADE_FROM_BLACK)
	end
}

script EverGrandeCity_EventScript_CaveBlocked {
	lock
	faceplayer
	msgbox(format("I'm sorry, this shortcut to the Pokemon League is for administrative staff. You'll have to become Champion first."))
	release
	end
}

script EverGrandeCity_EventScript_Ferry {
	lock
	faceplayer
	goto_if_gt(VAR_EVER_GRANDE_CITY_STATE, 11, Common_EventScript_Ferry)
	goto_if_set(FLAG_TEMP_1, BuildingDirections)
	msgbox(format("Ahoy, {PLAYER}! The ferry won't leave for a while. Explore the city and make sure you are all squared away for your journey!"))
	setflag(FLAG_TEMP_1)
	BuildingDirections:
	msgbox(format("Check-in is at the big building north of the plaza. You can't miss it!"))
	release
	end
}

script EverGrandeCity_EventScript_TuberM {
	lock
	faceplayer
	if (flag(FLAG_TEMP_2)) {
		msgbox(format("Actually, there's an intersection of four maps behind me. I'm protecting your game from the extra graphics load. You should be grateful!"))
		clearflag(FLAG_TEMP_2)
		release
		end
	}
	msgbox(format("This is my section of the beach. No big kids allowed!"))
	setflag(FLAG_TEMP_2)
	release
	end
}

script EverGrandeCity_EventScript_Beauty {
	lock
	faceplayer
	msgbox(format("I love the beach. The view is pretty, and the sand feels soothing on my bare feet."))
	msgbox(format("When I was a little girl I used to spend hours collecting pretty shells and digging for treasure."))
	msgbox(format("I think it's sad that as we grow older we lose the wonder and excitement that filled our lives as children."))
	msgbox(format("Now I come here every day after work to remind myself that all my adult worries are small compared to the magnificence of the world."))
	release
	end
}

script EverGrandeCity_EventScript_TuberF {
	lock
	faceplayer
	msgbox(format("My brother has his stupid section of the beach. I've got this awesome rock!"))
	release
	end
}

script EverGrandeCity_EventScript_Painter {
	lock
	msgbox(format("Oops!"))
	msgbox(format("Oh well, another happy tree."))
	release
	end
}

script EverGrandeCity_EventScript_Lass {
	lock
	faceplayer
	msgbox(format("Living in Ever Grande City means I get to catch a lot of glimpses of the Elite Four."))
	random(5)
	switch(var(VAR_RESULT)) {
		case 0:
			msgbox(format("Brandon has a lot of strong Pokémon from all the ancient ruins he's explored."))
		case 1:
			msgbox(format("I think Lucy is hot, but her style is a bit too edgy for my taste."))
		case 2:
			msgbox(format("Glacia sometimes visits the beach during the summer and has her Pokémon ice things up for a bit. The kids love it!"))
		case 3:
			msgbox(format("Drake intimidates me, honestly. He's a crusty old sailor from the Draconid tribes, and the strongest after the Champion."))
		default:
			msgbox(format("I don't see Champion Anabel much at all. She usually looks bored."))
	}
	release
	end
}

script EverGrandeCity_EventScript_Gentleman {
	lock
	faceplayer
	goto_if_set(FLAG_TEMP_3, Harrumph)
	goto_if_set(FLAG_TEMP_4, Archipelago)
	msgbox(format("Er-hem! Do you know who I am?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == NO) {
		setflag(FLAG_TEMP_3)
		Harrumph:
        msgbox(format("Hmph. Kids these days. Go fight a Gym Leader or something."))
        release
        end
    } else {
		setflag(FLAG_TEMP_4)
		msgbox(format("That's right! I used to set energy policy throughout the Hoenn Archipelago. Hah! Still got it!"))
		release
		end
	}
	Archipelago:
	msgbox(format("The Hoenn Archipelago refers to the islands to the east of mainland Hoenn."))
	msgbox(format("That includes Mossdeep, Sootopolis, Ever Grande, Pacifidlog, and a whole bundle of places with no permanent settlements."))
	msgbox(format("Want to start a fight in a boring meeting? Ask an intern whether Dewford and the southern islands should be grouped in the Archipelago!"))
	msgbox(format("Ah, the Director hated my shenanigans."))
	release
	end
}

script EverGrandeCity_EventScript_Fisherman {
	lock
	faceplayer
	checkitem(ITEM_OLD_ROD)
	if(var(VAR_RESULT)) {
		goto(GoodRodCheck)
	}

	msgbox(format("A promising Trainer! Tell me, do you like to fish?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == NO) {
		msgbox(format("A shame, but I suppose fishing isn't for everyone."))
		release
		end
	}
	msgbox(format("Excellent! Here's a little something to get you started!"))
	giveitem(ITEM_OLD_ROD)
	waitmessage
	release
	end

	GoodRodCheck:
	checkitem(ITEM_GOOD_ROD)
	if(var(VAR_RESULT)) {
		goto(SuperRodCheck)
	}
	msgbox(format("If you want a better fishing rod, my younger brother hangs around the river east of Mauville City."))
	release
	end

	SuperRodCheck:
	checkitem(ITEM_SUPER_ROD)
	if(var(VAR_RESULT)) {
		msgbox(format("Hey {PLAYER}! How are the fish biting?"))
		release
		end
	}
	msgbox(format("If you're looking to upgrade to the ultimate fishing rod, seek out my older brother in Mossdeep!"))
	release
	end
}

script EverGrandeCity_EventScript_SchoolKidM {
	lock
	faceplayer
	msgbox(format("They say it's good luck to catch a Pokémon here before challenging the Elite Four."))
	msgbox(format("There's even a whole superstition around which Pokémon are lucky or unlucky!"))
	msgbox(format("Finding an Absol means bad luck in your future, but I think that's ridiculous! Absol are cool! That's way better than any lame Xatu or Oricorio."))
	release
	end
}

script EverGrandeCity_EventScript_Mom {
	lock
	faceplayer
	msgbox(format("My kids make everything a competition."))
	msgbox(format("I hope when they get old enough to challenge the gym circuit they will drive each other to greater heights with that competitiveness."))
	msgbox(format("Until then, I'm stuck here dealing with every tantrum and argument."))
	release
	end
}

script EverGrandeCity_EventScript_Hiker {
	lock
	faceplayer
	goto_if_set(FLAG_GIVEN_POWER_ITEMS, WinstrateHint)
	msgbox(format("A full League challenge? In this economy? You'll want these!"))
	setflag(FLAG_GIVEN_POWER_ITEMS)
	giveitem(ITEM_POWER_WEIGHT)
	giveitem(ITEM_POWER_BRACER)
	giveitem(ITEM_POWER_BELT)
	giveitem(ITEM_POWER_LENS)
	giveitem(ITEM_POWER_BAND)
	giveitem(ITEM_POWER_ANKLET)
	msgbox(format("Power items can be held by Pokémon to influence the Effort Values they gain in battle."))
	WinstrateHint:
	msgbox(format("I hand out one free set of power items to newbie trainers. If you want more, the Winstrate family sells them. They live north of Mauville."))
	release
	end
}

script EverGrandeCity_EventScript_Maniac {
	lock
	faceplayer
	if(flag(FLAG_TEMP_5)) {
		goto(GoodLuck)
	}
	setflag(FLAG_TEMP_5)
	msgbox(format("Unknown trainer spotted! Foreign accent detected! Conclusion: you're here to challenge the Hoenn League!"))
	GoodLuck:
	msgbox(format("Good luck, stranger! You'll need it."))
	release
	end
}

script EverGrandeCity_EventScript_CitySign {
	lock
	msgbox(format("Ever Grande City\n“Flowering heart of the Hoenn region.”"))
	release
	end
}

script EverGrandeCity_EventScript_PlazaSign {
	lock
	msgbox(format("Ever Grande City\nLeague Plaza"))
	release
	end
}

script EverGrandeCity_EventScript_CaveSign {
	lock
	msgbox(format("Champion's Cut \n Access to Pokémon League"))
	release
	end
}

//Common ferry script for use at all terminals
script Common_EventScript_Ferry {
	lock
	faceplayer
	checkitem(ITEM_HM03_SURF)
	if(var(VAR_RESULT)) {
		//if the player has surf, allow any destination
		setflag(FLAG_TEMP_1F)
	} 
	//check map and count badges
	specialvar(VAR_RESULT, GetCurrentMapSec)
	call(Common_EventScript_CountBadges)

	//check if in Ever Grande and need to go teleport to Victory Road
	if(flag(FLAG_CHALLENGE_HM_NONE) && var(VAR_NUM_BADGES) > 7 && !flag(FLAG_SYS_GAME_CLEAR) && var(VAR_RESULT) == 0x0F) {
		msgbox(format("The warp statue in the Pokémon Center has been unlocked!"))
		release
		end
	}

	//check ticket
	call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_CheckFerryTicket)
	if(var(VAR_RESULT) == 0x0E) {
		//sootopolis city has a different intro
		msgbox(format("Yo, {PLAYER}! The ferry can't reach inside the caldera, but we can take you by Flying Pokémon!"))
	} else {
		msgbox(format("Ahoy, {PLAYER}! The ferry is ready to take you to your next destination!"))
	}

	//check mainland gyms
	//rustboro, mauville, lavaridge, petalburg, fortree
	if (flag(FLAG_BADGE01_GET) && flag(FLAG_BADGE03_GET) && flag(FLAG_BADGE04_GET) && flag(FLAG_BADGE05_GET) && flag(FLAG_BADGE06_GET)) {
		setflag(FLAG_TEMP_1E) //all mainland gyms defeated, must go to islands
	}
	
	if (var(VAR_RESULT) != 0x0A && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || !flag(FLAG_TEMP_1E) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Rustboro", 0)
	}
	if (var(VAR_RESULT) != 0x02 && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || !flag(FLAG_BADGE02_GET) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Dewford", 1)
	}
	if (var(VAR_RESULT) != 0x4B && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || !flag(FLAG_TEMP_1E) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Littleroot", 2)
	}
	if (var(VAR_RESULT) != 0x08 && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || !flag(FLAG_TEMP_1E) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Slateport", 3)
	}
	if (var(VAR_RESULT) != 0x0C && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || !flag(FLAG_TEMP_1E) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Lilycove", 4)
	}
	if (var(VAR_RESULT) != 0x0D && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || !flag(FLAG_BADGE07_GET) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Mossdeep", 5)
	}
	if (var(VAR_RESULT) != 0x0E && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || !flag(FLAG_BADGE08_GET) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Sootopolis", 6)
	}
	if (var(VAR_RESULT) != 0x0F && (!flag(FLAG_CHALLENGE_FERRY_LIMIT) || (var(VAR_NUM_BADGES) > 7) || flag(FLAG_TEMP_1F))) {
		dynmultipush("Ever Grande", 7)
	}

	//finished all checks, do menu choice
	msgbox(format("Where would you like to go?"))
	dynmultistack(0,0,FALSE,6,TRUE,0,DYN_MULTICHOICE_CB_NONE)

	//warp to new map, use ticket if doing challenge, set heal spawn to prevent locking
	switch(var(VAR_RESULT)) {
		case 0:
			warp(MAP_RUSTBORO_CITY, 13)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_RUSTBORO_CITY)
			setvar(VAR_FERRY_DESTINATION, 1)
		case 1:
			warp(MAP_DEWFORD_RESORT, 6)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_DEWFORD_TOWN)
			setvar(VAR_FERRY_DESTINATION, 2)
		case 2:
			warp(MAP_LITTLEROOT_SHORE, 1)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_LITTLEROOT_TOWN_MAYS_HOUSE)
			setvar(VAR_FERRY_DESTINATION, 3)
		case 3:
			warp(MAP_SLATEPORT_CITY, 9)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_SLATEPORT_CITY)
			setvar(VAR_FERRY_DESTINATION, 4)
		case 4:
			warp(MAP_LILYCOVE_CITY, 13)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_LILYCOVE_CITY)
			setvar(VAR_FERRY_DESTINATION, 5)
		case 5:
			warp(MAP_MOSSDEEP_CITY, 13)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_MOSSDEEP_CITY)
			setvar(VAR_FERRY_DESTINATION, 6)
		case 6:
			warp(MAP_SOOTOPOLIS_CITY, 14)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_SOOTOPOLIS_CITY)
			setvar(VAR_FERRY_DESTINATION, 7)
		case 7:
			warp(MAP_EVER_GRANDE_CITY, 10)
			call_if_set(FLAG_CHALLENGE_FERRY_LIMIT, Common_EventScript_UseFerryTicket)
			setrespawn(HEAL_LOCATION_EVER_GRANDE_CITY)
			setvar(VAR_FERRY_DESTINATION, 8)
		default:
			msgbox(format("See you later, then!"))
			release
			end
	}
	setflag(FLAG_DEPARTED_EVER_GRANDE)
	release
	end
}

script Common_EventScript_CheckFerryTicket {
	goto_if_set(FLAG_CHALLENGE_FERRY_TICKET, Common_EventScript_NopReturn)
	msgbox(format("Sorry, {PLAYER}! You need to refresh your ferry ticket by beating a Gym Leader!"))
	release
	end
}

script Common_EventScript_UseFerryTicket {
	clearflag(FLAG_CHALLENGE_FERRY_TICKET)
	return
}
