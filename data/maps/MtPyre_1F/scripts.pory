mapscripts MtPyre_1F_MapScripts {
	MAP_SCRIPT_ON_LOAD : MtPyre_1F_OnLoad
}

script MtPyre_1F_OnLoad {
	call(MtPyre_1F_EventScript_CheckTrainers)
	call_if_set(FLAG_TEMP_11, MtPyre_1F_EventScript_OpenExterior)
	end
}

script MtPyre_1F_EventScript_CheckTrainers {
	goto_if_not_defeated(TRAINER_MARK, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_DEZ_AND_LUKE, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_ZANDER, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_LEAH, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_WILLIAM, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_KAYLA, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_GABRIELLE, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_TASHA, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_ATSUSHI, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_VALERIE, Common_EventScript_NopReturn)
	goto_if_not_defeated(TRAINER_CEDRIC, Common_EventScript_NopReturn)
	setflag(FLAG_TEMP_11)
	return
}

script MtPyre_1F_EventScript_OpenExterior {
	setmetatile(6, 5, 0x2E7, FALSE)
	setmetatile(6, 6, 0x2E6, FALSE)
	return
}

script MtPyre_1F_EventScript_HexManiac {
	msgbox(format("The spirits prevent the unworthy from accessing the exterior. You must climb the stairs and defeat all the other Trainers to prove yourself…"), MSGBOX_NPC)
	end
}

raw `
MtPyre_1F_EventScript_CleanseTagWoman::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_CLEANSE_TAG, MtPyre_1F_EventScript_ReceivedCleanseTag
	msgbox MtPyre_1F_Text_TakeThisForYourOwnGood, MSGBOX_DEFAULT
	giveitem ITEM_CLEANSE_TAG
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setflag FLAG_RECEIVED_CLEANSE_TAG
	release
	end

MtPyre_1F_EventScript_ReceivedCleanseTag::
	msgbox MtPyre_1F_Text_ExplainCleanseTag, MSGBOX_DEFAULT
	release
	end

MtPyre_1F_EventScript_PokefanF::
	msgbox MtPyre_1F_Text_ComeToPayRespects, MSGBOX_NPC
	end

MtPyre_1F_EventScript_Man::
	msgbox MtPyre_1F_Text_RestingPlaceOfZigzagoon, MSGBOX_NPC
	end

MtPyre_1F_Text_TakeThisForYourOwnGood:
	.string "All sorts of beings wander the\n"
	.string "upper levels of Mt. Pyre…\p"
	.string "There is no telling what may happen.\n"
	.string "Take this. It's for your own good.$"

MtPyre_1F_Text_ExplainCleanseTag:
	.string "Have a Pokémon hold that Cleanse Tag.\n"
	.string "It will ward off wild Pokémon.$"

MtPyre_1F_Text_ComeToPayRespects:
	.string "Did you come to pay your respect\n"
	.string "to the spirits of departed Pokémon?\p"
	.string "You must care for your Pokémon a lot.$"

MtPyre_1F_Text_RestingPlaceOfZigzagoon:
	.string "This is the final resting place of my\n"
	.string "Zigzagoon. I cherished it…$"

`
