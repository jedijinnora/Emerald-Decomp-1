mapscripts MauvilleCity_WorkerBuilding_MapScripts {
    MAP_SCRIPT_ON_TRANSITION : MauvilleCity_WorkerBuilding_OnTransition
}

script MauvilleCity_WorkerBuilding_OnTransition {
    call_if_lt(VAR_NEW_MAUVILLE_STATE, 2, MauvilleCity_WorkerBuilding_EventScript_HideScotty)
    end
}

script MauvilleCity_WorkerBuilding_EventScript_HideScotty {
    setflag(FLAG_TEMP_15)
}

script MauvilleCity_WorkerBuilding_EventScript_SchoolKid {
    msgbox(format("My boss sent me to get a set of left-handed allen wrenches from the toolbox, but I just can't find it."), MSGBOX_NPC)
    end
}

script MauvilleCity_WorkerBuilding_EventScript_Maniac {
    msgbox(format("I'm a power tech and I'm okay, I sleep all night and I wrench all day."), MSGBOX_NPC)
    end
}

script MauvilleCity_WorkerBuilding_EventScript_Man1 {
    msgbox(format("Dang, the paperwork for this job keeps getting worse.\pDo you know how to fill out a 27b-6?"), MSGBOX_NPC)
    end
}

script MauvilleCity_WorkerBuilding_EventScript_BatteryGuy {
    lock
	faceplayer
    goto_if_set(FLAG_SYS_GAME_CLEAR, MauvilleCity_WorkerBuilding_EventScript_BatterySeller)
    msgbox(format("As soon as the new generators come online, I'm going to make bank selling my single-use chargeable items!"))
    release
    end
}

script MauvilleCity_WorkerBuilding_EventScript_BatterySeller {
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemart(BatteryMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart BatteryMart {
	ITEM_CELL_BATTERY
    ITEM_BOOSTER_ENERGY
}

script MauvilleCity_WorkerBuilding_EventScript_Scotty {
    lock
    faceplayer
    msgbox(format("Scotty: Beam me up! Look who it is!"))
    call_if_unset(FLAG_MET_ELECTRIC_TUTOR, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistInfo)
    goto(MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistMenu)
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistInfo {
    msgbox(format("Thanks for the help with that nasty Rotom."))
    msgbox(format("I'm Scotty, engineer and this region's Electric-type specialist."))
    msgbox(format("If you defeat my Electric Pokémon in battle, I will offer to teach you various Electric-type moves."))
    msgbox(format("There are three battles available: a standard double battle, a double battle with inverse type matchups, and a challenge battle where you must bring only Electric-type Pokémon."))
    setflag(FLAG_MET_ELECTRIC_TUTOR)
    return
}

script MauvilleCity_WorkerBuilding_EventScript_GiveElectricGem {
    msgbox(format("Here, this item boosts the power of one Electric-type move per battle."))
    giveitem(ITEM_ELECTRIC_GEM)
    goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
    setflag(FLAG_RECEIVED_ELECTRIC_GEM)
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistMenu {
    msgbox(format("How can I help?"))
    dynmultipush("Information", 0)
    dynmultipush("Move Tutor", 1)
    dynmultipush("Battle", 2)
    dynmultistack(0,0,FALSE,6,TRUE,0,DYN_MULTICHOICE_CB_NONE)
    switch(var(VAR_RESULT)) {
        case 0:
            call(MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistInfo)
        case 1:
            call(MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistTutor)
        case 2:
            call(MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistBattle)
        default:
            msgbox(format("Be seeing you."), MSGBOX_AUTOCLOSE)
            release
            end
    }
    goto(MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistMenu)
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistTutor {
    call(MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistCheckMoves)
    goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
    call_if_defeated(TRAINER_SCOTTY_INITIAL, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistInitialUnlock)
    call_if_defeated(TRAINER_SCOTTY_INVERSE, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistInverseUnlock)
    call_if_defeated(TRAINER_SCOTTY_CHALLENGE, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistChallengeUnlock)
    call(MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistUnlockOtherTutor)
    msgbox(format("Which move can I teach you?"))
    dynmultistack(0,0,FALSE,6,FALSE,0,DYN_MULTICHOICE_CB_NONE)
    switch(var(VAR_RESULT)) {
        case 11:
            setvar(VAR_0x8005, MOVE_CHARGE)
        case 12:
            setvar(VAR_0x8005, MOVE_MAGNET_RISE)
        case 13:
            setvar(VAR_0x8005, MOVE_ELECTRO_BALL)
        case 21:
            setvar(VAR_0x8005, MOVE_DISCHARGE)
        case 22:
            setvar(VAR_0x8005, MOVE_CHARGE_BEAM)
        case 23:
            setvar(VAR_0x8005, MOVE_ELECTROWEB)
        case 24:
            setvar(VAR_0x8005, MOVE_EERIE_IMPULSE)
        case 31:
            setvar(VAR_0x8005, MOVE_RISING_VOLTAGE)
        case 32:
            setvar(VAR_0x8005, MOVE_VOLT_TACKLE)
        case 33:
            setvar(VAR_0x8005, MOVE_ZAP_CANNON)
        case 41:
            setvar(VAR_0x8005, MOVE_THUNDER_FANG)
        case 42:
            setvar(VAR_0x8005, MOVE_ELECTRIC_TERRAIN)
        case 43:
            setvar(VAR_0x8005, MOVE_SHOCK_WAVE)
        default:
            return
    }
    call(MoveTutor_EventScript_OpenPartyMenu)
    msgbox(format("Continue teaching moves?"), MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, YES, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistTutor)
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistCheckMoves {
    goto_if_defeated(TRAINER_SCOTTY_INITIAL, Common_EventScript_NopReturn)
    goto_if_defeated(TRAINER_SCOTTY_INVERSE, Common_EventScript_NopReturn)
    goto_if_defeated(TRAINER_SCOTTY_CHALLENGE, Common_EventScript_NopReturn)
    goto_if_defeated(TRAINER_BITE_TUTOR, Common_EventScript_NopReturn)
    goto_if_defeated(TRAINER_CRITICAL_TUTOR, Common_EventScript_NopReturn)
    msgbox(format("You haven't unlocked any moves yet."))
    setvar(VAR_RESULT, FALSE)
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistInitialUnlock {
    dynmultipush("Charge", 11)
    dynmultipush("Magnet Rise", 12)
    dynmultipush("Electro Ball", 13)
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistInverseUnlock {
    dynmultipush("Discharge", 21)
    dynmultipush("Charge Beam", 22)
    dynmultipush("Electroweb", 23)
    dynmultipush("Eerie Impulse", 24)
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistChallengeUnlock {
    dynmultipush("Rising Voltage", 31)
    dynmultipush("Volt Tackle", 32)
    dynmultipush("Zap Cannon", 33)
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistUnlockOtherTutor {
    if(defeated(TRAINER_BITE_TUTOR)) {
        dynmultipush("Thunder Fang", 41)
    }
    if(defeated(TRAINER_TERRAIN_TUTOR_ELECTRIC)) {
        dynmultipush("Electric Terrain", 42)
    }
    if(flag(FLAG_MET_THOMPSON_ALWAYS_HIT_MOVES)) {
        dynmultipush("Shock Wave", 43)
    }
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistBattle {
    dynmultipush("Standard double battle", 1)
    dynmultipush("Inverse double battle", 2)
    dynmultipush("Electric-only double battle", 3)
    msgbox(format("What kind of battle would you like?"))
    dynmultistack(0,0,FALSE,6,TRUE,0,DYN_MULTICHOICE_CB_NONE)
    switch(var(VAR_RESULT)) {
        case 1:
            call(MauvilleCity_WorkerBuilding_EventScript_InitialBattle)
        case 2:
            call(MauvilleCity_WorkerBuilding_EventScript_InverseBattle)
        case 3:
            call(MauvilleCity_WorkerBuilding_EventScript_ChallengeBattle)
            call_if_unset(FLAG_RECEIVED_ELECTRIC_GEM, MauvilleCity_WorkerBuilding_EventScript_GiveElectricGem)
        default:
            return
    }
    call_if_unset(FLAG_ITEM_TM64, MauvilleCity_WorkerBuilding_EventScript_GiveThunderWave)
    release
    end
}

script MauvilleCity_WorkerBuilding_EventScript_InitialBattle {
    if(!defeated(TRAINER_SCOTTY_INITIAL)) {
        msgbox(format("Winning a standard double battle will unlock the tutor moves Charge, Magnet Rise, and Electro Ball."))
        setflag(FLAG_TEMP_11)
    }
    msgbox(format("Be careful not to bust up the engineering department! Ready?"), MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, NO, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistMenu)
    trainerbattle_no_intro(TRAINER_SCOTTY_INITIAL, MauvilleCity_WorkerBuilding_Text_ScottyDefeated)
    if(flag(FLAG_TEMP_11)) {
        msgbox(format("Added Charge, Magnet Rise, and Electro Ball to the tutor list."))
        clearflag(FLAG_TEMP_11)
    }
    return
}

script MauvilleCity_WorkerBuilding_EventScript_InverseBattle {
    if(!defeated(TRAINER_SCOTTY_INVERSE)) {
        msgbox(format("Winning an inverse double battle will unlock the tutor moves Discharge, Charge Beam, Electroweb, and Eerie Impulse."))
        setflag(FLAG_TEMP_11)
    }
    msgbox(format("Standing ready?"), MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, NO, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistMenu)
    setflag(FLAG_INVERSE_BATTLE)
    trainerbattle_no_intro(TRAINER_SCOTTY_INVERSE, MauvilleCity_WorkerBuilding_Text_ScottyDefeated)
    clearflag(FLAG_INVERSE_BATTLE)
    if(flag(FLAG_TEMP_11)) {
        msgbox(format("Added Discharge, Charge Beam, Electroweb, and Eerie Impulse to the tutor list."))
        clearflag(FLAG_TEMP_11)
    }
    return
}

script MauvilleCity_WorkerBuilding_EventScript_ChallengeBattle {
    setvar(VAR_0x8004, TYPE_ELECTRIC)
    specialvar(VAR_RESULT, CheckPartyMonotype)
    goto_if_eq(VAR_RESULT, FALSE, MauvilleCity_WorkerBuilding_EventScript_NotElectricMonotype)
    if(!defeated(TRAINER_SCOTTY_CHALLENGE)) {
        msgbox(format("Winning against me with all Electric-type Pokémon will unlock the tutor moves Rising Voltage, Volt Tackle, and Zap Cannon."))
        setflag(FLAG_TEMP_11)
    }
    msgbox(format("I'll give it all I've got. Will you?"), MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, NO, MauvilleCity_WorkerBuilding_EventScript_ElectricSpecialistMenu)
    trainerbattle_no_intro(TRAINER_SCOTTY_CHALLENGE, MauvilleCity_WorkerBuilding_Text_ScottyDefeated)
    if(flag(FLAG_TEMP_11)) {
        msgbox(format("Added Rising Voltage, Volt Tackle, and Zap Cannon to the tutor list."))
        clearflag(FLAG_TEMP_11)
    }
    return
}

script MauvilleCity_WorkerBuilding_EventScript_NotElectricMonotype {
    msgbox(format("You have a Pokémon that's not a Electric type."))
    release
    end
}

script MauvilleCity_WorkerBuilding_EventScript_GiveThunderWave {
    msgbox(format("You overcame my tech! Here, take this TM!"))
    giveitem(ITEM_TM64_THUNDER_WAVE)
    goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
    setflag(FLAG_ITEM_TM64)
    msgbox(format("Thunder Wave paralyzes the target. Don't underestimate it!"))
    return
}

text MauvilleCity_WorkerBuilding_Text_ScottyDefeated {
    "I can't take any more of that."
}