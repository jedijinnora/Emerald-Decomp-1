const LOCALID_STEVEN = 2

//FLAG_TEMP_11 : Stash
//FLAG_TEMP_12 : Steven

mapscripts MeteorFalls_StevensCave_MapScripts {
    MAP_SCRIPT_ON_TRANSITION : MeteorFalls_StevensCave_OnTransition
}

script MeteorFalls_StevensCave_OnTransition {
    call(MeteorFalls_StevensCave_EventScript_TryHideStash)
    call_if_unset(FLAG_MET_ROXANNE_STEVEN, MeteorFalls_StevensCave_EventScript_HideSteven)
    call_if_defeated(TRAINER_STEVEN, MeteorFalls_StevensCave_EventScript_HideSteven)
    end
}

script MeteorFalls_StevensCave_EventScript_TryHideStash {
    goto_if_set(FLAG_HIDE_SECRET_STASH_METEOR_FALLS, MeteorFalls_StevensCave_EventScript_HideStash)
    goto_if_ne(VAR_SECRET_STASH_STATE, 0, MeteorFalls_StevensCave_EventScript_HideStash)
    return
}

script MeteorFalls_StevensCave_EventScript_HideStash {
    setflag(FLAG_TEMP_11)
    return
}

script MeteorFalls_StevensCave_EventScript_HideSteven {
    setflag(FLAG_TEMP_12)
    return
}

script MeteorFalls_StevensCave_EventScript_Stash {
    setvar(VAR_TEMP_D, ITEM_ZINC)
	setvar(VAR_TEMP_E, ITEM_SUN_STONE)
	setvar(VAR_TEMP_F, ITEM_MOON_STONE)
	call(Common_EventScript_SecretStash)
    setflag(FLAG_HIDE_SECRET_STASH_METEOR_FALLS)
	end
}

script MeteorFalls_StevensCave_EventScript_Steven {
    lock
    msgbox(format("Steven: Hmph. When I'm Champion, Noland will rue the day he chose Roxanne over me!"))
    playse(SE_PIN)
    applymovement(LOCALID_STEVEN, Common_Movement_ExclamationMark)
    waitmovement(LOCALID_STEVEN)
    faceplayer
    msgbox(format("Steven: A visitor! It's my lucky day! I was getting bored training against wild Pokémon."))
    msgbox(format("I'll take my ancient Pokémon all the way to the top! You're but a stepping stone on my path to Champion!"))
    trainerbattle_no_intro(TRAINER_STEVEN, MeteorFalls_StevensCave_Text_StevenDefeated)
    msgbox(format("Steven: I don't know why I thought training in this cave would get me ahead…"))
    msgbox(format("You're an okay Trainer, I suppose."))
    giveitem(ITEM_HEART_SCALE, 3)
    msgbox(format("Steven: I'm heading back to my house in Mossdeep. Maybe training where I first met Beldum will make the difference."))
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    call_if_lt(VAR_TEMP_1, 11, MeteorFalls_StevensCave_EventScript_StevenExitRight)
    call_if_gt(VAR_TEMP_1, 11, MeteorFalls_StevensCave_EventScript_StevenExitLeft)
    waitmovement(0)
    playse(SE_DOOR)
    applymovement(LOCALID_STEVEN, MeteorFalls_StevensCave_Movement_StevenExitDoor)
    waitse
    waitmovement(0)
    removeobject(LOCALID_STEVEN)
    release
    end
}

script MeteorFalls_StevensCave_EventScript_StevenExitLeft {
    applymovement(LOCALID_STEVEN, MeteorFalls_StevensCave_Movement_StevenExitLeft)
    return
}

script MeteorFalls_StevensCave_EventScript_StevenExitRight {
    applymovement(LOCALID_STEVEN, MeteorFalls_StevensCave_Movement_StevenExitRight)
    return
}

movement MeteorFalls_StevensCave_Movement_StevenExitLeft {
    walk_left
    walk_down * 4
}

movement MeteorFalls_StevensCave_Movement_StevenExitRight {
    walk_right
    walk_down * 2
    walk_left * 2
    walk_down * 2
}

movement MeteorFalls_StevensCave_Movement_StevenExitDoor {
    walk_in_place_down
}

text MeteorFalls_StevensCave_Text_StevenDefeated {
    "…Fine. Perhaps I don't have all the\n"
    "answers yet."
}