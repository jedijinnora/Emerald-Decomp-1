mapscripts Route107_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : Route107_OnTransition
}

script Route107_OnTransition {
	call_if_unset(FLAG_SAND_FAMILY_MOVED, Route107_EventScript_HideNPCs)
	call(Common_EventScript_DisableBPReward)
	end
}

script Route107_EventScript_HideNPCs {
	setflag(FLAG_TEMP_11)
	setflag(FLAG_TEMP_12)
	return
}

script Route107_EventScript_BlackBelt {
	lock
	msgbox(format("Hah! Hi-yah!"))
	faceplayer
	msgbox(format("Buzz off! My Tyrogue and I are training! I'm gonna evolve him without any stinking evolution stone!"))
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script Route107_EventScript_Tyrogue {
	lock
	faceplayer
	playmoncry(SPECIES_TYROGUE, CRY_MODE_ENCOUNTER)
	msgbox(format("Tyrogue: Bbppi!"))
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script Route107_EventScript_BattleGirl {
	lock
	faceplayer
	goto_if_unset(FLAG_GIVEN_ROUTE_107_PUNCHING_GLOVE, Route107_EventScript_GiveGloves)
	msgbox(format("Not many people come to this side of the island."))
	release
	end
}

script Route107_EventScript_GiveGloves {
	msgbox(format("A Trainer like you should find this useful."))
	giveitem(ITEM_PUNCHING_GLOVE)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	setflag(FLAG_GIVEN_ROUTE_107_PUNCHING_GLOVE)
	msgbox(format("That's a glove back from when I was a champion boxer!"))
	msgbox(format("Try giving it to a Pokémon like Hitmonchan!"))
	release
	end
}

script Route107_EventScript_PokefanF {
	lock
	faceplayer
	msgbox(format("Isn't the sand on this beach wonderful?!!"))
	call_if_unset(FLAG_GIVEN_SOFT_SAND, Route107_EventScript_GiveSand)
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script Route107_EventScript_GiveSand {
	giveitem(ITEM_SOFT_SAND)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	setflag(FLAG_GIVEN_SOFT_SAND)
	return
}

script Route107_EventScript_LittleBoy {
	lock
	faceplayer
	msgbox(format("I hate you! You're worse than the beach!"))
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script Route107_EventScript_Kenneth {
	trainerbattle_single(TRAINER_KENNETH, Route107_Text_KennethIntro, Route107_Text_KennethDefeated)
	msgbox(Route107_Text_KennethPostBattle, MSGBOX_AUTOCLOSE)
	end
}

text Route107_Text_KennethIntro {
	"I've found a gap to the outside! Go\naway, this fishing spot is all mine!"
}

text Route107_Text_KennethDefeated {
	"What?! But my special Pokémon…"
}

text Route107_Text_KennethPostBattle {
	"I thought the Pokémon I caught here\nwould be special in some way…"
}

script Route107_EventScript_Darrin {
	trainerbattle_single(TRAINER_DARRIN, Route107_Text_DarrinIntro, Route107_Text_DarrinDefeated)
	msgbox(Route107_Text_DarrinPostBattle, MSGBOX_AUTOCLOSE)
	call(Route107_EventScript_TryDarrinRematch)
	release
	end
}
script Route107_EventScript_TryDarrinRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_DARRIN)
	setvar(VAR_0x8004, 1)
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_DARRIN, Route107_Text_DarrinDefeated)
	special(UpdateRecentTrainers)
	return
}

script Route107_EventScript_Tony {
	trainerbattle_single(TRAINER_TONY, Route107_Text_TonyIntro, Route107_Text_TonyDefeated)
	msgbox(Route107_Text_TonyPostBattle, MSGBOX_DEFAULT)
	call(Route107_EventScript_TryTonyRematch)
	release
	end
}
script Route107_EventScript_TryTonyRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_TONY)
	setvar(VAR_0x8004, 1)
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_TONY, Route107_Text_TonyDefeated)
	special(UpdateRecentTrainers)
	return
}


script Route107_EventScript_Denise {
	trainerbattle_single(TRAINER_DENISE, Route107_Text_DeniseIntro, Route107_Text_DeniseDefeated)
	msgbox(Route107_Text_DenisePostBattle, MSGBOX_AUTOCLOSE)
	call(Route107_EventScript_TryDeniseRematch)
	release
	end
}
script Route107_EventScript_TryDeniseRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_DENISE)
	setvar(VAR_0x8004, 1)
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_DENISE, Route107_Text_DeniseDefeated)
	special(UpdateRecentTrainers)
	return
}

script Route107_EventScript_Beth {
	trainerbattle_single(TRAINER_BETH, Route107_Text_BethIntro, Route107_Text_BethDefeated)
	msgbox(Route107_Text_BethPostBattle, MSGBOX_AUTOCLOSE)
	call(Route107_EventScript_TryBethRematch)
	release
	end
}
script Route107_EventScript_TryBethRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_BETH)
	setvar(VAR_0x8004, 1)
	if (defeated(TRAINER_CAMRON)) {
		msgbox(format("Include Camron and make it a double battle?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			addvar(VAR_0x8004, 1)
			call(Common_EventScript_CheckRecentRematchesSetBP)
			trainerbattle_two_trainers(TRAINER_BETH, Route107_Text_BethDefeated, TRAINER_CAMRON, Route107_Text_CamronDefeated)
			special(UpdateRecentTrainers)
			return
		}
	}
	call(Common_EventScript_CheckRecentRematchesSetBP)
    trainerbattle_no_intro(TRAINER_BETH, Route107_Text_BethDefeated)
	special(UpdateRecentTrainers)
	return
}

script Route107_EventScript_Lisa {
	trainerbattle_double(TRAINER_LISA_AND_RAY, Route107_Text_LisaIntro, Route107_Text_LisaDefeated, Route107_Text_LisaNotEnoughPokemon)
	msgbox(Route107_Text_LisaPostBattle, MSGBOX_AUTOCLOSE)
	call(Route107_EventScript_TryLisaRematch)
	release
	end
}
script Route107_EventScript_TryLisaRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_LISA_AND_RAY)
	setvar(VAR_0x8004, 1)
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_LISA_AND_RAY, Route107_Text_LisaDefeated)
	special(UpdateRecentTrainers)
	return
}

script Route107_EventScript_Ray {
	trainerbattle_double(TRAINER_LISA_AND_RAY, Route107_Text_RayIntro, Route107_Text_RayDefeated, Route107_Text_RayNotEnoughPokemon)
	msgbox(Route107_Text_RayPostBattle, MSGBOX_AUTOCLOSE)
	call(Route107_EventScript_TryRayRematch)
	release
	end
}
script Route107_EventScript_TryRayRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_LISA_AND_RAY)
	setvar(VAR_0x8004, 1)
	call(Common_EventScript_CheckRecentRematchesSetBP)
	trainerbattle_no_intro(TRAINER_LISA_AND_RAY, Route107_Text_RayDefeated)
	special(UpdateRecentTrainers)
	return
}

script Route107_EventScript_Camron {
	trainerbattle_single(TRAINER_CAMRON, Route107_Text_CamronIntro, Route107_Text_CamronDefeated)
	msgbox(Route107_Text_CamronPostBattle, MSGBOX_AUTOCLOSE)
	call(Route107_EventScript_TryCamronRematch)
	release
	end
}
script Route107_EventScript_TryCamronRematch {
	lock
    call(Common_EventScript_ShouldDoRematch)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setvar(VAR_0x8003, TRAINER_CAMRON)
	setvar(VAR_0x8004, 1)
	if (defeated(TRAINER_BETH)) {
		msgbox(format("Include Beth and make it a double battle?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			addvar(VAR_0x8004, 1)
			call(Common_EventScript_CheckRecentRematchesSetBP)
			trainerbattle_two_trainers(TRAINER_CAMRON, Route107_Text_CamronDefeated, TRAINER_BETH, Route107_Text_BethDefeated)
			special(UpdateRecentTrainers)
			return
		}
	}
	call(Common_EventScript_CheckRecentRematchesSetBP)
    trainerbattle_no_intro(TRAINER_CAMRON, Route107_Text_CamronDefeated)
	special(UpdateRecentTrainers)
	return
}
