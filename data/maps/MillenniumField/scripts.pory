mapscripts MillenniumField_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: MillenniumField_OnTransition
}

//Object IDs:
const LOCALID_KECLEON = 13  //x0y30 starting place
const LOCALID_NINJABOY = 14
const LOCALID_WELL_LADY = 16

//TEMPs Used:
//VAR_TEMP_1 = Pikachu item
//VAR_TEMP_2 = Kecleon item
//VAR_TEMP_3 = Kecleon successes
//VAR_TEMP_4 = Kecleon spot

script MillenniumField_OnTransition {
    call(MillenniumField_EventScript_ResetPikachu)
    call(MillenniumField_EventScript_ResetKecleon)
    call_if_set(FLAG_SYS_GAME_CLEAR, MillenniumField_EventScript_UnblockWell)
    end
}

script MillenniumField_EventScript_ResetPikachu {
    random(100)
    if (var(VAR_RESULT) < 32) {
         setvar(VAR_TEMP_1, ITEM_LAVA_COOKIE)
    } elif (var(VAR_RESULT) < 64) {
        setvar(VAR_TEMP_1, ITEM_FLUFFY_TAIL)
    } elif (var(VAR_RESULT) < 72) {
        setvar(VAR_TEMP_1, ITEM_LIGHT_BALL)
    } elif (var(VAR_RESULT) < 80) {
        setvar(VAR_TEMP_1, ITEM_STAR_PIECE)
    } elif (var(VAR_RESULT) < 89) {
        setvar(VAR_TEMP_1, ITEM_PP_UP)
    } elif (var(VAR_RESULT) < 93) {
        setvar(VAR_TEMP_1, ITEM_BOTTLE_CAP)
    } elif (var(VAR_RESULT) < 96) {
        setvar(VAR_TEMP_1, ITEM_PP_MAX)
    } elif (var(VAR_RESULT) < 99) {
        setvar(VAR_TEMP_1, ITEM_COMET_SHARD)
    } else {
        setvar(VAR_TEMP_1, ITEM_GOLD_BOTTLE_CAP)
    }
    return
}

script MillenniumField_EventScript_ResetKecleon {
    setobjectxyperm(LOCALID_KECLEON, 0, 30)
    random(12)
    switch(var(VAR_RESULT)) {
        case 0:
            setvar(VAR_TEMP_2, ITEM_BERRY_JUICE)
        case 1:
            setvar(VAR_TEMP_2, ITEM_AIR_BALLOON)
        case 2:
            setvar(VAR_TEMP_2, ITEM_EJECT_BUTTON)
        case 3:
            setvar(VAR_TEMP_2, ITEM_EJECT_PACK)
        case 4:
            setvar(VAR_TEMP_2, ITEM_EXPERT_BELT)
        case 5:
            setvar(VAR_TEMP_2, ITEM_FOCUS_SASH)
        case 6:
            setvar(VAR_TEMP_2, ITEM_SMOKE_BALL)
        case 7:
            setvar(VAR_TEMP_2, ITEM_RING_TARGET)
        case 8:
            setvar(VAR_TEMP_2, ITEM_QUICK_CLAW)
        case 9:
            setvar(VAR_TEMP_2, ITEM_MENTAL_HERB)
        case 10:
            setvar(VAR_TEMP_2, ITEM_RED_CARD)
        default:
            setvar(VAR_TEMP_2, ITEM_LUCKY_EGG)
    }
    return
}

script MillenniumField_EventScript_UnblockWell {
    setobjectxyperm(LOCALID_WELL_LADY, 18, 14)
    return
}

//Pikachu will hand out a daily party favor randomly chosen:
//0-31: Lava cookie
//32-63: Fluffy tail
//64-71: Light ball
//72-79: Star piece
//80-88: PP Up
//89-92: Bottle cap
//93-95: PP Max
//96-97: Comet shard
//99: Golden bottle cap
script MillenniumField_EventScript_Pikachu {
    lock
    faceplayer
    goto_if_set(FLAG_DAILY_PIKACHU_FAVOR, SeeYouTomorrow)
    playmoncry(SPECIES_PIKACHU, CRY_MODE_NORMAL)
    waitmoncry
    msgbox(format("Pikachu happily pushes a festival favor into your hands!"))
    setflag(FLAG_DAILY_PIKACHU_FAVOR)
    giveitem(VAR_TEMP_1)
    msgbox(format("Nice, free gift!"))
    waitmessage
    release
    end
    SeeYouTomorrow:
    playmoncry(SPECIES_PIKACHU, CRY_MODE_WEAK)
    waitmoncry
    msgbox(format("Pikachu has already given you a festival gift today. No double dipping!"))
    waitmessage
    release
    end
}

script MillenniumField_EventScript_TriathleteM {
    lock
    faceplayer
    msgbox(format("I bought a Galarica Wreath for my Slowpoke, but he won't evolve!"))
    msgbox(format("What a lousy excuse for a Water-type! I hope a Shellder eats him!"))
    release
    end
}

script MillenniumField_EventScript_ExpertF {
    lock
    faceplayer
    msgbox(format("My family has been caretakers for this festival ground for generations."))
    msgbox(format("No matter how old I get, there are always more young people excited to come here for the first time."))
    msgbox(format("I think being around their happiness keeps me young at heart."))
    release
    end
}

script MillenniumField_EventScript_Cook {
    lock
	faceplayer
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemart(MillenniumFieldCookMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart MillenniumFieldCookMart {
    ITEM_PEWTER_CRUNCHIES
    ITEM_RAGE_CANDY_BAR
    ITEM_OLD_GATEAU
    ITEM_CASTELIACONE
    ITEM_LUMIOSE_GALETTE
    ITEM_SHALOUR_SABLE
    ITEM_BIG_MALASADA
    ITEM_WHIPPED_DREAM
    ITEM_STRAWBERRY_SWEET
    ITEM_LOVE_SWEET
    ITEM_BERRY_SWEET
    ITEM_CLOVER_SWEET
    ITEM_FLOWER_SWEET
    ITEM_STAR_SWEET
    ITEM_RIBBON_SWEET
}

script MillenniumField_EventScript_Picnicker {
    lock
	faceplayer
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemart(MillenniumFieldPicnickerMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart MillenniumFieldPicnickerMart {
    ITEM_SWEET_APPLE
    ITEM_TART_APPLE
    ITEM_CRACKED_POT
    ITEM_CHIPPED_POT
    ITEM_GALARICA_CUFF
    ITEM_GALARICA_WREATH
    ITEM_SACHET
}

script MillenniumField_EventScript_LittleGirl {
    lock
	faceplayer
	goto_if_set(FLAG_MILLENNIUM_FIELD_SNOM_TRADE_COMPLETED, SnomTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_SNOM)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Hi! I really love making my own ice cream!"))
    msgbox(format("Could you trade me a big ice cream Pokémon to help me make more - I have this Snom I'd give you in return?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineSnomTrade)
	msgbox(format("Please help Snom grow up big and elegant!"))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineSnomTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotVanilluxe)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Yay! Here, have some ice cream."))
    giveitem(ITEM_CASTELIACONE, 3)
	setflag(FLAG_MILLENNIUM_FIELD_SNOM_TRADE_COMPLETED)
	release
	end

	DeclineSnomTrade:
	msgbox(format("I understand. That much ice cream could put too much power in my hands."))
	release
	end

	SelectedNotVanilluxe:
	bufferspeciesname(STR_VAR_1, VAR_0x800B)
	msgbox(format("{STR_VAR_1} doesn't have enough ice cream for me to trade you."))
	release
	end

	SnomTradeCompleted:
	msgbox(format("I scream for ice cream!"))
	release
	end
}

script MillenniumField_EventScript_Woman1 {
    lock
    faceplayer
    goto_if_set(FLAG_MILLENNIUM_FIELD_TYROGUE_TRADE_COMPLETED, TyrogueTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_TYROGUE)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("I have here a scrappy Fighting-type Pokémon."))
    msgbox(format("I'm looking for an extremely fluffy Pokémon, one I could bundle around myself like a scarf."))
    msgbox(format("Do you have a Furret? I'd trade you my Tyrogue."), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineTyrogueTrade)
	msgbox(format("Ooh, I can hardly wait to experience the fluff!"))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineTyrogueTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotFurret)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Thank you! I'll make a scarf for Furret right away."))
	setflag(FLAG_MILLENNIUM_FIELD_TYROGUE_TRADE_COMPLETED)
	release
	end

	DeclineTyrogueTrade:
	msgbox(format("I understand. Want to learn to crochet? Knit? No? Come back if you change your mind."))
	release
	end

	SelectedNotFurret:
	bufferspeciesname(STR_VAR_1, VAR_0x800B)
	msgbox(format("{STR_VAR_1}? It's not really suitable."))
	release
	end

	TyrogueTradeCompleted:
	msgbox(format("In case you don't know, you can evolve a Tyrogue with a Dawn, Dusk, or Shiny Stone."))
	release
	end
}

script MillenniumField_EventScript_ExpertM {
    lock
	faceplayer
    msgbox(gText_HowMayIServeYou)
	waitmessage
	pokemart(MillenniumFieldExpertMMart)
	msgbox(gText_PleaseComeAgain)
	release
	end
}

mart MillenniumFieldExpertMMart {
    ITEM_LEVEL_BALL
    ITEM_LURE_BALL
    ITEM_MOON_BALL
    ITEM_FRIEND_BALL
    ITEM_LOVE_BALL
    ITEM_FAST_BALL
    ITEM_HEAVY_BALL
}

script MillenniumField_EventScript_Kecleon {
    lock
    faceplayer
    if (flag(FLAG_DAILY_KECLEON_WON)) {
        playmoncry(SPECIES_KECLEON, CRY_MODE_NORMAL)
        waitmoncry
        msgbox(format("Kecleon looks forward to challenging you tomorrow!"))
        waitmessage
    } elif (var(VAR_TEMP_3) == 4) {
        call(MillenniumField_EventScript_KecleonPrize)
    } elif (var(VAR_TEMP_3) > 0) {
        call(MillenniumField_EventScript_KecleonVanish)
    } elif (flag(FLAG_DAILY_KECLEON_HUNT)) {
        playmoncry(SPECIES_KECLEON, CRY_MODE_ENCOUNTER)
        waitmoncry
        msgbox(format("Kecleon taunts you for your failure earlier today!"))
        waitmessage
    } else {
        setflag(FLAG_DAILY_KECLEON_HUNT)
        msgbox(format("Kecleon challenges you to a game of hide and seek!"))
        waitmessage
        call(MillenniumField_EventScript_KecleonVanish)
    }
    release
    end
}

script MillenniumField_EventScript_KecleonPrize {
    setflag(FLAG_DAILY_KECLEON_WON)
    setvar(VAR_TEMP_3, -1)
    playfanfare(MUS_LEVEL_UP)
    waitfanfare
    msgbox(format("Kecleon acknowledges his defeat, and presents you with your prize!"))
    giveitem(VAR_TEMP_2)
    playmoncry(SPECIES_KECLEON, CRY_MODE_WEAK)
    waitmoncry
    setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
    applymovement(LOCALID_KECLEON, KecleonVanish)
    waitmovement(LOCALID_KECLEON)
    setobjectxy(LOCALID_KECLEON, 0, 30)
    setobjectxyperm(LOCALID_KECLEON, 0, 30)
    applymovement(LOCALID_KECLEON, KecleonAppear)
    waitmovement(LOCALID_KECLEON)
    clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
    msgbox(format("Come back tomorrow to play again!"))
    waitmessage
    release
    end
}

script MillenniumField_EventScript_KecleonVanish {
    addvar(VAR_TEMP_3, 1)
    playmoncry(SPECIES_KECLEON, CRY_MODE_NORMAL)
    waitmoncry
    setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
    applymovement(LOCALID_KECLEON, KecleonVanish)
    waitmovement(LOCALID_KECLEON)
    do {
        random(15)
    } while (var(VAR_RESULT) == VAR_TEMP_4)
    switch(var(VAR_RESULT)) {
        case 0:
            setobjectxy(LOCALID_KECLEON, 17, 39)
            setobjectxyperm(LOCALID_KECLEON, 17, 39)
            setvar(VAR_TEMP_4, 0)
        case 1:
            setobjectxy(LOCALID_KECLEON, 4, 13)
            setobjectxyperm(LOCALID_KECLEON, 4, 13)
            setvar(VAR_TEMP_4, 1)
        case 2:
            setobjectxy(LOCALID_KECLEON, 8, 6)
            setobjectxyperm(LOCALID_KECLEON, 8, 6)
            setvar(VAR_TEMP_4, 2)
        case 3:
            setobjectxy(LOCALID_KECLEON, 17, 5)
            setobjectxyperm(LOCALID_KECLEON, 17, 5)
            setvar(VAR_TEMP_4, 3)
        case 4:
            setobjectxy(LOCALID_KECLEON, 28, 6)
            setobjectxyperm(LOCALID_KECLEON, 28, 6)
            setvar(VAR_TEMP_4, 4)
        case 5:
            setobjectxy(LOCALID_KECLEON, 35, 17)
            setobjectxyperm(LOCALID_KECLEON, 35, 17)
            setvar(VAR_TEMP_4, 5)
        case 6:
            setobjectxy(LOCALID_KECLEON, 27, 34)
            setobjectxyperm(LOCALID_KECLEON, 27, 34)
            setvar(VAR_TEMP_4, 6)
        case 7:
            setobjectxy(LOCALID_KECLEON, 39, 32)
            setobjectxyperm(LOCALID_KECLEON, 39, 32)
            setvar(VAR_TEMP_4, 7)
        case 8:
            setobjectxy(LOCALID_KECLEON, 31, 22)
            setobjectxyperm(LOCALID_KECLEON, 31, 22)
            setvar(VAR_TEMP_4, 8)
        case 9:
            setobjectxy(LOCALID_KECLEON, 25, 19)
            setobjectxyperm(LOCALID_KECLEON, 25, 19)
            setvar(VAR_TEMP_4, 9)
        case 10:
            setobjectxy(LOCALID_KECLEON, 12, 19)
            setobjectxyperm(LOCALID_KECLEON, 12, 19)
            setvar(VAR_TEMP_4, 10)
        case 11:
            setobjectxy(LOCALID_KECLEON, 12, 31)
            setobjectxyperm(LOCALID_KECLEON, 12, 31)
            setvar(VAR_TEMP_4, 11)
        case 12:
            setobjectxy(LOCALID_KECLEON, 8, 16)
            setobjectxyperm(LOCALID_KECLEON, 8, 16)
            setvar(VAR_TEMP_4, 12)
        case 13:
            setobjectxy(LOCALID_KECLEON, 23, 5)
            setobjectxyperm(LOCALID_KECLEON, 23, 5)
            setvar(VAR_TEMP_4, 13)
        default:
            setobjectxy(LOCALID_KECLEON, 21, 13)
            setobjectxyperm(LOCALID_KECLEON, 21, 13)
            setvar(VAR_TEMP_4, 14)
    }
    applymovement(LOCALID_KECLEON, KecleonAppear)
    waitmovement(LOCALID_KECLEON)
    clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
    return
}

movement KecleonAppear {
    set_visible
}

movement KecleonVanish {
	set_invisible
	delay_16
	set_visible
	delay_16
	set_invisible
	delay_8
	set_visible
	delay_8
	set_invisible
	delay_8
	set_visible
	delay_8
	set_invisible
	delay_4
	set_visible
	delay_4
	set_invisible
	delay_4
	set_visible
	delay_4
	set_invisible
}

script MillenniumField_EventScript_NinjaBoy {
    lock
    faceplayer
    playse(SE_PIN)
    applymovement(LOCALID_NINJABOY, Common_Movement_ExclamationMark)
    waitmovement(LOCALID_NINJABOY)
    msgbox(format("You can see me?! I guess my ninja abilities need work…"))
    msgbox(format("I figured the Expert Belt I got for beating Kecleon at hide and seek would improve my skills."))
    msgbox(format("Okay! I'll find myself a Covert Cloak. Know anybody with one?"))
    release
    end
}

script MillenniumField_EventScript_PokefanF {
    lock
    faceplayer
    msgbox(format("Legend says that Millennium Field is built over the resting place of the Mythical Pokémon Jirachi."))
    msgbox(format("Jirachi hibernates until a particular comet appears in the sky. Then it wakes for seven days and grants wishes to whomever finds it."))
    msgbox(format("What do you think would happen if someone wished for world peace or happiness for everyone?"))
    msgbox(format("Would that be a safe request to make of such a powerful creature?"))
    release
    end
}

script MillenniumField_EventScript_Mom {
    lock
    faceplayer
    if(flag(FLAG_SYS_GAME_CLEAR)) {
        msgbox(format("Once a day, the wishmaker might reward you for throwing a treasure down the well."))
        release
        end
    }
    msgbox(format("Wishmaker's Well is not open to visitors."))
    release
    end
}

script MillenniumField_EventScript_Skitty {
    lock
    faceplayer
    playmoncry(SPECIES_SKITTY, CRY_MODE_NORMAL)
    msgbox(format("Kitty!"))
    release
    end
}

script MillenniumField_EventScript_LittleBoy {
    lock
    faceplayer
	goto_if_set(FLAG_MILLENNIUM_FIELD_MORPEKO_TRADE_COMPLETED, MorpekoTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_MORPEKO)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Ugh, my sister always makes way more ice cream than we can eat before it melts."))
    msgbox(format("Could you trade me a Pokémon with a lot of spoons to help me eat all our ice cream? I'll give you a Morpeko in exchange!"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineMorpekoTrade)
	msgbox(format("Yum yum!"))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineMorpekoTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotAlakazam)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Yes! We'll spoon up tons of ice cream together!"))
	setflag(FLAG_MILLENNIUM_FIELD_MORPEKO_TRADE_COMPLETED)
	release
	end

	DeclineMorpekoTrade:
	msgbox(format("Awww."))
	release
	end

	SelectedNotAlakazam:
	bufferspeciesname(STR_VAR_1, VAR_0x800B)
	msgbox(format("That's a {STR_VAR_1}. Moar spoons please!"))
	release
	end

	MorpekoTradeCompleted:
	msgbox(format("Ooogh, brain freeze!"))
	release
	end
}

script MillenniumField_EventScript_Man4 {
    lock
    faceplayer
    msgbox(format("Groan…\p Driving all the way out here with supplies for the festival really takes it out of me."))
    release
    end
}

script MillenniumField_EventScript_TriathleteF {
    lock
    faceplayer
    msgbox(format("I love how the festival runs year-round."))
    msgbox(format("You can buy items from other regions here!"))
    release
    end
}

script MillenniumField_EventScript_FieldSign {
    lock
    msgbox(format("Millennium Field \n “The eternal festival ground.”"))
    release
    end
}

script MillenniumField_EventScript_WellSign {
    lock
    msgbox(format("Wishmaker's Well \n “Where lost wishes sleep forever.”"))
    release
    end
}

script MillenniumField_EventScript_KecleonSign {
    lock
    msgbox(format("Hide and seek rules:\n Speak to Kecleon to begin."))
    msgbox(format("Find Kecleon 4 times without leaving Millennium Field to win!"))
    release
    end
}