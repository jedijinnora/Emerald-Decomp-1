mapscripts Route123_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : Route123_OnTransition
	MAP_SCRIPT_ON_RESUME : Route123_OnResume
}

script Route123_OnTransition {
	special(SetRoute123Weather)
	end
}

script Route123_OnResume {
	call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, Route123_EventScript_TryRemoveStaticBall)
	end
}

script Route123_EventScript_TryRemoveStaticBall {
	specialvar(VAR_RESULT, GetBattleOutcome)
	goto_if_ne(VAR_RESULT, B_OUTCOME_CAUGHT, Common_EventScript_NopReturn)
	removeobject(VAR_LAST_TALKED)
	return
}

script Route123_EventScript_LikelyVoltorbBall {
	lock
	random(5)
	switch(var(VAR_RESULT)) {
		case 0:
			call(Route123_EventScript_EncounterFoongus)
			call_if_set(FLAG_TEMP_2, Route123_EventScript_EncounterStunfisk)
		case 1:
			call(Route123_EventScript_EncounterStunfisk)
			call_if_set(FLAG_TEMP_3, Route123_EventScript_EncounterFoongus)
		default:
			call(Route123_EventScript_EncounterVoltorb)
	}
	if(flag(FLAG_TEMP_1)) {
		call_if_set(FLAG_TEMP_2, Route123_EventScript_EncounterStunfisk)
		call_if_unset(FLAG_TEMP_2, Route123_EventScript_EncounterFoongus)
	}
	call(Route123_EventScript_BuffStaticMon)
	call(Route123_EventScript_SelectStaticMon)
	specialvar(VAR_RESULT, GetBattleOutcome)
	goto_if_eq(VAR_RESULT, B_OUTCOME_WON, Route123_EventScript_DefeatBall1)
	goto_if_eq(VAR_RESULT, B_OUTCOME_RAN, Route123_EventScript_DefeatBall1)
	goto_if_eq(VAR_RESULT, B_OUTCOME_PLAYER_TELEPORTED, Route123_EventScript_DefeatBall1)
	setflag(FLAG_HIDE_ROUTE_123_BALL_1)
	release
	end
}

script Route123_EventScript_LikelyFoongusBall {
	lock
	random(5)
	switch(var(VAR_RESULT)) {
		case 0:
			call(Route123_EventScript_EncounterVoltorb)
			call_if_set(FLAG_TEMP_1, Route123_EventScript_EncounterStunfisk)
		case 1:
			call(Route123_EventScript_EncounterStunfisk)
			call_if_set(FLAG_TEMP_3, Route123_EventScript_EncounterVoltorb)
		default:
			call(Route123_EventScript_EncounterFoongus)
	}
	if(flag(FLAG_TEMP_2)) {
		call_if_set(FLAG_TEMP_3, Route123_EventScript_EncounterVoltorb)
		call_if_unset(FLAG_TEMP_3, Route123_EventScript_EncounterStunfisk)
	}
	call(Route123_EventScript_BuffStaticMon)
	call(Route123_EventScript_SelectStaticMon)
	specialvar(VAR_RESULT, GetBattleOutcome)
	goto_if_eq(VAR_RESULT, B_OUTCOME_WON, Route123_EventScript_DefeatBall2)
	goto_if_eq(VAR_RESULT, B_OUTCOME_RAN, Route123_EventScript_DefeatBall2)
	goto_if_eq(VAR_RESULT, B_OUTCOME_PLAYER_TELEPORTED, Route123_EventScript_DefeatBall2)
	setflag(FLAG_HIDE_ROUTE_123_BALL_2)
	release
	end
}

script Route123_EventScript_LikelyStunfiskBall {
	lock
	random(5)
	switch(var(VAR_RESULT)) {
		case 0:
			call(Route123_EventScript_EncounterFoongus)
			call_if_set(FLAG_TEMP_2, Route123_EventScript_EncounterVoltorb)
		case 1:
			call(Route123_EventScript_EncounterVoltorb)
			call_if_set(FLAG_TEMP_1, Route123_EventScript_EncounterFoongus)
		default:
			call(Route123_EventScript_EncounterStunfisk)
	}
	if(flag(FLAG_TEMP_3)) {
		call_if_set(FLAG_TEMP_1, Route123_EventScript_EncounterFoongus)
		call_if_unset(FLAG_TEMP_1, Route123_EventScript_EncounterVoltorb)
	}
	call(Route123_EventScript_BuffStaticMon)
	call(Route123_EventScript_SelectStaticMon)
	specialvar(VAR_RESULT, GetBattleOutcome)
	goto_if_eq(VAR_RESULT, B_OUTCOME_WON, Route123_EventScript_DefeatBall3)
	goto_if_eq(VAR_RESULT, B_OUTCOME_RAN, Route123_EventScript_DefeatBall3)
	goto_if_eq(VAR_RESULT, B_OUTCOME_PLAYER_TELEPORTED, Route123_EventScript_DefeatBall3)
	setflag(FLAG_HIDE_ROUTE_123_BALL_3)
	release
	end
	
}

script Route123_EventScript_SelectStaticMon {
	switch(var(VAR_TEMP_2)) {
		case 1:
			setflag(FLAG_TEMP_1)
			switch(var(VAR_NUM_BADGES)) {
				case 0:
					setwildbattle(SPECIES_VOLTORB_HISUIAN, 14)
					playmoncry(SPECIES_VOLTORB_HISUIAN, CRY_MODE_ENCOUNTER)
				case 1:
					setwildbattle(SPECIES_VOLTORB_HISUIAN, 23)
					playmoncry(SPECIES_VOLTORB_HISUIAN, CRY_MODE_ENCOUNTER)
				case 2:
					setwildbattle(SPECIES_VOLTORB_HISUIAN, 32)
					playmoncry(SPECIES_VOLTORB_HISUIAN, CRY_MODE_ENCOUNTER)
				case 3:
					setwildbattle(SPECIES_ELECTRODE_HISUIAN, 41)
					playmoncry(SPECIES_ELECTRODE_HISUIAN, CRY_MODE_ENCOUNTER)
				case 4:
					setwildbattle(SPECIES_ELECTRODE_HISUIAN, 50)
					playmoncry(SPECIES_ELECTRODE_HISUIAN, CRY_MODE_ENCOUNTER)
				case 5:
					setwildbattle(SPECIES_ELECTRODE_HISUIAN, 59)
					playmoncry(SPECIES_ELECTRODE_HISUIAN, CRY_MODE_ENCOUNTER)
				case 6:
					setwildbattle(SPECIES_ELECTRODE_HISUIAN, 68)
				default:
					setwildbattle(SPECIES_ELECTRODE_HISUIAN, 77)
					playmoncry(SPECIES_ELECTRODE_HISUIAN, CRY_MODE_ENCOUNTER)
			}
		case 2:
			setflag(FLAG_TEMP_2)
			switch(var(VAR_NUM_BADGES)) {
				case 0:
					setwildbattle(SPECIES_FOONGUS, 14)
					playmoncry(SPECIES_FOONGUS, CRY_MODE_ENCOUNTER)
				case 1:
					setwildbattle(SPECIES_FOONGUS, 23)
					playmoncry(SPECIES_FOONGUS, CRY_MODE_ENCOUNTER)
				case 2:
					setwildbattle(SPECIES_FOONGUS, 32)
					playmoncry(SPECIES_FOONGUS, CRY_MODE_ENCOUNTER)
				case 3:
					setwildbattle(SPECIES_AMOONGUSS, 41)
					playmoncry(SPECIES_AMOONGUSS, CRY_MODE_ENCOUNTER)
				case 4:
					setwildbattle(SPECIES_AMOONGUSS, 50)
					playmoncry(SPECIES_AMOONGUSS, CRY_MODE_ENCOUNTER)
				case 5:
					setwildbattle(SPECIES_AMOONGUSS, 59)
					playmoncry(SPECIES_AMOONGUSS, CRY_MODE_ENCOUNTER)
				case 6:
					setwildbattle(SPECIES_AMOONGUSS, 68)
					playmoncry(SPECIES_AMOONGUSS, CRY_MODE_ENCOUNTER)
				default:
					setwildbattle(SPECIES_AMOONGUSS, 77)
					playmoncry(SPECIES_AMOONGUSS, CRY_MODE_ENCOUNTER)
			}
		case 3:
			setflag(FLAG_TEMP_3)
			switch(var(VAR_NUM_BADGES)) {
				case 0:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 14)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
				case 1:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 23)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
				case 2:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 32)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
				case 3:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 41)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
				case 4:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 50)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
				case 5:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 59)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
				case 6:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 68)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
				default:
					setwildbattle(SPECIES_STUNFISK_GALARIAN, 77)
					playmoncry(SPECIES_STUNFISK_GALARIAN, CRY_MODE_ENCOUNTER)
			}
		default:
		//error handler?
		setwildbattle(SPECIES_DITTO, 15)
		playmoncry(SPECIES_DITTO, CRY_MODE_ENCOUNTER)
	}
	waitmoncry
	delay(30)
	setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	dowildbattle
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	clearflag(FLAG_WILD_HIDDEN_ABILITY)
	clearflag(FLAG_WILD_PERFECT_IVS)
	return
}

script Route123_EventScript_EncounterVoltorb {
	setvar(VAR_TEMP_2, 1)
	return
}

script Route123_EventScript_EncounterFoongus {
	setvar(VAR_TEMP_2, 2)
	return
}

script Route123_EventScript_EncounterStunfisk {
	setvar(VAR_TEMP_2, 3)
	return
}

script Route123_EventScript_BuffStaticMon {
	call(Route123_EventScript_CountDefeatedTrainers)
	call_if_ge(VAR_TEMP_1, 10, Route123_EventScript_GiveEncounterHiddenAbility)
	random(16)
	call_if_gt(VAR_TEMP_1, VAR_RESULT, Route123_EventScript_GiveEncounterPerfectIVs)
	return
}

script Route123_EventScript_CountDefeatedTrainers {
	setvar(VAR_TEMP_1, 0)
	call_if_defeated(TRAINER_WENDY, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_BRAXTON, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_VIOLET, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_MIU_AND_YUKI, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_CAMERON, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_JACKI, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_KINDRA, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_JONAS, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_KAYLEY, Route123_EventScript_CountTrainer)
	call_if_defeated(TRAINER_ED, Route123_EventScript_CountTrainer)
	return
}

script Route123_EventScript_CountTrainer {
	addvar(VAR_TEMP_1, 1)
	return
}

script Route123_EventScript_GiveEncounterHiddenAbility {
	setflag(FLAG_WILD_HIDDEN_ABILITY)
	return
}

script Route123_EventScript_GiveEncounterPerfectIVs {
	setflag(FLAG_WILD_PERFECT_IVS)
	return
}

script Route123_EventScript_DefeatBall1 {
	setflag(FLAG_HIDE_ROUTE_123_BALL_1)
	goto(Common_EventScript_RemoveStaticPokemon)
	end
}

script Route123_EventScript_DefeatBall2 {
	setflag(FLAG_HIDE_ROUTE_123_BALL_2)
	goto(Common_EventScript_RemoveStaticPokemon)
	end
}

script Route123_EventScript_DefeatBall3 {
	setflag(FLAG_HIDE_ROUTE_123_BALL_3)
	goto(Common_EventScript_RemoveStaticPokemon)
	end
}


raw `
Route123_EventScript_GigaDrainGirl::
	lock
	faceplayer
	goto_if_set FLAG_ITEM_TM61, Route123_EventScript_ReceivedGigaDrain
	msgbox Route123_Text_LoveGrassMonsHaveAny, MSGBOX_DEFAULT
	special IsGrassTypeInParty
	goto_if_eq VAR_RESULT, FALSE, Route123_EventScript_NoGrassMons
	msgbox Route123_Text_YouLikeGrassMonsTooHaveThis, MSGBOX_DEFAULT
	giveitem ITEM_TM61_GIGA_DRAIN
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setflag FLAG_ITEM_TM61
	msgbox Route123_Text_CheckTreesWithMyGrassMon, MSGBOX_DEFAULT
	release
	end

Route123_EventScript_NoGrassMons::
	release
	end

Route123_EventScript_ReceivedGigaDrain::
	msgbox Route123_Text_CheckTreesWithMyGrassMon, MSGBOX_DEFAULT
	release
	end

Route123_EventScript_RouteSign::
	msgbox Route123_Text_RouteSign, MSGBOX_SIGN
	end

Route123_EventScript_RouteSignMtPyre::
	msgbox Route123_Text_RouteSignMtPyre, MSGBOX_SIGN
	end

Route123_EventScript_BerryMastersHouseSign::
	msgbox Route123_Text_BerryMastersHouse, MSGBOX_SIGN
	end

Route123_EventScript_Wendy::
	trainerbattle_single TRAINER_WENDY, Route123_Text_WendyIntro, Route123_Text_WendyDefeat
	msgbox Route123_Text_WendyPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Braxton::
	trainerbattle_single TRAINER_BRAXTON, Route123_Text_BraxtonIntro, Route123_Text_BraxtonDefeat
	msgbox Route123_Text_BraxtonPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Violet::
	trainerbattle_single TRAINER_VIOLET, Route123_Text_VioletIntro, Route123_Text_VioletDefeat
	msgbox Route123_Text_VioletPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Cameron::
	trainerbattle_single TRAINER_CAMERON, Route123_Text_CameronIntro, Route123_Text_CameronDefeat, Route123_EventScript_RegisterCameron
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route123_EventScript_RematchCameron
	msgbox Route123_Text_CameronPostBattle, MSGBOX_DEFAULT
	release
	end

Route123_EventScript_RegisterCameron::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route123_Text_CameronRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_CAMERON
	release
	end

Route123_EventScript_RematchCameron::
	trainerbattle_rematch TRAINER_CAMERON, Route123_Text_CameronRematchIntro, Route123_Text_CameronRematchDefeat
	msgbox Route123_Text_CameronPostRematch, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Jacki::
	trainerbattle_single TRAINER_JACKI, Route123_Text_JackiIntro, Route123_Text_JackiDefeat, Route123_EventScript_RegisterJacki
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route123_EventScript_RematchJacki
	msgbox Route123_Text_JackiPostBattle, MSGBOX_DEFAULT
	release
	end

Route123_EventScript_RegisterJacki::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route123_Text_JackiRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_JACKI
	release
	end

Route123_EventScript_RematchJacki::
	trainerbattle_rematch TRAINER_JACKI, Route123_Text_JackiRematchIntro, Route123_Text_JackiRematchDefeat
	msgbox Route123_Text_JackiPostRematch, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Miu::
	trainerbattle_double TRAINER_MIU_AND_YUKI, Route123_Text_MiuIntro, Route123_Text_MiuDefeat, Route123_Text_MiuNotEnoughMons
	msgbox Route123_Text_MiuPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Yuki::
	trainerbattle_double TRAINER_MIU_AND_YUKI, Route123_Text_YukiIntro, Route123_Text_YukiDefeat, Route123_Text_YukiNotEnoughMons
	msgbox Route123_Text_YukiPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Kindra::
	trainerbattle_single TRAINER_KINDRA, Route123_Text_KindraIntro, Route123_Text_KindraDefeat
	msgbox Route123_Text_KindraPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Frederick::
	trainerbattle_single TRAINER_FREDRICK, Route123_Text_FrederickIntro, Route123_Text_FrederickDefeat
	msgbox Route123_Text_FrederickPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Alberto::
	trainerbattle_single TRAINER_ALBERTO, Route123_Text_AlbertoIntro, Route123_Text_AlbertoDefeat
	msgbox Route123_Text_AlbertoPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Ed::
	trainerbattle_single TRAINER_ED, Route123_Text_EdIntro, Route123_Text_EdDefeat
	msgbox Route123_Text_EdPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Kayley::
	trainerbattle_single TRAINER_KAYLEY, Route123_Text_KayleyIntro, Route123_Text_KayleyDefeat
	msgbox Route123_Text_KayleyPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Jonas::
	trainerbattle_single TRAINER_JONAS, Route123_Text_JonasIntro, Route123_Text_JonasDefeat
	msgbox Route123_Text_JonasPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Jazmyn::
	trainerbattle_single TRAINER_JAZMYN, Route123_Text_JazmynIntro, Route123_Text_JazmynDefeat
	msgbox Route123_Text_JazmynPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Davis::
	trainerbattle_single TRAINER_DAVIS, Route123_Text_DavisIntro, Route123_Text_DavisDefeat
	msgbox Route123_Text_DavisPostBattle, MSGBOX_AUTOCLOSE
	end

Route123_EventScript_Fernando::
	trainerbattle_single TRAINER_FERNANDO, Route123_Text_FernandoIntro, Route123_Text_FernandoDefeat, Route123_EventScript_RegisterFernando
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route123_EventScript_RematchFernando
	msgbox Route123_Text_FernandoPostBattle, MSGBOX_DEFAULT
	release
	end

Route123_EventScript_RegisterFernando::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route123_Text_FernandoRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_FERNANDO
	release
	end

Route123_EventScript_RematchFernando::
	trainerbattle_rematch TRAINER_FERNANDO, Route123_Text_FernandoRematchIntro, Route123_Text_FernandoRematchDefeat
	msgbox Route123_Text_FernandoPostRematch, MSGBOX_AUTOCLOSE
	end

Route123_Text_LoveGrassMonsHaveAny:
	.string "I love Grass-type Pokémon!\p"
	.string "Do you have any Grass-type Pokémon?$"

Route123_Text_YouLikeGrassMonsTooHaveThis:
	.string "Oh?\p"
	.string "You like Grass-type Pokémon, too,\n"
	.string "don't you?\p"
	.string "I'm so happy, you can have this!\n"
	.string "It's a token of our friendship.$"

Route123_Text_CheckTreesWithMyGrassMon:
	.string "I check trees with my Grass-type\n"
	.string "Pokémon. I'm like a tree doctor.$"

Route123_Text_RouteSign:
	.string "{RIGHT_ARROW} Route 123\n"
	.string "{LEFT_ARROW} Route 118$"

Route123_Text_RouteSignMtPyre:
	.string "{UP_ARROW} Mt. Pyre\n"
	.string "“Forbidden to the faint of heart.”$"

Route123_Text_BerryMastersHouse:
	.string "Berry Master's House$"
`
