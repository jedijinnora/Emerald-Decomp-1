const LOCALID_RIVAL = 5

//FLAG_TEMP_1A : Rival

//VAR_TEMP_1 : x coord
//VAR_TEMP_2 : y coord
//VAR_TEMP_A : north rival
//VAR_TEMP_B : east rival
//VAR_TEMP_C : south rival
//VAR_TEMP_D : west rival

mapscripts FallarborTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : FallarborTown_OnTransition
}

script FallarborTown_OnTransition {
	call_if_unset(FLAG_VISITED_FALLARBOR_TOWN, FallarborTown_EventScript_VisitFallarbor)
	call_if_eq(VAR_FALLARBOR_TOWN_STATE, 0, FallarborTown_EventScript_TrySpawnRival)
	setflag(FLAG_TEMP_1A)
	call_if_ne(VAR_FALLARBOR_TOWN_STATE, 0, FallarborTown_EventScript_SetupRival)
	end
}

script FallarborTown_EventScript_VisitFallarbor {
	setflag(FLAG_VISITED_FALLARBOR_TOWN)
	addvar(VAR_CITIES_VISITED, 1)
	return
}

script FallarborTown_EventScript_TrySpawnRival {
	goto_if_lt(VAR_CITIES_VISITED, 4, Common_EventScript_NopReturn)
	goto_if_eq(VAR_DIFFICULTY_SETTING, 2, FallarborTown_EventScript_SpawnRival)
	goto_if_lt(VAR_CITIES_VISITED, 5, Common_EventScript_NopReturn)
	FallarborTown_EventScript_SpawnRival:
	getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
	if (var(VAR_TEMP_1) > 200) { 
		//west entry
		setvar(VAR_0x8004, 3)
	} elif (var(VAR_TEMP_1) > 19) { 
		//east entry
		setvar(VAR_0x8004, 1)
	} else {
		//warp entry
		setvar(VAR_0x8004, 4)
	}
	setvar(VAR_0x8005, 6)
	specialvar(VAR_FALLARBOR_TOWN_STATE, MultiplyVars)
	copyvar(VAR_0x8004, VAR_FALLARBOR_TOWN_STATE)
	copyvar(VAR_0x8005, VAR_BLOCKER_RIVAL_RANDOM)
	specialvar(VAR_FALLARBOR_TOWN_STATE, AddVars)
	call(Common_EventScript_IncrementRivalVar)
	return
}

script FallarborTown_EventScript_SetupRival {
	//if rival has been defeated, don't do anything
	goto_if_gt(VAR_FALLARBOR_TOWN_STATE, 99, Common_EventScript_NopReturn)

	//clear rival object flag
	clearflag(FLAG_TEMP_1A)

	//lock all exits if the player has been visiting new cities
	if(var(VAR_CITIES_VISITED) > 3) {
		while (var(VAR_FALLARBOR_TOWN_STATE) < 25) {
			addvar(VAR_FALLARBOR_TOWN_STATE, 6)
		}
	}

	//buffer city variable and call setup
	copyvar(VAR_0x8004, VAR_FALLARBOR_TOWN_STATE)
	call(Common_EventScript_SetupRival)
	return
}

script FallarborTown_EventScript_Rival {
	lock
	faceplayer
	switch(var(VAR_FALLARBOR_TOWN_STATE)) {
		case 1:
		case 7:
		case 13:
		case 19:
		case 25:
			call(FallarborTown_EventScript_RivalLeaf)
		case 2:
		case 8:
		case 14:
		case 20:
		case 26:
			call(FallarborTown_EventScript_RivalCalem)
		case 3:
		case 9:
		case 15:
		case 21:
		case 27:
			call(FallarborTown_EventScript_RivalDawn)
		case 4:
		case 10:
		case 16:
		case 22:
		case 28:
			call(FallarborTown_EventScript_RivalHilbert)
		case 5:
		case 11:
		case 17:
		case 23:
		case 29:
			call(FallarborTown_EventScript_RivalKris)
		case 6:
		case 12:
		case 18:
		case 24:
		case 30:
			call(FallarborTown_EventScript_RivalRei)
		default:
	}
	release
	end
}

script FallarborTown_EventScript_RivalLeaf {
	msgbox(format("Leaf: I heard Lanette is an even better programmer than Bill! But no one seems to know where she is…"))
	return
}

script FallarborTown_EventScript_RivalKris {
	msgbox(format("Kris: This place could really use a visit from Suicune, don't you think? I heard your Champion sometimes uses one in battle!"))
	return
}

script FallarborTown_EventScript_RivalDawn {
	msgbox(format("Dawn: Back home, lots of the region was covered in snow. Here, it's volcanic ash. Which do you prefer?"))
	return
}

script FallarborTown_EventScript_RivalRei {
	msgbox(format("Rei: Have you gotten some of the special flutes from the Glass Workshop?"))
	return
}

script FallarborTown_EventScript_RivalHilbert {
	msgbox(format("Hilbert: Huh, I'm getting definite Lentimas Town vibes from this part of Hoenn."))
	return
}

script FallarborTown_EventScript_RivalCalem {
	msgbox(format("Calem: Ugh, the volcanic ash is getting in my hair… I should visit a salon soon."))
	return
}

script FallarborTown_EventScript_RivalEast {
	lock
	setobjectxyperm(LOCALID_RIVAL, 10, 9)
	setobjectxy(LOCALID_RIVAL, 10, 9)
	addobject(LOCALID_RIVAL)
	applymovement(LOCALID_RIVAL, FallarborTown_Movement_RivalApproachEast)
	waitmovement(LOCALID_RIVAL)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(LOCALID_RIVAL)
	getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
	switch(var(VAR_TEMP_2)) {
		case 6:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerEast_1)
		case 7:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerEast_2)
		case 8:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerEast_3)
		case 9:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerEast_4)
		case 10:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerEast_5)
		case 11:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerEast_6)
		case 12:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerEast_7)
		default:
	}
	waitmovement(OBJ_EVENT_ID_PLAYER)
	copyvar(VAR_0x8004, VAR_FALLARBOR_TOWN_STATE)
	call(Common_EventScript_RivalBattle)
	setvar(VAR_FALLARBOR_TOWN_STATE, 100)
	closemessage
	applymovement(LOCALID_RIVAL, FallarborTown_Movement_RivalDepartEast)
	waitmovement(LOCALID_RIVAL)
	removeobject(LOCALID_RIVAL)
	call(Common_EventScript_ClearRivalAll)
	release
	end
}
movement FallarborTown_Movement_RivalApproachEast {
	walk_right * 7
}
movement FallarborTown_Movement_RivalDepartEast {
	walk_left * 8
}
movement FallarborTown_Movement_PlayerEast_1 {
	walk_down * 3
	walk_in_place_faster_left
}
movement FallarborTown_Movement_PlayerEast_2 {
	walk_down * 2
	walk_in_place_faster_left
}
movement FallarborTown_Movement_PlayerEast_3 {
	walk_down
	walk_in_place_faster_left
}
movement FallarborTown_Movement_PlayerEast_4 {
	walk_in_place_faster_left
}
movement FallarborTown_Movement_PlayerEast_5 {
	walk_up
	walk_in_place_faster_left
}
movement FallarborTown_Movement_PlayerEast_6 {
	walk_up * 2
	walk_in_place_faster_left
}
movement FallarborTown_Movement_PlayerEast_7 {
	walk_up * 3
	walk_in_place_faster_left
}

script FallarborTown_EventScript_RivalWest {
	lock
	setobjectxyperm(LOCALID_RIVAL, 9, 9)
	setobjectxy(LOCALID_RIVAL, 9, 9)
	addobject(LOCALID_RIVAL)
	applymovement(LOCALID_RIVAL, FallarborTown_Movement_RivalApproachWest)
	waitmovement(LOCALID_RIVAL)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(LOCALID_RIVAL)
	getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
	switch(var(VAR_TEMP_2)) {
		case 7:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerWest_1)
		case 8:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerWest_2)
		case 9:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerWest_3)
		case 10:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerWest_4)
		case 11:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerWest_5)
		case 12:
			applymovement(OBJ_EVENT_ID_PLAYER, FallarborTown_Movement_PlayerWest_6)
		default:
	}
	waitmovement(OBJ_EVENT_ID_PLAYER)
	copyvar(VAR_0x8004, VAR_FALLARBOR_TOWN_STATE)
	call(Common_EventScript_RivalBattle)
	setvar(VAR_FALLARBOR_TOWN_STATE, 100)
	closemessage
	applymovement(LOCALID_RIVAL, FallarborTown_Movement_RivalDepartWest)
	waitmovement(LOCALID_RIVAL)
	removeobject(LOCALID_RIVAL)
	call(Common_EventScript_ClearRivalAll)
	release
	end
}
movement FallarborTown_Movement_RivalApproachWest {
	walk_left * 7
}
movement FallarborTown_Movement_RivalDepartWest {
	walk_right * 8
}
movement FallarborTown_Movement_PlayerWest_1 {
	walk_down * 2
	walk_in_place_faster_right
}
movement FallarborTown_Movement_PlayerWest_2 {
	walk_down
	walk_in_place_faster_right
}
movement FallarborTown_Movement_PlayerWest_3 {
	walk_in_place_faster_right
}
movement FallarborTown_Movement_PlayerWest_4 {
	walk_up
	walk_in_place_faster_right
}
movement FallarborTown_Movement_PlayerWest_5 {
	walk_up * 2
	walk_in_place_faster_right
}
movement FallarborTown_Movement_PlayerWest_6 {
	walk_up * 3
	walk_in_place_faster_right
}

script FallarborTown_EventScript_ExpertM {
	lock
	faceplayer
	msgbox(FallarborTown_Text_RegionKnownForMeteors)
	release
	end
}

script FallarborTown_EventScript_Girl {
	lock
	faceplayer
	msgbox(FallarborTown_Text_MyPreciousAzurill)
	release
	end
}

script FallarborTown_EventScript_Gentleman {
	lock
	faceplayer
	msgbox(format("Have you challenged Moore, the leader of Lavaridge Gym? He was once a member of the Elite Four!"))
	msgbox(format("It wouldn't surprise me to see his granddaughter Flannery follow in his footsteps."))
	release
	end
}

script FallarborTown_EventScript_Azurill {
	lock
	faceplayer
	waitse
	playmoncry(SPECIES_AZURILL, CRY_MODE_NORMAL)
	msgbox(FallarborTown_Text_Azurill, MSGBOX_DEFAULT)
	waitmoncry
	release
	end
}

script FallarborTown_EventScript_TownSign {
	msgbox(FallarborTown_Text_TownSign, MSGBOX_SIGN)
	end
}

script FallarborTown_EventScript_MoveTutorSign {
	msgbox(FallarborTown_Text_MoveTutorSign, MSGBOX_SIGN)
	end
}
raw `
FallarborTown_Text_RegionKnownForMeteors:
	.string "This region's been known for meteors\n"
	.string "since the olden days.\p"
	.string "They say Meteor Falls was gouged out\n"
	.string "by falling meteorites long ago.$"

FallarborTown_Text_MyPreciousAzurill:
	.string "See! Take a look!\n"
	.string "This is my precious Azurill!\p"
	.string "It's slick and smooth and plushy, too!$"

FallarborTown_Text_Azurill:
	.string "Azurill: Rooreelooo.$"

FallarborTown_Text_TownSign:
	.string "Fallarbor Town\n"
	.string "“A farm community with small gardens.”$"

FallarborTown_Text_MoveTutorSign:
	.string "Utility House\n"
	.string "“Pokémon services offered!”$"
`
