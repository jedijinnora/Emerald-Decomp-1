mapscripts EverGrandeCity_Headquarters_1F_MapScripts {}

//Object IDs
const LOCALID_DESKLADY = 1
const LOCALID_BOX = 7

//TEMPs Used:
//FLAG_TEMP_1 SecurityMan intro
//FLAG_TEMP_2 Konami intro


script EverGrandeCity_Headquarters_1F_EventScript_DeskLady {
	lock
	faceplayer
    goto_if_set(FLAG_SYS_NATIONAL_DEX, CheerBye)

	msgbox(format("You must be our foreign exchange student from Sinnoh. Welcome to Hoenn!"))
    msgbox(format("Let me grab a box for you."))
    waitmessage

    delay(10)
    applymovement(LOCALID_DESKLADY, Common_Movement_FaceLeft)
    waitmovement(LOCALID_DESKLADY)
    delay(10)
    removeobject(LOCALID_BOX)
    delay(10)
    applymovement(LOCALID_DESKLADY, Common_Movement_FaceDown)
    waitmovement(LOCALID_DESKLADY)
    delay(10)

    msgbox(format("You'll need a fully upgraded Pokédex to be able to view data on all the Pokémon in this region."))
    msgbox(format("One moment please…"))
    setflag(FLAG_SYS_NATIONAL_DEX)
    special(EnableNationalPokedex)
    playfanfare(MUS_OBTAIN_ITEM)
    waitfanfare
    msgbox(format("There, I've installed the National expansion on your Pokédex."))
    waitmessage

    msgbox(format("You also get a Pokénav. This one is a new edition, so it may be a little buggy."))
    setflag(FLAG_SYS_POKENAV_GET)
    setflag(FLAG_RECEIVED_POKENAV)
    setflag(FLAG_ADDED_MATCH_CALL_TO_POKENAV)
    playfanfare(MUS_OBTAIN_ITEM)
    waitfanfare
    msgbox(format("I'm legally required to inform you that attempting to damage, modify, upgrade, or customize your Pokénav without speaking to a licensed representative of the Devon Corporation is a felony under Hoenn law and will void your warranty."))
    msgbox(format("Isn't that delightful? Sorry, this bigwig at Devon has been complaining about patent violation or some other legal muck. We used to just have Silph Co. goons screaming on the phone about re-usable TMs…"))
    
    msgbox(format("You're all set! I'll update the system with your information."))
    msgbox(format("Speak to the sailor at the dock to use the ferry."))
    msgbox(format("If you want more information on where to go or would like to activate optional challenges, speak to my associates at the computers in the corner."))

    msgbox(format("Finally, here is your Exp. Share!"))
    giveitem(ITEM_EXP_SHARE)
    msgbox(format("Use it to toggle whether all party Pokémon gain experience during battle."))

    CheerBye:
    goto_if_set(FLAG_SYS_GAME_CLEAR, CheerChamp)
    msgbox(format("Go get 'em, future Champion!"))
    release
    end
    CheerChamp:
    msgbox(format("Go get 'em, Champion {PLAYER}!"))
	release
	end
}

script EverGrandeCity_Headquarters_1F_EventScript_Beauty {
	lock
	faceplayer
	msgbox(format("Government buildings are surprisingly mundane. You think it's all high-security and labyrinthine bureaucracy, but it's just full of regular office workers."))
    msgbox(format("Action movies lied to me! Why can't reality be more like fiction?"))
	release
	end
}

script EverGrandeCity_Headquarters_1F_EventScript_LeftScientist {
	lock
	faceplayer
    goto_if_unset(FLAG_SYS_NATIONAL_DEX, GoCheckIn)
    msgbox(format("I can provide you with a variety of helpful information."))
    LeftMenu:
    msgbox(format("What would you like to know?"))
	dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Gym challenge info", "View level caps", "Unlimited candy mode", "Routes to each Gym", "Challenges info")
    switch(var(VAR_RESULT)) {
        case 0:
            goto(GymInfo)
        case 1:
            goto(CapsInfo)
        case 2:
            goto(CandyInfo)
        case 3:
            goto(RoutesInfo)
        case 4:
            goto(ChallengesInfo)
        default:
        goto(ScientistOut)
    }

    GymInfo:
    msgbox(format("In the original Pokémon Emerald, the sequence of Gyms was largely determined by the need to obtain various HMs to move around the Hoenn region."))
    msgbox(format("In Emerald Beryl, the ferry system has been expanded: Lilycove, Mossdeep, Sootopolis, Ever Grande, Slateport, Littleroot, Dewford, and Rustboro are all available destinations."))
    msgbox(format("This and other map changes makes it possible to challenge the Gyms in any order. Gym leaders may use different teams depending on which badge you are challenging them for, and enemy trainers' levels will scale based on your number of badges."))
    msgbox(format("Though levels will be scaled, beware of encountering trainers with fully evolved teams or competitive builds before you are ready!"))
    msgbox(format("Gym leaders give you a choice of HM after winning a new badge, further expanding your ability to move around the region."))
    msgbox(format("HMs may be used whenever you have the corresponding HM item in your Bag and a Pokémon in your party which could learn it."))
    goto(LeftMenu)

    CapsInfo:
    msgbox(format("0 badge level cap: 16 \n 1 badge level cap: 25"))
    msgbox(format("2 badge level cap: 34 \n 3 badge level cap: 43"))
    msgbox(format("4 badge level cap: 52 \n 5 badge level cap: 61"))
    msgbox(format("6 badge level cap: 70 \n 7 badge level cap: 79"))
    msgbox(format("8 badge level cap: 87 \n Elite 4 level cap: 90"))
    msgbox(format("Postgame level cap: 100"))
    goto(LeftMenu)

    CandyInfo:
    msgbox(format("The unlimited candy option removes all methods of gaining experience except an unlimited rare candy key item, which can level your Pokémon to your current cap."))
    msgbox(format("This may be especially desirable for players attempting nuzlockes or those who wish to remove grinding."))
    msgbox(format("However, if you want mid-battle level ups you'll have to manage your experience points the old-fashioned way!"))
    msgbox(format("What would you like to do?"))
    dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Enable infinite candies", "Disable infinite candies", "Return to previous menu")
    switch(var(VAR_RESULT)) {
        case 0:
            if(!flag(FLAG_INFINITE_CANDY)) {
                //giveitem(ITEM_INFINITE_CANDY)
                setflag(FLAG_INFINITE_CANDY)
            }
            msgbox(format("Unlimited candy enabled! Don't do too many drugs, kid!"))
        case 1:
            if(flag(FLAG_INFINITE_CANDY)) {
                //removeitem(ITEM_INFINITE_CANDY)
                clearflag(FLAG_INFINITE_CANDY)
            }
            msgbox(format("Unlimited candy disabled! Illegal drugs are subject to confiscation!"))
        case 2:
        default:
    }
    goto(LeftMenu)

    RoutesInfo:
    msgbox(format("I can tell you routes to any Gym! No HMs needed!"))
    dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Rustboro", "Dewford", "Mauville", "Lavaridge", "Petalburg", "Fortree", "Mossdeep", "Sootopolis")
    switch(var(VAR_RESULT)) {
        case 0:
            msgbox(format("To get to Rustboro, just take the ferry! Rustboro is a classic first badge to obtain."))
        case 1:
            msgbox(format("To get to Dewford, just take the ferry! Dewford is a solid choice for your first badge, as long as you aren't afraid of the dark!"))
        case 2:
            msgbox(format("To get to Mauville, take the ferry to Slateport and journey north through Route 110!"))
            msgbox(format("You could also pick up an Acro Bike in Lilycove and take the long way via Routes 121, 122, 123, and 118."))
        case 3:
            msgbox(format("To get to Lavaridge, take the ferry to Rustboro and journey east through Route 116 and then northeast through Forina!"))
            msgbox(format("For a slightly longer and more dangerous route, start from Slateport and head north on Route 110 to Mauville. Then find the entrance to the Valley of Steel on Route 117 and go north!"))
            msgbox(format("Real explorers head north from Rustboro through Route 115, Meteor Falls, and Route 114 to Fallarbor. Then west via Route 113, south on Route 111 until you can access the Cable Car station on Route 112, and finally south from Mt. Chimney through Jagged Pass!"))
        case 4:
            msgbox(format("To get to Petalburg, you can take the ferry to Littleroot, head north until you reach Oldale, and then go west through Route 102."))
            msgbox(format("Or take the ferry to Rustboro and head south through Route 104 and Petalburg Woods!"))
        case 5:
            msgbox(format("To get to Fortree, take the ferry to Lilycove and then journey west through Route 123 and north through Route 121."))
            msgbox(format("If you go to Mauville and get a Bike, you can cross the narrow bridge over Route 118 and take Route 119 north to Fortree."))
        case 6:
            msgbox(format("To get to Mossdeep, just take the ferry! I wouldn't recommend it for your first badge though, it's one of the more challenging Gyms."))
        case 7:
            msgbox(format("To get to Sootopolis, just take the ferry! We use flying Pokémon to get you inside the caldera."))
        default:
            goto(LeftMenu)
    }
    goto(RoutesInfo)

    ChallengesInfo:
    msgbox(format("My colleague handles this game's optional challenges. If that interests you, speak to him!"))
    goto(LeftMenu)

    GoCheckIn:
    msgbox(format("You have to check in at the main desk before I can help you with anything."))
    ScientistOut:
    msgbox(format("May the odds be ever in your favor."))
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    release
    end
}

script EverGrandeCity_Headquarters_1F_EventScript_RightScientist {
	lock
	faceplayer
    goto_if_unset(FLAG_SYS_NATIONAL_DEX, GoCheckIn)
	msgbox(format("I can tell you about Emerald Beryl's optional challenge settings."))

    RightMenu:
    msgbox(format("What would you like to hear more about?"))
	dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Optional challenge info", "HM Challenges info", "Ferry Challenge info", "Modify optional challenges", "Nuzlocke/Monotype?")
    switch(var(VAR_RESULT)) {
        case 0:
            msgbox(format("Optional challenges may be enabled or disabled freely prior to leaving Ever Grande City for the first time."))
            msgbox(format("Speak to me after becoming Champion and I will reward you for any challenges completed. Alternatively, return here prior to becoming Champion and I will allow you to forfeit your challenges if you turn out to be, as the phrase goes, a total scrub."))
            msgbox(format("While an optional challenge is active you will be forced into the Set battle style and item use in battle will be disabled."))
        case 1:
            msgbox(format("It is possible to reach any Gym in the Hoenn region without the use of any HMs. In the standard game, one HM of your choice is awarded each time you get a new badge."))
            msgbox(format("The Random HMs challenge gives you a random HM after each gym instead of allowing you to choose."))
            msgbox(format("This challenge is mostly to make the game more interesting, and the reward is a sense of pride and accomplishment."))
            msgbox(format("In the No HMs challenge no HMs will be rewarded by Gym Leaders. This severely restricts the areas you can fully explore and limits you from many useful items and encounters."))
            msgbox(format("You will have to acquire all 8 badges without the use of HMs, and then return here."))
            msgbox(format("Once you have 8 badges, I will teleport you to the beginning of Victory Road, which will have obstacles removed or circumvented so you can proceed without needing HMs."))
            msgbox(format("Speak to me after becoming Champion, and I will give you all 8 HMs and shiny 6IV versions of Turtwig, Chimchar, and Piplup."))
        case 2:
            msgbox(format("In the standard game, you may ride the ferry around the Hoenn region as much or as little as you like."))
            msgbox(format("In the Ferry Challenge you begin with just one ferry ride. Further ferry rides are only available if you have beaten another Gym since your last use."))
            msgbox(format("In addition, you must select either the Random HMs or No HMs challenge."))
            msgbox(format("Some ferry destinations may be removed to avoid potential softlocks, though I'm sure you could still find a way to screw things up."))
            msgbox(format("Speak to me after becoming Champion and I will unlock unlimited use of the ferry and additional rewards."))
            msgbox(format("Along with the HM Challenge rewards you will receive a shiny 6IV Imposter Ditto and other gifts which are secret for now."))
        case 3:
            msgbox(format("Please select your desired option."))
            dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "No challenges", "Activate Random HMs challenge", "Activate No HMs challenge", "Activate Ferry Challenge with Random HMs", "Activate Ferry Challenge with No HMs")
            switch(var(VAR_RESULT)) {
                case 0:
                    clearflag(FLAG_CHALLENGE_HM_RANDOM)
                    clearflag(FLAG_CHALLENGE_HM_NONE)
                    clearflag(FLAG_CHALLENGE_FERRY_LIMIT)
                    clearflag(FLAG_CHALLENGE_FERRY_TICKET)
                    msgbox(format("All optional challenges removed."))
                case 1:
                    setflag(FLAG_CHALLENGE_HM_RANDOM)
                    clearflag(FLAG_CHALLENGE_HM_NONE)
                    msgbox(format("You activated the Random HMs challenge."))
                case 2:
                    setflag(FLAG_CHALLENGE_HM_RANDOM)
                    setflag(FLAG_CHALLENGE_HM_NONE)
                    msgbox(format("You activated the No HMs challenge."))
                case 3:
                    setflag(FLAG_CHALLENGE_HM_RANDOM)
                    clearflag(FLAG_CHALLENGE_HM_NONE)
                    setflag(FLAG_CHALLENGE_FERRY_LIMIT)
                    setflag(FLAG_CHALLENGE_FERRY_TICKET)
                    msgbox(format("You activated the Ferry challenge with Random HMs."))
                case 4:
                    setflag(FLAG_CHALLENGE_HM_RANDOM)
                    setflag(FLAG_CHALLENGE_HM_NONE)
                    setflag(FLAG_CHALLENGE_FERRY_LIMIT)
                    setflag(FLAG_CHALLENGE_FERRY_TICKET)
                    msgbox(format("You activated the Ferry challenge with No HMs."))
                default:
                    msgbox(format("Your previous challenge settings will be retained."))
            }
        case 4:
            msgbox(format("Additional challenges such as monotype or nuzlocke runs are the responsibility of the player to manage."))
        default:
            goto(ScientistOut)
    }
    goto(RightMenu)
}

script EverGrandeCity_Headquarters_1F_EventScript_SecurityMan {
	lock
	faceplayer
    goto_if_set(FLAG_TEMP_1, OhNoAirPods)
    setflag(FLAG_TEMP_1)
	msgbox(format("I'm a security officer. Check out my badge! You should respect my authority, or else!"))
    release
    end

    OhNoAirPods:
    msgbox(format("Seriously, though, I just stand here and watch for people getting too rowdy. I'm extremely bored! They confiscated my airpods last week."))
	release
	end
}

script EverGrandeCity_Headquarters_1F_EventScript_GameBoyKid {
	lock
    goto_if_set(FLAG_TEMP_2, UsefulCode)
    setflag(FLAG_TEMP_2)
    msgbox(format("Never forget: \n {UP_ARROW} {UP_ARROW} {DOWN_ARROW} {DOWN_ARROW} {LEFT_ARROW} {RIGHT_ARROW} {LEFT_ARROW} {RIGHT_ARROW} B A start."))
    release
    end

    UsefulCode:
    waitmessage
    msgbox(format("Want another code?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        msgbox(format("Affirmative."))
        release
        end
    }
    random(9)
    switch(var(VAR_RESULT)) {
        case 0: 
            msgbox(format("Don't forget to call ur mom if you have encountered ShoalShock!"))
            msgbox(format("{UP_ARROW} {RIGHT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {DOWN_ARROW} {DOWN_ARROW} {LEFT_ARROW} {UP_ARROW} {RIGHT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {UP_ARROW} {LEFT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {UP_ARROW} {RIGHT_ARROW} {DOWN_ARROW}"))	
        case 1: 
            msgbox(format("You need help with this one? Total n00b. \n {DOWN_ARROW} {DOWN_ARROW} {RIGHT_ARROW} {UP_ARROW} {LEFT_ARROW} {UP_ARROW} {RIGHT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {DOWN_ARROW}"))
        case 2: 
            msgbox(format("How many Swordfish can 1d10t5 like you fit in one password file?"))
            msgbox(format("{UP_ARROW} {RIGHT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {UP_ARROW} {RIGHT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {UP_ARROW} {RIGHT_ARROW} {UP_ARROW} {RIGHT_ARROW}"))
        case 3: 
            msgbox(format("I goin in ur cave, taking ur icicles! \n {LEFT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {DOWN_ARROW} {RIGHT_ARROW} {DOWN_ARROW}"))
        case 4: 
            msgbox(format("Articuno can't Kanto deez nuts! \n {LEFT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {DOWN_ARROW} {RIGHT_ARROW} {UP_ARROW} {UP_ARROW} {LEFT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {UP_ARROW} {RIGHT_ARROW} {UP_ARROW}"))
        case 5: 
            msgbox(format("I hacked the Trick Master! \n {RIGHT_ARROW} {UP_ARROW} {RIGHT_ARROW} {UP_ARROW} {LEFT_ARROW} {DOWN_ARROW} {UP_ARROW} {LEFT_ARROW} {DOWN_ARROW} {RIGHT_ARROW} {DOWN_ARROW}"))
        case 6: 
            msgbox(format("In ur Trick House, lootin ur doorz! \n {UP_ARROW} {LEFT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {DOWN_ARROW} {LEFT_ARROW} {UP_ARROW} {RIGHT_ARROW} {DOWN_ARROW} {RIGHT_ARROW} {UP_ARROW}"))
        case 7:
            msgbox(format("Is that a Sky Pillar in your pants or are you just happy to see me?"))
            msgbox(format("{LEFT_ARROW} {UP_ARROW} {UP_ARROW} {UP_ARROW} {LEFT_ARROW} {UP_ARROW} {UP_ARROW} {UP_ARROW} {UP_ARROW} {RIGHT_ARROW} {RIGHT_ARROW} {RIGHT_ARROW} {UP_ARROW}"))
        default:
            msgbox(format("GAME OVER! Insert quarter to play again."))
            clearflag(FLAG_TEMP_2)
            release
            end
    }
    goto(UsefulCode)
}

script EverGrandeCity_Headquarters_1F_EventScript_ScientistCredits {
    lock
    faceplayer
    msgbox(format("Greetings! I can tell you about many of the resources and features Jinnora used while creating this game."))
    msgbox(format("You should also find this info in the README.txt or in the master forum posts about the game, along with links."))
    CreditsMenu:
    msgbox(format("Which would you like to hear about?"))
    dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "General resources used", "Features included")
    switch(var(VAR_RESULT)) {
        case 0:
            ResourcesMenu:
            msgbox(format("Which resource interests you?"))
            dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Team Aqua's Hideout", "Porymap", "Poryscript", "RHH Discord", "Pokeemerald Tutorials Master")
            switch(var(VAR_RESULT)) {
                case 0:
                    msgbox(format("Team Aqua's Hideout is a Youtube channel with many helpful guides on using the decompilation projects. These were an amazing resource throughout the project."))
                    goto(ResourcesMenu)
                case 1:
                    msgbox(format("Porymap is a software developed by huderlem for the decomp projects that allows easy editing of all the maps in the game. A lot of time was spent using this application!"))
                    goto(ResourcesMenu)
                case 2:
                    msgbox(format("Poryscript is a scripting language developed by huderlem, much easier to use than the game's default .inc syntax. Nearly all NPC scripts created for this game were written in Poryscript!"))
                    goto(ResourcesMenu)
                case 3:
                    msgbox(format("Rom Hacking Hideout is the community which created the pokeemerald-expansion repository this project is based on. You may have noticed their credits scene in the game intro. A big thanks to everyone who continues to work on the expansion project!"))
                    goto(ResourcesMenu)
                case 4:
                    msgbox(format("The tutorials master page located on GitHub at pret/pokeemerald/wiki/Tutorials was an invaluable resource for locating guides to many of the QOL changes that are incorporated into this game."))
                    goto(ResourcesMenu)
                default:
                    goto(CreditsMenu)
            }
        case 1:
            FeaturesMenu:
            msgbox(format("Features will be listed by order of inclusion into this project."))
            msgbox(format("Which kind of features would you like to hear about?"))
            dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Player quality of life", "Creator quality of life", "Bug fixes", "Game file knowledge/minor tweaks")
            switch(var(VAR_RESULT)) {
                case 0:
                    msgbox(format("Nature stat colors, by DizzyEgg."))
                    msgbox(format("Bag sorting, by ghoulslash."))
                    msgbox(format("IV/EV summary screen, by BuffelSaft."))
                    msgbox(format("Increased bag size, by MapleFall."))
                    msgbox(format("Fast Berry interactions, by dunsparce9."))
                    msgbox(format("Berry trees remain until picked, by BuffelSaft."))
                    msgbox(format("Mart items by badge count, by ghoulslash."))
                    msgbox(format("Swap Poké Balls via bag use, by Lunos."))
                    msgbox(format("Match call only for rematch, by devolov."))
                    msgbox(format("Nickname Pokémon from party menu, by Shinny456, TheXaman, Lunos and Zadien."))
                    msgbox(format("Use HMs as long as a party Pokémon could learn them, by devolov and ScyrousFX."))
                    msgbox(format("Relearn moves from party menu, by Diego Mertens."))
                    goto(FeaturesMenu)
                case 1:
                    msgbox(format("Generic item ball script, by Deokishisu."))
                    msgbox(format("Dynamic multichoice, by SBird."))
                    msgbox(format("Decapitalization, by AsparagusEduardo."))
                    msgbox(format("Emerald textbox simulator, by Meejle."))
                    goto(FeaturesMenu)
                case 2:
                    msgbox(format("Disable unnecessary Union Room check, from the tutorial master list."))
                    msgbox(format("Surf dismount ground effects, from the tutorial master list."))
                    msgbox(format("Poison Heal protects from overworld poison, by Lunos."))
                    goto(FeaturesMenu)
                case 3:
                    msgbox(format("Lanette's PC from game start, by devolov."))
                    msgbox(format("Change Birch's intro Pokémon, by Avara."))
                    msgbox(format("Modify Pickup items, by Lunos."))
                    msgbox(format("Change default text speed, by Avara."))
                    msgbox(format("Allow forgetting of HM moves, by Lunos, thespbgamer, and Jaizu."))
                    goto(FeaturesMenu)
                default:
                    goto(CreditsMenu)
            }
        default:
    }
    msgbox(format("Be seeing you!"))
    release
    end
}