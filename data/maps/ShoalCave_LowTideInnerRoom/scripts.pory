mapscripts ShoalCave_LowTideInnerRoom_MapScripts {
	MAP_SCRIPT_ON_LOAD : ShoalCave_LowTideInnerRoom_OnLoad
	MAP_SCRIPT_ON_TRANSITION : ShoalCave_LowTideInnerRoom_OnTransition
}

script ShoalCave_LowTideInnerRoom_OnTransition {
	goto_if_set(FLAG_SYS_SHOAL_TIDE, ShoalCave_LowTideInnerRoom_EventScript_SetHighTide)
	goto(ShoalCave_LowTideInnerRoom_EventScript_SetLowTide)
}


script ShoalCave_LowTideInnerRoom_EventScript_SetHighTide {
	setmaplayoutindex(LAYOUT_SHOAL_CAVE_HIGH_TIDE_INNER_ROOM)
	if(flag(FLAG_SHOAL_CAVE_SPHEAL_TRADE_COMPLETED)) {
		setflag(FLAG_TEMP_11)
	}
	end
}

script ShoalCave_LowTideInnerRoom_EventScript_SetLowTide {
	setmaplayoutindex(LAYOUT_SHOAL_CAVE_LOW_TIDE_INNER_ROOM)
	setflag(FLAG_TEMP_11)
	end
}

script ShoalCave_LowTideInnerRoom_OnLoad {
	call(ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles)
	end
}

script ShoalCave_LowTideInnerRoom_EventScript_SphealTrader {
	lock
	faceplayer
	goto_if_set(FLAG_SHOAL_CAVE_SPHEAL_TRADE_COMPLETED, SphealTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_SPHEAL)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Do you know about Spheal Team Six? I helped train them!"))
	msgbox(format("I'm getting a bit stir crazy, and I think I'd like to explore the rest of Hoenn."))
	msgbox(format("Do you have a Vibrava? I'll trade you one of my beloved Spheal."), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineSphealTrade)
	msgbox(format("Cool, thanks!"))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineSphealTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotVibrava)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Please cherish Nelson! He's got a lot of heart."))
	setflag(FLAG_SHOAL_CAVE_SPHEAL_TRADE_COMPLETED)
	release
	end

	DeclineSphealTrade:
	msgbox(format("If you won't trade, I suppose I'll have to figure something else out."))
	release
	end

	SelectedNotVibrava:
	bufferspeciesname(STR_VAR_1, VAR_0x800B)
	msgbox(format("{STR_VAR_1}? No, I've got my heart set on a Vibrava I can evolve to Flygon myself."))
	release
	end

	SphealTradeCompleted:
	msgbox(format("I'm going to evolve Vibrava and have Flygon take me wherever I decide to go."))
	release
	end
}

raw `
ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles::
	goto_if_set FLAG_RECEIVED_SHOAL_SALT_1, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles2
	goto_if_set FLAG_SYS_SHOAL_TIDE, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles2
	setmetatile 31, 8, METATILE_Cave_ShoalCave_DirtPile_Large, TRUE
	goto ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles2
	end

ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles2::
	goto_if_set FLAG_RECEIVED_SHOAL_SALT_2, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles3
	goto_if_set FLAG_SYS_SHOAL_TIDE, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles3
	setmetatile 14, 26, METATILE_Cave_ShoalCave_DirtPile_Large, TRUE
	goto ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles3
	end

ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles3::
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_1, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles4
	setmetatile 41, 20, METATILE_Cave_ShoalCave_BlueStone_Large, TRUE
	goto ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles4
	end

ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles4::
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_2, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles5
	setmetatile 41, 10, METATILE_Cave_ShoalCave_BlueStone_Large, TRUE
	goto ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles5
	end

ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles5::
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_3, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles6
	setmetatile 6, 9, METATILE_Cave_ShoalCave_BlueStone_Large, TRUE
	goto ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles6
	end

ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatiles6::
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_4, ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatilesEnd
	setmetatile 16, 13, METATILE_Cave_ShoalCave_BlueStone_Large, TRUE
	return

ShoalCave_LowTideInnerRoom_EventScript_SetShoalItemMetatilesEnd::
	return

ShoalCave_LowTideInnerRoom_EventScript_ShoalShell1::
	lockall
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_1, ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalShell
	giveitem ITEM_SHOAL_SHELL
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setmetatile 41, 20, METATILE_Cave_ShoalCave_BlueStone_Small, FALSE
	special DrawWholeMapView
	setflag FLAG_RECEIVED_SHOAL_SHELL_1
	releaseall
	end

ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalShell::
	msgbox ShoalCave_Text_WasShoallShellNowNothing, MSGBOX_DEFAULT
	releaseall
	end

ShoalCave_LowTideInnerRoom_EventScript_ShoalShell2::
	lockall
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_2, ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalShell
	giveitem ITEM_SHOAL_SHELL
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setmetatile 41, 10, METATILE_Cave_ShoalCave_BlueStone_Small, FALSE
	special DrawWholeMapView
	setflag FLAG_RECEIVED_SHOAL_SHELL_2
	releaseall
	end

ShoalCave_LowTideInnerRoom_EventScript_ShoalShell3::
	lockall
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_3, ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalShell
	giveitem ITEM_SHOAL_SHELL
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setmetatile 6, 9, METATILE_Cave_ShoalCave_BlueStone_Small, FALSE
	special DrawWholeMapView
	setflag FLAG_RECEIVED_SHOAL_SHELL_3
	releaseall
	end

ShoalCave_LowTideInnerRoom_EventScript_ShoalShell4::
	lockall
	goto_if_set FLAG_RECEIVED_SHOAL_SHELL_4, ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalShell
	giveitem ITEM_SHOAL_SHELL
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setmetatile 16, 13, METATILE_Cave_ShoalCave_BlueStone_Small, FALSE
	special DrawWholeMapView
	setflag FLAG_RECEIVED_SHOAL_SHELL_4
	releaseall
	end

ShoalCave_LowTideInnerRoom_EventScript_ShoalSalt1::
	lockall
	goto_if_set FLAG_RECEIVED_SHOAL_SALT_1, ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalSalt
	giveitem ITEM_SHOAL_SALT
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setmetatile 31, 8, METATILE_Cave_ShoalCave_DirtPile_Small, FALSE
	special DrawWholeMapView
	setflag FLAG_RECEIVED_SHOAL_SALT_1
	releaseall
	end

ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalSalt::
	msgbox ShoalCave_Text_WasShoalSaltNowNothing, MSGBOX_DEFAULT
	releaseall
	end

ShoalCave_LowTideInnerRoom_EventScript_ShoalSalt2::
	lockall
	goto_if_set FLAG_RECEIVED_SHOAL_SALT_2, ShoalCave_LowTideInnerRoom_EventScript_ReceivedShoalSalt
	giveitem ITEM_SHOAL_SALT
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setmetatile 14, 26, METATILE_Cave_ShoalCave_DirtPile_Small, FALSE
	special DrawWholeMapView
	setflag FLAG_RECEIVED_SHOAL_SALT_2
	releaseall
	end
`
