mapscripts MossdeepCity_House5_MapScripts {}

script MossdeepCity_House5_EventScript_Mindy {
    lock
	faceplayer
	goto_if_set(FLAG_MOSSDEEP_HAUNTER_TRADE_COMPLETED, HaunterTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_HAUNTER)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Hi, I'm Mindy. You may know of my identical cousin in Snowpoint City."))
	msgbox(format("Want to trade a Medicham for my Haunter?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineHaunterTrade)
	msgbox(format("My cousin named her Haunter Gaspar, but I gave mine a different name."))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineHaunterTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotMedicham)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Haha, did my Haunter evolve? I made sure not to give him an Everstone."))
	setflag(FLAG_MOSSDEEP_HAUNTER_TRADE_COMPLETED)
	release
	end

	DeclineHaunterTrade:
	msgbox(format("Not sure that's the right choice, but suit yourself."))
	release
	end

	SelectedNotMedicham:
	bufferspeciesname(STR_VAR_1, VAR_0x8009)
	msgbox(format("{STR_VAR_1} isn't a Medicham."))
	release
	end

	HaunterTradeCompleted:
	msgbox(format("I hope you continue to have good karma!"))
	release
	end
}