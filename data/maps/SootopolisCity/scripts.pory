mapscripts SootopolisCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : SootopolisCity_OnTransition
	MAP_SCRIPT_ON_RESUME : SootopolisCity_OnResume
}

script SootopolisCity_OnTransition {
	setflag(FLAG_VISITED_SOOTOPOLIS_CITY)
	end
}

script SootopolisCity_OnResume {
	setdivewarp(MAP_UNDERWATER_SOOTOPOLIS_CITY, 9, 6)
	end
}

script SootopolisCity_EventScript_Kiri {
	lock
	faceplayer
	goto_if_unset(FLAG_DAILY_SOOTOPOLIS_RECEIVED_BERRY, SootopolisCity_EventScript_KiriGiveBerry)

	msgbox(SootopolisCity_Text_LikeSeasonBornIn, MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES) {
		msgbox(SootopolisCity_Text_ThenILoveAutumn)
		release
		end
	}
	msgbox(SootopolisCity_Text_OhDoesntMatter, MSGBOX_DEFAULT)
	release
	end
}

script SootopolisCity_EventScript_KiriGiveBerry {
	dotimebasedevents
	special(GetPlayerBigGuyGirlString)

	msgbox(SootopolisCity_Text_NameIsKiriHaveOneOfThese)
	random(NUM_KIRI_BERRIES)
	addvar(VAR_RESULT, NUM_KIRI_BERRIES_SKIPPED)
	addvar(VAR_RESULT, FIRST_BERRY_INDEX)
	giveitem(VAR_RESULT)

	msgbox(SootopolisCity_Text_GiveYouThisBerryToo)
	random(5)
	switch(var(VAR_RESULT)) {
		case 0:
			giveitem(ITEM_FIGY_BERRY)
		case 1:
			giveitem(ITEM_WIKI_BERRY)
		case 2:
			giveitem(ITEM_MAGO_BERRY)
		case 3:
			giveitem(ITEM_AGUAV_BERRY)
		default:
			giveitem(ITEM_IAPAPA_BERRY)
	}

	msgbox(SootopolisCity_Text_WhatKindOfWishInYourName)
	setflag(FLAG_DAILY_SOOTOPOLIS_RECEIVED_BERRY)
	release
	end
}

script SootopolisCity_EventScript_NinjaBoy {
	lock
	faceplayer
	msgbox(SootopolisCity_Text_WonderWhatWorldIsLike)
	release
	end
}

script SootopolisCity_EventScript_Boy1 {
	lock
	faceplayer
	msgbox(SootopolisCity_Text_PhysicallyFitLivingHere)
	release
	end
}

script SootopolisCity_EventScript_Man {
	lock
	faceplayer
	msgbox(SootopolisCity_Text_NoOrdinaryTourist, MSGBOX_DEFAULT)
	release
	end
}

script SootopolisCity_EventScript_Woman1 {
	lock
	faceplayer
	goto_if_set(FLAG_TEMP_1, NightSky)
	msgbox(SootopolisCity_Text_SootopolisSkyBeautiful)
	setflag(FLAG_TEMP_1)
	release
	end

	NightSky:
	msgbox(SootopolisCity_Text_NightSkyFavoriteScenery)
	clearflag(FLAG_TEMP_1)
	release
	end
}

script SootopolisCity_EventScript_Ferry {
	lock
	faceplayer
	msgbox(format("Yo, {PLAYER}! The ferry can't reach inside the caldera, but we can take you by Flying Pokémon!"))
	msgbox(format("Where would you like to go?"))
	dynmultichoice(0,0,FALSE,6,0,DYN_MULTICHOICE_CB_NONE, "Rustboro", "Dewford", "Littleroot", "Slateport", "Lilycove", "Mossdeep", "Ever Grande")
	switch(var(VAR_RESULT)) {
		case 0:
			warp(MAP_RUSTBORO_CITY, -1, 11, 26)
		case 1:
			warp(MAP_DEWFORD_RESORT, -1, 6, 16)
		case 2:
			warp(MAP_LITTLEROOT_SHORE, -1, 26, 21)
		case 3:
			warp(MAP_SLATEPORT_CITY, -1, 11, 26)
		case 4:
			warp(MAP_LILYCOVE_CITY, -1, 11, 35)
		case 5:
			warp(MAP_MOSSDEEP_CITY, -1, 87, 36)
		case 6:
			warp(MAP_EVER_GRANDE_CITY, -1, 24, 30)
		default:
			msgbox(format("See you later, then!"))
	}
	release
	end
}

script SootopolisCity_EventScript_SchoolKidM {
	lock
	faceplayer
	msgbox(format("I want to be just like Juan when I grow up!"))
	msgbox(format("A powerful Trainer, ruling a gym filled with gorgeous underlings…"))
	msgbox(format("Pity I have no charisma or social skills."))
	release
	end
}

script SootopolisCity_EventScript_Woman2 {
	lock
	faceplayer
	msgbox(format("It's so peaceful here."))
	msgbox(format("And yet, when this volcano was active we would be standing within the magma chamber, waiting for an explosive eruption."))
	msgbox(format("I guess having peace today doesn't mean there was peace yesterday, or will be peace tomorrow."))
	release
	end
}

script SootopolisCity_EventScript_GymSign {
	msgbox(SootopolisCity_Text_GymSign, MSGBOX_SIGN)
}

script SootopolisCity_EventScript_CitySign {
	msgbox(SootopolisCity_Text_CitySign, MSGBOX_SIGN)
}

script EventScript_ClosedSootopolisDoor {
	msgbox(SootopolisCity_Text_DoorIsClosed, MSGBOX_SIGN)
}

script SootopolisCity_EventScript_ConnectorSign {
	msgbox("Sootopolis Connector\n Access to Route 126", MSGBOX_SIGN)
}
raw `
SootopolisCity_Text_GymSign:
	.string "Sootopolis City Pokémon Gym\n"
	.string "Leader: Juan\p"
	.string "“The Gym Leader with the beauty\n"
	.string "of pure water!”$"

SootopolisCity_Text_CitySign:
	.string "Sootopolis City\p"
	.string "“The mystical city where history\n"
	.string "slumbers.”$"

SootopolisCity_Text_DoorIsClosed:
	.string "The door is closed.$"

SootopolisCity_Text_PhysicallyFitLivingHere:
	.string "Diving in the sea. Climbing up and\n"
	.string "down stairs all the time…\p"
	.string "If you live in this town, you end up\n"
	.string "getting physically fit.$"

SootopolisCity_Text_WonderWhatWorldIsLike:
	.string "I… I've never been out of this city.\p"
	.string "I wonder what the world is like on\n"
	.string "the other side of this round sky?$"

SootopolisCity_Text_SeeingLegendWithOwnEyes:
	.string "There's an ancient legend that claims\n"
	.string "the land and sea were shaped by\l"
	.string "a colossal battle between Pokémon.\p"
	.string "Well, I'm seeing that happen with\n"
	.string "my very own eyes!\p"
	.string "Whoa! I never expected to be\n"
	.string "witness to something this huge!$"

SootopolisCity_Text_SawLegendWithOwnEyes:
	.string "There's an ancient legend that claims\n"
	.string "the land and sea were shaped by\l"
	.string "a colossal battle between Pokémon.\p"
	.string "Well, I saw that happen with\n"
	.string "my very own eyes!\p"
	.string "Whoa! I never expected to be\n"
	.string "witness to something this huge!$"

SootopolisCity_Text_NoOrdinaryTourist:
	.string "Hm!\n"
	.string "You've come all the way to Sootopolis?\l"
	.string "You're no ordinary tourist.\p"
	.string "But I suppose that doesn't make you\n"
	.string "an extraordinary tourist, either.$"

SootopolisCity_Text_CityRegainedCalm:
	.string "The city has regained its calm…$"

SootopolisCity_Text_CaveOfOriginPleaseLeave:
	.string "Who might you be?\p"
	.string "This is the Cave of Origin.\p"
	.string "The spirits of Pokémon, becalmed at\n"
	.string "Mt. Pyre, are said to be revived here.\p"
	.string "Please leave.$"

SootopolisCity_Text_LeadSuperiorTrainerToCave:
	.string "A person with a strong will and\n"
	.string "superior talent…\p"
	.string "A Trainer who has knowledge and\n"
	.string "experience of many kinds of Pokémon…\p"
	.string "If such a person were to appear, I was\n"
	.string "instructed by Wallace to lead that\l"
	.string "Trainer to this Cave.$"

SootopolisCity_Text_CaveOfOriginSleepsToo:
	.string "This is the Cave of Origin…\p"
	.string "With the passing of the crisis,\n"
	.string "the cave, too, shall sleep…$"

SootopolisCity_Text_SootopolisSkyBeautiful:
	.string "Sootopolis sprang up as a town in\n"
	.string "the crater of a volcano.\p"
	.string "If you look up at the sky, the lip of\n"
	.string "the crater is also visible.\l"
	.string "So, all you see is the sky in a circle.\p"
	.string "But that's what makes the sky above\n"
	.string "Sootopolis the most beautiful.$"

SootopolisCity_Text_NightSkyFavoriteScenery:
	.string "A circle of a night sky framed by\n"
	.string "the crater of a volcano…\p"
	.string "And in that ring, stars flicker and\n"
	.string "blink as if they were alive…\l"
	.string "It's my favorite scenery.$"
`