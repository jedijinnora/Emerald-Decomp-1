const LOCALID_ANABEL = 1

mapscripts EverGrandeCity_ChampionsRoom_MapScripts {
	MAP_SCRIPT_ON_TRANSITION : EverGrandeCity_ChampionsRoom_OnTransition
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
		VAR_TEMP_1, 0: EverGrandeCity_ChampionsRoom_EventScript_PlayerTurnNorth
	]
	MAP_SCRIPT_ON_FRAME_TABLE[
		VAR_TEMP_1, 0: EverGrandeCity_ChampionsRoom_EventScript_EnterRoom
	]
}

script EverGrandeCity_ChampionsRoom_OnTransition {
	setflag(FLAG_ENTERED_CHAMPION_ROOM)
	setflag(FLAG_USE_NEXT_LEVEL_CAP)
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_PlayerTurnNorth {
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_EnterRoom{
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp4)
	waitmovement(0)
	delay(40)
	applymovement(OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerApproachAnabel)
	waitmovement(0)
	setvar(VAR_TEMP_1, 1)
	goto(EverGrandeCity_ChampionsRoom_EventScript_Anabel)
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_SetShayminTeam {
	goto_if_set(FLAG_RECEIVED_GRACIDEA, Common_EventScript_NopReturn)
	setvar(VAR_TEMP_2, 3)
	return
}

script EverGrandeCity_ChampionsRoom_EventScript_Anabel {
	playbgm(MUS_ENCOUNTER_CHAMPION, FALSE)
	msgbox(EverGrandeCity_ChampionsRoom_Text_IntroSpeech, MSGBOX_DEFAULT)
	random(4)
	copyvar(VAR_TEMP_2, VAR_RESULT)
	call_if_eq(VAR_DIFFICULTY_SETTING, 1, EverGrandeCity_ChampionsRoom_EventScript_SetShayminTeam)
	setvar(VAR_0x8004, SPECIES_SHAYMIN)
	specialvar(VAR_RESULT, CheckPartyForMon)
	call_if_eq(VAR_RESULT, TRUE, EverGrandeCity_ChampionsRoom_EventScript_SetShayminTeam)
	addvar(VAR_TEMP_2, 1)
	switch(var(VAR_TEMP_2)) {
		case 1:
			trainerbattle_no_intro(TRAINER_ANABEL_1, EverGrandeCity_ChampionsRoom_Text_Defeat)
		case 2:
			trainerbattle_no_intro(TRAINER_ANABEL_2, EverGrandeCity_ChampionsRoom_Text_Defeat)
		case 3:
			trainerbattle_no_intro(TRAINER_ANABEL_3, EverGrandeCity_ChampionsRoom_Text_Defeat)
		default:
			trainerbattle_no_intro(TRAINER_ANABEL_4, EverGrandeCity_ChampionsRoom_Text_Defeat)
	}

	EverGrandeCity_ChampionsRoom_EventScript_ClearLegendaryFlags:
	switch(var(VAR_TEMP_2)) {
		case 1:
			clearflag(FLAG_DEFEATED_RAIKOU)
			clearflag(FLAG_DEFEATED_ENTEI)
			clearflag(FLAG_DEFEATED_SUICUNE)
		case 2:
			clearflag(FLAG_DEFEATED_LATIAS_OR_LATIOS)
		case 3:
			clearflag(FLAG_DEFEATED_MANAPHY)
		case 4:
			clearflag(FLAG_DEFEATED_SHAYMIN)
	}

	EverGrandeCity_ChampionsRoom_EventScript_Defeated:
	playse(SE_DOOR)
	clearflag(FLAG_USE_NEXT_LEVEL_CAP)
	setmetatile(6, 1, METATILE_EliteFour_OpenDoorChampion_Frame, FALSE)
	setmetatile(6, 2, METATILE_EliteFour_OpenDoorChampion_Opening, FALSE)
	special(DrawWholeMapView)
	msgbox(EverGrandeCity_ChampionsRoom_Text_PostBattleSpeech, MSGBOX_DEFAULT)
	closemessage
	setvar(VAR_0x8004, SPECIES_SHAYMIN)
	specialvar(VAR_RESULT, CheckPartyForMon)
	call_if_eq(VAR_RESULT, TRUE, EverGrandeCity_ChampionsRoom_EventScript_TryGiveGracidea)
	applymovement(LOCALID_ANABEL, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)
	delay(20)
	applymovement(LOCALID_ANABEL, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	msgbox(EverGrandeCity_ChampionsRoom_Text_AnabelComeWithMe, MSGBOX_DEFAULT)
	closemessage
	delay(30)
	applymovement(LOCALID_ANABEL, EverGrandeCity_ChampionsRoom_Movement_AnabelExitStart)
	applymovement(OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerExitStart)
	waitmovement(0)
	applymovement(LOCALID_ANABEL, EverGrandeCity_ChampionsRoom_Movement_AnabelExit)
	applymovement(OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerExit)
	waitmovement(0)
	warp(MAP_EVER_GRANDE_CITY_HALL_OF_FAME, 7, 16)
	waitstate
	releaseall
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_TryGiveGracidea {
	goto_if_set(FLAG_RECEIVED_GRACIDEA, Common_EventScript_NopReturn)
	msgbox(format("Anabel: And you brought a Shaymin of your own, too!"))
	msgbox(format("Here, you should have this!"))
	giveitem(ITEM_GRACIDEA)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_NopReturn)
	setflag(FLAG_RECEIVED_GRACIDEA)
	return
}


movement EverGrandeCity_ChampionsRoom_Movement_PlayerApproachAnabel {
	walk_up
	walk_up
	step_end
}
movement EverGrandeCity_ChampionsRoom_Movement_PlayerExitStart {
	walk_up
	walk_up
	step_end
}
movement EverGrandeCity_ChampionsRoom_Movement_PlayerExit {
	walk_up
	delay_16
	walk_up
	delay_8
	set_invisible
	step_end
}
movement EverGrandeCity_ChampionsRoom_Movement_AnabelExitStart {
	walk_up
	walk_up
	walk_in_place_faster_down
	step_end
}
movement EverGrandeCity_ChampionsRoom_Movement_AnabelExit {
	walk_up
	delay_8
	set_invisible
	step_end
}

raw `
EverGrandeCity_ChampionsRoom_Text_IntroSpeech:
	.string "Anabel: Welcome, {PLAYER}.\p"
	.string "We Trainers raise Pokémon by giving\n"
	.string "them items and by teaching them new\l"
	.string "techniques for battle.\p"
	.string "But we ourselves also learn many\n"
	.string "things from Pokémon.\p"
	.string "And this is where your achievements\n"
	.string "are put to the test against what\l"
	.string "others like you have achieved.\p"
	.string "Now!\p"
	.string "Who can most elegantly dance with\n"
	.string "their Pokémon in Hoenn?\p"
	.string "Show me right here and now!$"

EverGrandeCity_ChampionsRoom_Text_Defeat:
	.string "I, the Champion, fall in defeat…\p"
	.string "That was wonderful work.\n"
	.string "You were elegant, infuriatingly so.\l"
	.string "And yet it was utterly glorious!\p"
	.string "Kudos to you, {PLAYER}!\p"
	.string "You are a truly noble Pokémon\n"
	.string "Trainer!$"

EverGrandeCity_ChampionsRoom_Text_PostBattleSpeech:
	.string "Anabel: The Pokémon you sent into\n"
	.string "battle…\p"
	.string "At times they danced like a spring\n"
	.string "breeze, and at times they struck\l"
	.string "like lightning.\p"
	.string "It was with light, yet surefooted,\n"
	.string "elegance that you led your Pokémon.\p"
	.string "Even I felt bedazzled by your\n"
	.string "masterful performance!\p"
	.string "You now stand at the glorious peak\n"
	.string "of the Pokémon League.\p"
	.string "I now proclaim you to be the new\n"
	.string "Hoenn region Champion!$"

EverGrandeCity_ChampionsRoom_Text_AnabelComeWithMe:
	.string "Anabel: {PLAYER}…\p"
	.string "No, let me rephrase that properly.\p"
	.string "The new Champion!\p"
	.string "Come with me.$"
`