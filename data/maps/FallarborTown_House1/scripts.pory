mapscripts FallarborTown_House1_MapScripts {}

script FallarborTown_House1_EventScript_DurantTrader {
    lock
	faceplayer
	goto_if_set(FLAG_FALLARBOR_DURANT_TRADE_COMPLETED, DurantTradeCompleted)
	setvar(VAR_0x8008, INGAME_TRADE_DURANT)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("The most powerful Pokémon of all…"))
    msgbox(format("The god, the GOAT, the unstoppable monster…"))
	msgbox(format("I NEED WOOPER! I'll trade you a Durant."), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, DeclineDurantTrade)
	msgbox(format("Woop woop will be mine!"))
	waitmessage
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, DeclineDurantTrade)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SelectedNotWooper)
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Don't worry, I'll never make the mistake of evolving Wooper."))
	setflag(FLAG_FALLARBOR_DURANT_TRADE_COMPLETED)
	release
	end

	DeclineDurantTrade:
	msgbox(format("Fine, I guess I'll let you come back later."))
	release
	end

	SelectedNotWooper:
	bufferspeciesname(STR_VAR_1, VAR_0x800B)
	msgbox(format("Useless! Pathetic! What garbage compared to a proper Wooper!"))
	release
	end

	DurantTradeCompleted:
	msgbox(format("I'm going to take Woop Woop and sweep the world, you'll see."))
	release
	end
}

script FallarborTown_House1_EventScript_Shelf {
    msgbox(format("There are a lot of trophies for records in… pencil sharpening? Really?"), MSGBOX_SIGN)
    end
}
